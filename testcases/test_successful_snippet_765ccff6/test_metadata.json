{
  "cobol_file": {
    "file_name": "successful_snippet_765ccff6.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AOC-2020-16-1.\n       AUTHOR ANNA KOSIERADZKA.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT INPUTFILE ASSIGN TO \"d16.input\"\n           ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n\n       FILE SECTION.\n         FD INPUTFILE\n         RECORD IS VARYING IN SIZE FROM 1 to 99\n         DEPENDING ON REC-LEN.\n         01 INPUTRECORD PIC X(99).\n         \n       WORKING-STORAGE SECTION.\n         01 WS-RESULT PIC 9(9) VALUE 0.\n         \n         01 REC-LEN PIC 9(2) COMP.\n         01 FILE-STATUS PIC 9 VALUE 0.\n         01 WS-SECTION PIC 9 VALUE 1.\n         \n         01 WS-MIN-1 PIC 9(3) OCCURS 20 TIMES.\n         01 WS-MAX-1 PIC 9(3) OCCURS 20 TIMES.\n         01 WS-MIN-2 PIC 9(3) OCCURS 20 TIMES.\n         01 WS-MAX-2 PIC 9(3) OCCURS 20 TIMES.\n         01 WS-TMP1 PIC X(32).\n         01 WS-TMP2 PIC X(32).\n         01 WS-TMP3 PIC X(32).\n         01 WS-TMP4 PIC X(32).\n         01 WS-TMP5 PIC X(32).\n         01 WS-TMP6 PIC X(32).\n         01 WS-ROW PIC 9(3) OCCURS 20 TIMES.\n         01 WS-VAL PIC 9(3).\n         01 WS-VAL-CORRECT PIC 9.\n\n       LOCAL-STORAGE SECTION.\n         01 I UNSIGNED-INT VALUE 0.\n         01 J UNSIGNED-INT VALUE 0.\n         01 K UNSIGNED-INT VALUE 0.\n         01 N UNSIGNED-INT VALUE 0.  \n         01 STRING-PTR UNSIGNED-INT VALUE 1.\n\n       PROCEDURE DIVISION.\n       001-MAIN.\n           OPEN INPUT INPUTFILE.\n           PERFORM 002-READ UNTIL FILE-STATUS = 1.\n           CLOSE INPUTFILE.\n           DISPLAY WS-RESULT.\n           STOP RUN.\n\n       002-READ.\n            READ INPUTFILE\n                AT END MOVE 1 TO FILE-STATUS\n                NOT AT END PERFORM 003-PROCESS-RECORD\n            END-READ.\n\n        003-PROCESS-RECORD.\n            IF REC-LEN = 0 THEN\n                ADD 1 TO WS-SECTION\n            ELSE            \n                IF WS-SECTION = 1 THEN\n                    PERFORM 004-PROCESS-RECORD-TYPE-1\n                ELSE\n                    IF WS-SECTION = 2 THEN \n                        PERFORM 005-PROCESS-RECORD-TYPE-2\n                    ELSE\n                        PERFORM 006-PROCESS-RECORD-TYPE-3\n                    END-IF\n                END-IF\n            END-IF.\n\n        004-PROCESS-RECORD-TYPE-1.\n      * \"arrival platform: 46-644 or 659-970\"\n           UNSTRING INPUTRECORD DELIMITED BY \"-\" OR \":\" OR \" or \" INTO \n               WS-TMP1\n               WS-TMP2\n               WS-TMP3\n               WS-TMP4\n               WS-TMP5\n               WS-TMP6.\n            ADD 1 TO N.\n            MOVE WS-TMP2 TO WS-MIN-1(N).\n            MOVE WS-TMP3 TO WS-MAX-1(N).\n            MOVE WS-TMP4 TO WS-MIN-2(N).\n            MOVE WS-TMP5 TO WS-MAX-2(N).\n\n        005-PROCESS-RECORD-TYPE-2.\n      *     DISPLAY 'Type 2 record: ' INPUTRECORD.\n            \n        006-PROCESS-RECORD-TYPE-3.\n           ADD 1 TO I.\n           IF I = 1 THEN \n             EXIT PARAGRAPH\n           END-IF.          \n           MOVE 1 TO STRING-PTR.\n           PERFORM VARYING J FROM 1 BY 1 UNTIL J > N\n             UNSTRING INPUTRECORD DELIMITED BY ',' INTO WS-ROW(J)\n             WITH POINTER STRING-PTR\n           END-PERFORM.\n           PERFORM VARYING J FROM 1 BY 1 UNTIL J > N\n             MOVE WS-ROW(J) TO WS-VAL\n             PERFORM 007-CHECK-VAL\n           END-PERFORM.\n\n        007-CHECK-VAL.\n            MOVE 0 TO WS-VAL-CORRECT.\n            PERFORM VARYING K FROM 1 BY 1 UNTIL K > N\n                IF WS-VAL >= WS-MIN-1(K) AND WS-VAL <= WS-MAX-1(K) OR\n                  WS-VAL >= WS-MIN-2(K) AND WS-VAL <= WS-MAX-2(K) THEN\n                    MOVE 1 TO WS-VAL-CORRECT\n                END-IF\n            END-PERFORM.\n            IF WS-VAL-CORRECT = 0 THEN\n                ADD WS-VAL TO WS-RESULT\n            END-IF.\n"
  },
  "input_files": [
    {
      "file_name": "d16.input",
      "content": "arrival location: 25-873 or 895-957\ndeparture station: 39-660 or 672-954\ndeparture platform: 36-330 or 341-973\ndeparture track: 26-921 or 934-952\ndeparture date: 30-626 or 651-960\ndeparture time: 48-793 or 818-949\narrival station: 44-535 or 557-950\narrival platform: 46-644 or 659-970\narrival track: 37-338 or 353-963\nclass: 29-544 or 550-961\nduration: 37-727 or 746-966\nprice: 42-913 or 939-952\nroute: 49-603 or 626-965\nrow: 40-222 or 231-955\nseat: 36-64 or 89-974\ntrain: 50-721 or 741-962\ntype: 26-55 or 80-960\nwagon: 43-709 or 732-967\nzone: 25-418 or 441-965\n\nyour ticket:\n139,109,61,149,101,89,103,127,151,131,97,53,137,59,113,157,107,167,163,173\n\nnearby tickets:\n680,418,202,55,792,800,896,801,312,252,721,702,24,112,608,837,98,262,175,754\n554,374,586,530,329,868,152,814,896,330,153,117,754,437,758,52,474,858,100,512\n329,616,789,98,61,763,825,119,202,98,100,691,379,758,325,202,943,326,691,445\n765,691,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759\n759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759\n759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759\n759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759\n759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759,759"
    }
  ],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}