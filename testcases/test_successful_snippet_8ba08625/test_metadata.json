{
  "cobol_file": {
    "file_name": "successful_snippet_8ba08625.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. iban-main.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01  iban                    PIC X(50).\n       01  iban-flag               PIC X.\n           88  is-valid            VALUE \"Y\", FALSE \"N\".\n\n       PROCEDURE DIVISION.\n       main-line.\n           MOVE \"GB82 WEST 1234 5698 7654 32\" TO iban\n           PERFORM display-validity\n\n           MOVE \"GB82 TEST 1234 5698 7654 32\" TO iban\n           PERFORM display-validity\n\n           GOBACK\n           .\n       display-validity.\n           CALL \"validate-iban\" USING CONTENT iban, REFERENCE iban-flag\n           IF is-valid\n               DISPLAY FUNCTION TRIM(iban) \" is valid.\"\n           ELSE\n               DISPLAY FUNCTION TRIM(iban) \" is not valid.\"\n           END-IF\n           .\n       END PROGRAM iban-main.\n\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. validate-iban.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01  country-lengths-area    VALUE \"AD24AE23AL28AT20AZ28BA20BE16\"\n           & \"BG22BH22BR29CH21CR21CY28CZ24DE22DK18DO28EE20ES24FI18FO18F\"\n           & \"R27GB22GE22GI23GL18GR27GT28HR21HU28IE22IL23IS26IT27KW30KZ\"\n           & \"20LB28LI21LT20LU20LV21MC27MD24ME22MK19MR27MT31MU30NL18NO1\"\n           & \"5PK24PL28PS29PT25RO24RS22SA24SE24SI19SK24SM27TN24TR26VG24\"\n           .\n           03  country-lengths     OCCURS 64 TIMES\n                                   INDEXED BY country-lengths-idx.\n               05  country-code    PIC XX.\n               05  country-len     PIC 99.\n\n       01  offset                  PIC 99.\n\n       01  i                       PIC 99.\n\n       01  len                     PIC 99.\n\n       LINKAGE SECTION.\n       01  iban                    PIC X(50).\n\n       01  valid-flag              PIC X.\n           88  is-valid            VALUE \"Y\", FALSE \"N\".\n\n       PROCEDURE DIVISION USING iban, valid-flag.\n           MOVE FUNCTION UPPER-CASE(iban) TO iban\n           CALL \"remove-spaces\" USING iban\n\n           \n           INITIALIZE len\n           INSPECT iban TALLYING len FOR CHARACTERS BEFORE SPACE\n           SET country-lengths-idx TO 1\n           SEARCH country-lengths\n               AT END\n                   SET is-valid TO FALSE\n                   GOBACK\n\n               WHEN country-code (country-lengths-idx) = iban (1:2)\n                   IF country-len (country-lengths-idx) NOT = len\n                       SET is-valid TO FALSE\n                       GOBACK\n                   END-IF\n           END-SEARCH\n\n           CALL \"create-iban-number\" USING CONTENT len, REFERENCE iban\n\n           \n           IF FUNCTION MOD(iban, 97) = 1\n               SET is-valid TO TRUE\n           ELSE\n               SET is-valid TO FALSE\n           END-IF\n           .\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. remove-spaces.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01  i                       PIC 99.\n       01  offset                  PIC 99.\n\n       LINKAGE SECTION.\n       01  str                     PIC X(50).\n\n       PROCEDURE DIVISION USING str.\n           INITIALIZE offset\n           PERFORM VARYING i FROM 1 BY 1 UNTIL i > 50\n               EVALUATE TRUE\n                   WHEN str (i:1) = SPACE\n                       ADD 1 TO offset\n               \n                   WHEN offset NOT = ZERO\n                       MOVE str (i:1) TO str (i - offset:1)\n               END-EVALUATE\n           END-PERFORM\n           MOVE SPACES TO str (50 - offset + 1:)\n           .\n       END PROGRAM remove-spaces.\n\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. create-iban-number.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01  first-four              PIC X(4).\n\n       01  iban-num                PIC X(50).\n       01  digit-num               PIC 99 VALUE 1.       \n\n       01  i                       PIC 99.\n\n       01  letter-num              PIC 99.\n\n       LINKAGE SECTION.\n       01  len                     PIC 99.\n\n       01  iban                    PIC X(50).\n\n       PROCEDURE DIVISION USING len, iban.\n           \n           MOVE iban (1:4) TO first-four\n           MOVE iban (5:) TO iban\n           MOVE first-four TO iban (len - 3:)\n\n           \n           INITIALIZE iban-num, digit-num ALL TO VALUE\n           PERFORM VARYING i FROM 1 BY 1\n                   UNTIL i > len OR iban (i:1) = SPACE\n               IF iban (i:1) IS NUMERIC\n                   MOVE iban (i:1) TO iban-num (digit-num:1)\n                   ADD 1 TO digit-num\n               ELSE\n                   COMPUTE letter-num =\n                       FUNCTION ORD(iban (i:1)) - FUNCTION ORD(\"A\") + 10\n                   MOVE letter-num TO iban-num (digit-num:2)\n                   ADD 2 TO digit-num\n               END-IF\n           END-PERFORM\n\n           MOVE iban-num TO iban\n           .\n           \n       END PROGRAM create-iban-number.\n\n       END PROGRAM validate-iban.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}