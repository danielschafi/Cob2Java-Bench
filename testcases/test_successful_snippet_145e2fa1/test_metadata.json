{
  "cobol_file": {
    "file_name": "successful_snippet_145e2fa1.cob",
    "content": "        IDENTIFICATION DIVISION.\n        PROGRAM-ID. OLD-LADY.\n\n        DATA DIVISION.\n        WORKING-STORAGE SECTION.\n\n        01  LYRICS.\n            03  THERE-WAS       PIC X(38) VALUE\n            \"There was an old lady who swallowed a \".\n            03  SHE-SWALLOWED   PIC X(18) VALUE \"She swallowed the \".\n            03  TO-CATCH        PIC X(14) VALUE \" to catch the \".\n        01  ANIMALS.\n            03  FLY.\n                05  NAME        PIC X(6) VALUE \"fly\".\n                05  VERSE       PIC X(60) VALUE\n            \"I don't know why she swallowed a fly. Perhaps she'll die.\".\n            03  SPIDER.\n                05  NAME        PIC X(6) VALUE \"spider\".\n                05  VERSE       PIC X(60) VALUE\n            \"That wiggled and jiggled and tickled inside her.\".\n            03  BIRD.\n                05  NAME        PIC X(6) VALUE \"bird\".\n                05  VERSE       PIC X(60) VALUE\n            \"How absurd, to swallow a bird.\".\n            03  CAT.\n                05  NAME        PIC X(6) VALUE \"cat\".\n                05  VERSE       PIC X(60) VALUE\n            \"Imagine that, she swallowed a cat.\".\n            03  DOG.\n                05  NAME        PIC X(6) VALUE \"dog\".\n                05  VERSE       PIC X(60) VALUE\n            \"What a hog, to swallow a dog.\".\n            03  GOAT.\n                05  NAME        PIC X(6) VALUE \"goat\".\n                05  VERSE       PIC X(60) VALUE\n            \"She just opened her throat and swallowed that goat.\".\n            03  COW.\n                05  NAME        PIC X(6) VALUE \"cow\".\n                05  VERSE       PIC X(60) VALUE\n            \"I don't know how she swallowed that cow.\".\n            03  HORSE.\n                05  NAME        PIC X(6) VALUE \"horse\".\n                05  VERSE       PIC X(60) VALUE\n            \"She's dead, of course.\".\n        01  ANIMAL-ARRAY REDEFINES ANIMALS.\n            03  ANIMAL OCCURS 8 TIMES.\n                05  NAME        PIC X(6).\n                05  VERSE       PIC X(60).\n        01  MISC.\n            03  LINE-OUT        PIC X(80).\n            03  A-IDX           PIC 9(2).\n            03  S-IDX           PIC 9(2).\n\n        PROCEDURE DIVISION.\n        MAIN SECTION.\n            PERFORM DO-ANIMAL\n                VARYING A-IDX FROM 1 BY 1 UNTIL A-IDX > 8.\n            STOP RUN.\n\n        DO-ANIMAL SECTION.\n            MOVE SPACES TO LINE-OUT.\n            STRING\n                THERE-WAS DELIMITED BY SIZE,\n                NAME OF ANIMAL(A-IDX) DELIMITED BY SPACE,\n                \",\"\n                INTO LINE-OUT\n            END-STRING.\n            DISPLAY LINE-OUT.\n            IF A-IDX > 1 THEN\n                DISPLAY VERSE OF ANIMAL(A-IDX)\n            END-IF.\n            IF A-IDX = 8 THEN\n                EXIT SECTION\n            END-IF.\n            PERFORM DO-SWALLOW \n                VARYING S-IDX FROM A-IDX BY -1 UNTIL S-IDX = 1.\n            DISPLAY VERSE OF ANIMAL(1).\n            DISPLAY SPACES.\n\n        DO-SWALLOW SECTION.\n            MOVE SPACES TO LINE-OUT.\n            STRING\n                SHE-SWALLOWED DELIMITED BY SIZE,\n                NAME OF ANIMAL(S-IDX) DELIMITED BY SPACE,\n                TO-CATCH DELIMITED BY SIZE,\n                NAME OF ANIMAL(S-IDX - 1) DELIMITED BY SPACE\n                INTO LINE-OUT\n            END-STRING.\n            DISPLAY LINE-OUT.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}