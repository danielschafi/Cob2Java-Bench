{
    "cobol_file": {
        "file_name": "successful_snippet_b5635024.cob",
        "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AOC-2021-13-1.\n       AUTHOR. ANNA KOSIERADZKA.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT INPUTFILE ASSIGN TO \"d13.input\"\n           ORGANIZATION IS LINE SEQUENTIAL.\n           \n       DATA DIVISION.\n\n       FILE SECTION.\n         FD INPUTFILE.\n         01 INPUTRECORD PIC X(16).\n\n       WORKING-STORAGE SECTION.\n         01 FILE-STATUS PIC 9 VALUE 0.\n         01 WS-DOTS-ARRAY OCCURS 2000 TIMES.\n           05 WS-DOT PIC 9 VALUE 0 OCCURS 2000 TIMES.\n         77 WS-RESULT PIC 9(6) VALUE 0.\n         77 N PIC 9(4) VALUE 2000.\n         77 I PIC 9(4) VALUE 1.\n         77 J PIC 9(4) VALUE 1.\n         77 K PIC 9(4) VALUE 1.\n         77 I1 PIC 9(4) VALUE 1.\n         77 J1 PIC 9(4) VALUE 1.\n         77 X PIC 9(4). \n         77 Y PIC 9(4).\n\n       PROCEDURE DIVISION.\n       001-MAIN.\n           OPEN INPUT INPUTFILE.\n           PERFORM 002-READ UNTIL FILE-STATUS = 1.\n           CLOSE INPUTFILE.\n           STOP RUN. \n           \n       002-READ.\n           READ INPUTFILE\n             AT END MOVE 1 TO FILE-STATUS\n             NOT AT END PERFORM 003-PROCESS-RECORD\n           END-READ.\n    \n       003-PROCESS-RECORD.\n           IF INPUTRECORD(1:1) = ' ' THEN\n             MOVE N TO X\n             MOVE N TO Y\n             EXIT PARAGRAPH\n           END-IF.\n           IF INPUTRECORD(1:1) <> 'f' THEN\n             PERFORM 004-PLACE-DOT\n           ELSE\n             IF INPUTRECORD(12:1) = 'x' THEN\n               MOVE INPUTRECORD(14:3) TO X\n               ADD 1 TO X\n               PERFORM 005-FOLD-X\n             ELSE \n               MOVE INPUTRECORD(14:3) TO Y\n               ADD 1 TO Y\n               PERFORM 006-FOLD-Y\n             END-IF \n             PERFORM 007-COUNT-DOTS\n             DISPLAY WS-RESULT\n             CLOSE INPUTFILE\n             STOP RUN\n           END-IF.\n\n       004-PLACE-DOT.\n           UNSTRING INPUTRECORD DELIMITED BY ',' INTO X Y.\n           ADD 1 TO X.\n           ADD 1 TO Y.\n           MOVE 1 TO WS-DOT(X, Y).\n      \n       005-FOLD-X.\n           COMPUTE I1 = X + 1.\n           PERFORM VARYING J FROM 1 BY 1 UNTIL J > Y\n             MOVE 0 TO WS-DOT(X, J)\n             PERFORM VARYING I FROM I1 BY 1 UNTIL I > 2 * X\n               IF WS-DOT(I, J) = 1 THEN\n                 COMPUTE K = 2 * X - I\n                 MOVE 1 TO WS-DOT(K, J)\n                 MOVE 0 TO WS-DOT(I, J)\n               END-IF  \n             END-PERFORM\n           END-PERFORM.\n       \n       006-FOLD-Y.\n           COMPUTE J1 = Y + 1.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > X\n             MOVE 0 TO WS-DOT(I, Y)\n             PERFORM VARYING J FROM J1 BY 1 UNTIL J > 2 * Y\n               IF WS-DOT(I, J) = 1 THEN\n                 COMPUTE K = 2 * Y - J\n                 MOVE 1 TO WS-DOT(I, K)\n                 MOVE 0 TO WS-DOT(I, J)\n               END-IF\n             END-PERFORM\n           END-PERFORM.\n\n       007-COUNT-DOTS.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > Y - 1\n             PERFORM VARYING J FROM 1 BY 1 UNTIL J > X - 1\n               IF WS-DOT(J, I) = 1 THEN\n                 ADD 1 TO WS-RESULT\n               END-IF\n             END-PERFORM\n           END-PERFORM.\n"
    },
    "input_files": [
        {
            "file_name": "d13.input",
            "content": "6,10\n0,14\n9,10\n0,3\n10,4\n4,11\n6,0\n6,12\n4,1\n0,13\n10,12\n3,4\n3,0\n8,4\n1,10\n2,14\n8,10\n9,0\n\nfold along y=7"
        }
    ],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_b5635024.cob",
    "compiled_file": "successful_snippet_b5635024",
    "expected_output": "000017\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}