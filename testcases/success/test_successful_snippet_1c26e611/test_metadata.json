{
    "cobol_file": {
        "file_name": "successful_snippet_1c26e611.cob",
        "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. I-SQRT.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01 QUAD-RET-VARS.\n          03 X          PIC 9(18).\n          03 Q          PIC 9(18).\n          03 Z          PIC 9(18).\n          03 T          PIC S9(18).\n          03 R          PIC 9(18).\n\n       01 TO-65-VARS.\n          03 ISQRT-N    PIC 99.\n          03 DISP-LN    PIC X(22) VALUE SPACES.\n          03 DISP-FMT   PIC Z9.\n          03 PTR        PIC 99 VALUE 1.\n\n       01 BIG-SQRT-VARS.\n          03 POW-7      PIC 9(18) VALUE 7.\n          03 POW-N      PIC 99 VALUE 1.\n          03 POW-N-OUT  PIC Z9.\n          03 POW-7-OUT  PIC Z(10).\n\n       PROCEDURE DIVISION.\n       BEGIN.\n           PERFORM SQRTS-TO-65.\n           PERFORM BIG-SQRTS.\n           STOP RUN.\n\n       SQRTS-TO-65.\n           PERFORM DISP-SMALL-SQRT\n           VARYING ISQRT-N FROM 0 BY 1\n           UNTIL ISQRT-N IS GREATER THAN 65.\n\n       DISP-SMALL-SQRT.\n           MOVE ISQRT-N TO X.\n           PERFORM ISQRT.\n           MOVE R TO DISP-FMT.\n           STRING DISP-FMT DELIMITED BY SIZE INTO DISP-LN \n           WITH POINTER PTR.\n           IF PTR IS GREATER THAN 22, \n               DISPLAY DISP-LN, \n               MOVE 1 TO PTR.\n\n       BIG-SQRTS.\n           PERFORM BIG-SQRT 10 TIMES.\n \n       BIG-SQRT.\n           MOVE POW-7 TO X.\n           PERFORM ISQRT.\n           MOVE POW-N TO POW-N-OUT. \n           MOVE R TO POW-7-OUT. \n           DISPLAY \"ISQRT(7^\" POW-N-OUT \") = \" POW-7-OUT.\n           ADD 2 TO POW-N.\n           MULTIPLY 49 BY POW-7.\n\n       ISQRT.\n           MOVE 1 TO Q.\n           PERFORM MUL-Q-BY-4 UNTIL Q IS GREATER THAN X.\n           MOVE X TO Z.\n           MOVE ZERO TO R.\n           PERFORM ISQRT-STEP UNTIL Q IS NOT GREATER THAN 1. \n\n       MUL-Q-BY-4.\n           MULTIPLY 4 BY Q.\n\n       ISQRT-STEP.\n           DIVIDE 4 INTO Q.\n           COMPUTE T = Z - R - Q.\n           DIVIDE 2 INTO R.\n           IF T IS NOT LESS THAN ZERO,\n               MOVE T TO Z,\n               ADD Q TO R.\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_1c26e611.cob",
    "compiled_file": "successful_snippet_1c26e611",
    "expected_output": " 0 1 1 1 2 2 2 2 2 3 3\n 3 3 3 3 3 4 4 4 4 4 4\n 4 4 4 5 5 5 5 5 5 5 5\n 5 5 5 6 6 6 6 6 6 6 6\n 6 6 6 6 6 7 7 7 7 7 7\n 7 7 7 7 7 7 7 7 7 8 8\nISQRT(7^ 1) =          2\nISQRT(7^ 3) =         18\nISQRT(7^ 5) =        129\nISQRT(7^ 7) =        907\nISQRT(7^ 9) =       6352\nISQRT(7^11) =      44467\nISQRT(7^13) =     311269\nISQRT(7^15) =    2178889\nISQRT(7^17) =   15252229\nISQRT(7^19) =  106765608\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}