{
    "cobol_file": {
        "file_name": "successful_snippet_dcc7efe9.cob",
        "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. EULER.\n       DATA DIVISION.\n       FILE SECTION.\n       WORKING-STORAGE SECTION.\n       1   TABLE-LENGTH CONSTANT 250.\n       1   SEARCHING-FLAG     PIC 9.\n        88  FINISHED-SEARCHING VALUE IS 1\n                               WHEN SET TO FALSE IS 0.\n       1  CALC.\n        3  A               PIC 999 USAGE COMPUTATIONAL-5.\n        3  B               PIC 999 USAGE COMPUTATIONAL-5.\n        3  C               PIC 999 USAGE COMPUTATIONAL-5.\n        3  D               PIC 999 USAGE COMPUTATIONAL-5.\n        3  ABCD            PIC 9(18) USAGE COMPUTATIONAL-5.\n        3  FIFTH-ROOT-OFFS PIC 999 USAGE COMPUTATIONAL-5.\n        3  POWER-COUNTER   PIC 999 USAGE COMPUTATIONAL-5.\n        88 POWER-MAX       VALUE TABLE-LENGTH.\n       \n       1   PRETTY.\n        3  A               PIC ZZ9.\n        3  FILLER          VALUE \"^5 + \".\n        3  B               PIC ZZ9.\n        3  FILLER          VALUE \"^5 + \".\n        3  C               PIC ZZ9.\n        3  FILLER          VALUE \"^5 + \".\n        3  D               PIC ZZ9.\n        3  FILLER          VALUE \"^5 = \".\n        3  FIFTH-ROOT-OFFS PIC ZZ9.\n        3  FILLER          VALUE \"^5.\".\n\n       1   FIFTH-POWER-TABLE   OCCURS TABLE-LENGTH TIMES\n                               ASCENDING KEY IS FIFTH-POWER\n                               INDEXED BY POWER-INDEX.\n        3  FIFTH-POWER PIC 9(18) USAGE COMPUTATIONAL-5.\n\n\n       PROCEDURE DIVISION.\n       MAIN-PARAGRAPH.\n           SET FINISHED-SEARCHING TO FALSE.\n           PERFORM POWERS-OF-FIVE-TABLE-INIT.\n           PERFORM VARYING\n               A IN CALC\n               FROM 1 BY 1 UNTIL A IN CALC = TABLE-LENGTH\n\n               AFTER B IN CALC\n               FROM 1 BY 1 UNTIL B IN CALC = A IN CALC\n\n               AFTER C IN CALC\n               FROM 1 BY 1 UNTIL C IN CALC = B IN CALC\n\n               AFTER D IN CALC\n               FROM 1 BY 1 UNTIL D IN CALC = C IN CALC\n\n               IF FINISHED-SEARCHING\n                   STOP RUN\n               END-IF\n\n               PERFORM POWER-COMPUTATIONS\n\n           END-PERFORM.\n\n       POWER-COMPUTATIONS.\n\n           MOVE ZERO TO ABCD IN CALC.\n\n           ADD FIFTH-POWER(A IN CALC)\n               FIFTH-POWER(B IN CALC)\n               FIFTH-POWER(C IN CALC)\n               FIFTH-POWER(D IN CALC)\n                   TO ABCD IN CALC.\n\n           SET POWER-INDEX TO 1.\n\n           SEARCH ALL FIFTH-POWER-TABLE\n               WHEN FIFTH-POWER(POWER-INDEX) = ABCD IN CALC\n                      MOVE POWER-INDEX TO FIFTH-ROOT-OFFS IN CALC\n                      MOVE CORRESPONDING CALC TO PRETTY\n                      DISPLAY PRETTY END-DISPLAY\n                      SET FINISHED-SEARCHING TO TRUE\n           END-SEARCH\n\n           EXIT PARAGRAPH.\n\n       POWERS-OF-FIVE-TABLE-INIT.\n           PERFORM VARYING POWER-COUNTER FROM 1 BY 1 UNTIL POWER-MAX\n               COMPUTE FIFTH-POWER(POWER-COUNTER) = \n                   POWER-COUNTER *\n                   POWER-COUNTER *\n                   POWER-COUNTER *\n                   POWER-COUNTER *\n                   POWER-COUNTER \n               END-COMPUTE\n           END-PERFORM.\n           EXIT PARAGRAPH.\n\n       END PROGRAM EULER.\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_dcc7efe9.cob",
    "compiled_file": "successful_snippet_dcc7efe9",
    "expected_output": "133^5 + 110^5 +  84^5 +  27^5 = 144^5.\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}