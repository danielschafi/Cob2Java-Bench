{
    "cobol_file": {
        "file_name": "successful_snippet_450fc767.cob",
        "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs06b.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(1024).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs06b'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(009) COMP    VALUE 0.\n           05  DAY-COUNT          PIC 9(009) COMP    VALUE 0.\n           05  NB-DAYS            PIC 9(009) COMP    VALUE 0.\n           05  UNSTRING-PTR       PIC 9(009) COMP    VALUE 1.\n           05  FISH-SUB           PIC 9(009) COMP    VALUE 0.\n           05  TOTAL-FISH         PIC 9(018) COMP    VALUE 0.\n           05  FISH-SWAP          PIC 9(018) COMP    VALUE 0.\n           05  CLI-ARGS           PIC X(080)         VALUE LOW-VALUES.\n           05  FISH-X             PIC X(001)         VALUE LOW-VALUES.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n           05  NB-DAYS-X          PIC X(004)         VALUE LOW-VALUES.\n           05  FISH-SUB-OUT       PIC 9.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT            PIC X(1024)        VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n\n       01  FISH-TABLE.\n           05  FISH-TBL\n               OCCURS 10.\n               10  FISH           PIC 9(018) COMP.\n\n       Procedure Division.\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n\n           ACCEPT CLI-ARGS FROM COMMAND-LINE\n           UNSTRING CLI-ARGS DELIMITED SPACE OR LOW-VALUE\n             INTO PROCESS-TYPE NB-DAYS-X\n           END-UNSTRING\n\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           MOVE FUNCTION NUMVAL(NB-DAYS-X) TO NB-DAYS\n\n           DISPLAY MYNAME ' number of days ' NB-DAYS\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           INITIALIZE FISH-TABLE\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM 1000-LOAD-INPUT UNTIL FISH-X = SPACES\n\n           CLOSE INPT-DATA\n\n           PERFORM VARYING FISH-SUB FROM 2 BY 1\n           UNTIL FISH-SUB > 9\n             ADD FISH(FISH-SUB) TO TOTAL-FISH\n           END-PERFORM\n           DISPLAY MYNAME ' initial number of fish ' TOTAL-FISH\n\n           PERFORM 9010-DUMP-FISH-TABLE\n\n           PERFORM 2000-PROCESS-ONE-DAY\n             VARYING DAY-COUNT FROM 1 BY 1\n             UNTIL DAY-COUNT > NB-DAYS\n\n           MOVE 0 TO TOTAL-FISH\n           PERFORM VARYING FISH-SUB FROM 1 BY 1\n           UNTIL FISH-SUB > 9\n             ADD FISH(FISH-SUB) TO TOTAL-FISH\n           END-PERFORM\n           SUBTRACT 1 FROM DAY-COUNT\n           DISPLAY MYNAME ' number of fish ' TOTAL-FISH\n             ' after ' DAY-COUNT ' days'\n           DISPLAY MYNAME ' records read ' WS-REC-COUNT\n\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n           GOBACK.\n\n       1000-LOAD-INPUT.\n           MOVE SPACES TO FISH-X\n           UNSTRING WS-INPT\n             DELIMITED ','\n             INTO FISH-X\n             POINTER UNSTRING-PTR\n           END-UNSTRING\n\n           IF FISH-X NOT = SPACES\n               COMPUTE FISH-SUB = FUNCTION NUMVAL(FISH-X) + 1\n               ADD 1 TO FISH(FISH-sub)\n           END-IF\n           .\n\n       2000-PROCESS-ONE-DAY.\n           IF PROCESS-TEST\n               RESET TRACE\n           END-IF          \n\n           MOVE FISH(1) TO FISH-SWAP\n           PERFORM VARYING FISH-SUB FROM 2 BY 1\n           UNTIL FISH-SUB > 9\n             MOVE FISH(FISH-SUB) TO FISH(FISH-SUB - 1)\n           END-PERFORM\n           MOVE 0 TO FISH(9)\n           ADD FISH-SWAP TO FISH(7) FISH(9) \n\n           IF PROCESS-TEST\n               PERFORM 9010-DUMP-FISH-TABLE\n               READY TRACE\n           END-IF          \n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n           END-READ\n\n           .\n\n       9010-DUMP-FISH-TABLE.\n               DISPLAY MYNAME ' day ' DAY-COUNT SPACE WITH NO ADVANCING\n               PERFORM VARYING FISH-SUB FROM 1 BY 1\n               UNTIL FISH-SUB > 9\n                 COMPUTE FISH-SUB-OUT = FISH-SUB - 1\n                 DISPLAY FISH-SUB-OUT SPACE FISH(FISH-SUB) ','\n                   WITH NO ADVANCING\n               END-PERFORM\n               DISPLAY SPACE\n           .\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": {
        "file_name": "sysin.txt",
        "content": "PROCESS-TYPE1 NB-DAYS-X005\nPROCESS-TYPE2 NB-DAYS-X010\nPROCESS-TYPE3 NB-DAYS-X015\nPROCESS-TYPE4 NB-DAYS-X020\nPROCESS-TYPE5 NB-DAYS-X025\nPROCESS-TYPE6 NB-DAYS-X030\nPROCESS-TYPE7 NB-DAYS-X035\nPROCESS-TYPE8 NB-DAYS-X040"
    },
    "code_file": "successful_snippet_450fc767.cob",
    "compiled_file": "successful_snippet_450fc767",
    "expected_output": "cs06b    2025100900131065+0200\ncs06b    number of days 000000000\ncs06b    initial number of fish 000000000000000000\ncs06b    day 000000000 0 000000000000000001,1 000000000000000000,2 000000000000000000,3 000000000000000000,4 000000000000000000,5 000000000000000000,6 000000000000000000,7 000000000000000000,8 000000000000000000, \ncs06b    number of fish 000000000000000001 after 000000000 days\ncs06b    records read 000000001\ncs06b    2025100900131065+0200\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}