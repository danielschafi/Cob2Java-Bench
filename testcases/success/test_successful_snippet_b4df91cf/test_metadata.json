{
    "cobol_file": {
        "file_name": "successful_snippet_b4df91cf.cob",
        "content": "       >>SOURCE FREE\nIDENTIFICATION DIVISION.\nPROGRAM-ID. stooge-sort-test.\n\nDATA DIVISION.\nWORKING-STORAGE SECTION.\n01  Arr-Len                             CONSTANT 7.\n\n01  arr-area                            VALUE \"00004001000020000005000230000000000\".\n    03  arr-elt                         PIC 9(5) OCCURS Arr-Len TIMES\n                                        INDEXED BY arr-idx.\n\nPROCEDURE DIVISION.\n    DISPLAY \"Unsorted: \" NO ADVANCING\n    PERFORM VARYING arr-idx FROM 1 BY 1 UNTIL Arr-Len < arr-idx\n        DISPLAY arr-elt (arr-idx) \" \" NO ADVANCING\n    END-PERFORM\n    DISPLAY SPACE\n\n    CALL \"stooge-sort\" USING arr-area, OMITTED, OMITTED\n\n    DISPLAY \"Sorted:   \" NO ADVANCING\n    PERFORM VARYING arr-idx FROM 1 BY 1 UNTIL Arr-Len < arr-idx\n        DISPLAY arr-elt (arr-idx) \" \" NO ADVANCING\n    END-PERFORM\n    DISPLAY SPACE\n    .\nEND PROGRAM stooge-sort-test.\n    \n\nIDENTIFICATION DIVISION.\nPROGRAM-ID. stooge-sort RECURSIVE.\n\nDATA DIVISION.\nLOCAL-STORAGE SECTION.\n01  Arr-Len                             CONSTANT 7.\n\n01  i                                   PIC 99 COMP.\n01  j                                   PIC 99 COMP.\n\n01  temp                                PIC 9(5).\n\n01  t                                   PIC 99 COMP.\n\nLINKAGE SECTION.\n01  arr-area.\n    03  arr-elt                         PIC 9(5) OCCURS Arr-Len TIMES.\n\n01  i-val                               PIC 99 COMP.\n01  j-val                               PIC 99 COMP.\n\nPROCEDURE DIVISION USING arr-area, OPTIONAL i-val, OPTIONAL j-val.\n    IF i-val IS OMITTED\n        MOVE 1 TO i\n    ELSE\n        MOVE i-val TO i\n    END-IF\n    IF j-val IS OMITTED\n        MOVE Arr-Len TO j\n    ELSE\n        MOVE j-val TO j\n    END-IF\n    \n    IF arr-elt (j) < arr-elt (i)\n        MOVE arr-elt (i) TO temp\n        MOVE arr-elt (j) TO arr-elt (i)\n        MOVE temp TO arr-elt (j)\n    END-IF   \n    \n    IF j - i + 1 >= 3\n        COMPUTE t = (j - i + 1) / 3\n        SUBTRACT t FROM j\n        CALL \"stooge-sort\" USING arr-area, CONTENT i, j\n        ADD t TO i, j\n        CALL \"stooge-sort\" USING arr-area, CONTENT i, j\n        SUBTRACT t FROM i, j\n        CALL \"stooge-sort\" USING arr-area, CONTENT i, j\n    END-IF\n    .\nEND PROGRAM stooge-sort.\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_b4df91cf.cob",
    "compiled_file": "successful_snippet_b4df91cf",
    "expected_output": "Unsorted: 00004 00100 00200 00005 00023 00000 00000  \nSorted:   00000 00000 00004 00005 00023 00100 00200  \n",
    "expected_output_files": [],
    "expected_input_output_files": []
}