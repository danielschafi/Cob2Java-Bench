{
    "cobol_file": {
        "file_name": "successful_snippet_37131dc2.cob",
        "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs07a.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(4096).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs07a'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(009) COMP    VALUE 0.\n           05  NB-DAYS            PIC 9(009) COMP    VALUE 0.\n           05  UNSTRING-PTR       PIC 9(009) COMP    VALUE 1.\n           05  NB-CRABS           PIC 9(009) COMP    VALUE 0.\n           05  TOTAL-FUEL         PIC 9(018) COMP    VALUE 0.\n           05  LOW-TOTAL-FUEL     PIC 9(018) COMP    VALUE 999999999999.\n           05  HIGH-CRAB          PIC 9(004) COMP    VALUE 0.\n           05  LOW-CRAB           PIC 9(004) COMP    VALUE 9999.\n           05  HPOSN-DIFF         PIC S9(004) COMP   VALUE +0.\n           05  HPOSN              PIC 9(004) COMP    VALUE 0.\n           05  LOW-HPOSN          PIC 9(004) COMP    VALUE 0.\n           05  CRAB-X             PIC X(004)         VALUE LOW-VALUES.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT            PIC X(4096)        VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n\n       01  CRAB-TABLE.\n           05  CRAB-TBL\n               OCCURS 1000\n               INDEXED CRAB-INDX.\n               10  CRAB           PIC 9(004) COMP.\n\n       Procedure Division.\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n\n           ACCEPT PROCESS-TYPE FROM COMMAND-LINE\n\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           INITIALIZE CRAB-TABLE\n           SET CRAB-INDX TO 1\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM 1000-LOAD-INPUT UNTIL CRAB-X = SPACES\n\n           CLOSE INPT-DATA\n\n           DISPLAY MYNAME ' low crab        ' LOW-CRAB\n           DISPLAY MYNAME ' high crab       ' HIGH-CRAB\n           DISPLAY MYNAME ' nb crabs        ' NB-CRABS\n\n           PERFORM 2000-PROCESS\n             VARYING HPOSN FROM 0 BY 1\n             UNTIL HPOSN > HIGH-CRAB + 1\n\n           DISPLAY MYNAME ' low fuel hposn  ' LOW-HPOSN\n           DISPLAY MYNAME ' low fuel amount ' LOW-TOTAL-FUEL\n           DISPLAY MYNAME ' records read    ' WS-REC-COUNT\n\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n           GOBACK.\n\n       1000-LOAD-INPUT.\n           MOVE SPACES TO CRAB-X\n           UNSTRING WS-INPT\n             DELIMITED ','\n             INTO CRAB-X\n             POINTER UNSTRING-PTR\n           END-UNSTRING\n\n           IF CRAB-X NOT = SPACES\n               ADD 1 TO NB-CRABS\n               SET CRAB-INDX TO NB-CRABS\n               MOVE FUNCTION NUMVAL(CRAB-X) TO CRAB(CRAB-INDX)\n               IF CRAB(CRAB-INDX) > HIGH-CRAB\n                   MOVE CRAB(CRAB-INDX) TO HIGH-CRAB\n               END-IF\n               IF CRAB(CRAB-INDX) < LOW-CRAB\n                   MOVE CRAB(CRAB-INDX) TO LOW-CRAB\n               END-IF\n           END-IF\n           .\n\n       2000-PROCESS.\n           IF PROCESS-TEST\n               RESET TRACE\n               DISPLAY ' hposn ' HPOSN\n           END-IF          \n\n           MOVE 0 TO TOTAL-FUEL\n           PERFORM VARYING CRAB-INDX FROM 1 BY 1\n           UNTIL CRAB-INDX > NB-CRABS\n             COMPUTE HPOSN-DIFF = CRAB(CRAB-INDX) - HPOSN\n             IF PROCESS-TEST\n                 DISPLAY MYNAME ' diff  ' FUNCTION ABS(HPOSN-DIFF)\n             END-IF\n             COMPUTE TOTAL-FUEL = \n               TOTAL-FUEL + FUNCTION ABS(HPOSN-DIFF)\n           END-PERFORM\n\n           IF TOTAL-FUEL < LOW-TOTAL-FUEL\n               MOVE TOTAL-FUEL TO LOW-TOTAL-FUEL\n               MOVE HPOSN TO LOW-HPOSN\n           END-IF\n\n           IF PROCESS-TEST\n               DISPLAY ' total ' TOTAL-FUEL\n               READY TRACE\n           END-IF          \n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n           END-READ\n\n           .\n\n\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": {
        "file_name": "sysin.txt",
        "content": "LOAD  \nBACKUP  \nREPORT  \nVALIDATE  \nUPDATE  \nDELETE  \nINSERT  \nEXIT"
    },
    "code_file": "successful_snippet_37131dc2.cob",
    "compiled_file": "successful_snippet_37131dc2",
    "expected_output": "cs07a    2025100900122436+0200\ncs07a    low crab        0000\ncs07a    high crab       0000\ncs07a    nb crabs        000000001\ncs07a    low fuel hposn  0000\ncs07a    low fuel amount 000000000000000000\ncs07a    records read    000000001\ncs07a    2025100900122436+0200\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}