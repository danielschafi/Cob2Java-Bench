{
    "cobol_file": {
        "file_name": "successful_snippet_f280d69d.cob",
        "content": "       IDENTIFICATION DIVISION.\n      *=======================*\n       PROGRAM-ID.   CGPRG010.\n       AUTHOR.       MIGUEL MORAIS.\n       DATE-WRITTEN. 29/10/2019.\n      *--------------------------------------------------------------*\n      * DISIPLINA: PROGRAMACAO MAINFRAME\n      *--------------------------------------------------------------*\n      * OBJETIVO: LER DADOS DO CADASTRO DE ALUNOS E GERAR RELATORIO\n      *           COM A AVALIACAO FINAL DOS ALUNOS DA TURMA\n      *           SOLICITADA NO PARAMETRO VIA SYSIN\n      *--------------------------------------------------------------*\n      *------------------> HISTORICO - MANUTENCAO <------------------*\n      * VERSAO  MES/ANO  NR.DOC  IDENT.  DESCRICAO\n      * ------  -------  ------  ------  -------------------------   *\n      *  V01    DEZ/2018 010001  SISTEMA LER ARQUIVO E GERA RELATORIO\n      *--------------------------------------------------------------*\n       ENVIRONMENT DIVISION.\n      *====================*\n       CONFIGURATION SECTION.\n      *---------------------*\n       SPECIAL-NAMES.\n           DECIMAL-POINT IS COMMA\n           CURRENCY SIGN IS \"R$ \" WITH PICTURE SYMBOL \"$\"\n           .\n       INPUT-OUTPUT SECTION.\n      *---------------------*\n       FILE-CONTROL.\n           SELECT CADALU ASSIGN TO CADALUJ\n                  FILE STATUS   IS WS-FS-ALU\n           .\n           SELECT RELALU ASSIGN TO RELALUJ\n                  FILE STATUS   IS WS-FS-REL\n           .\n       DATA DIVISION.\n      *=============*\n       FILE SECTION.\n      *------------*\n       FD  CADALU\n           LABEL RECORD STANDARD\n           RECORDING MODE  F\n           .\n       01  REG-CADALU             PIC X(70)\n           .\n       FD  RELALU\n           LABEL RECORD OMITTED\n           RECORDING MODE  F\n           .\n       01  REG-RELALU             PIC X(80)\n           .\n       WORKING-STORAGE SECTION.\n      *-----------------------*\n       01  FILLER                 PIC X(35)       VALUE\n           '**** INICIO DA WORKING-STORAGE ****'.\n\n      *-----> VARIAVEIS AUXILIARES UTILIZADA NO PROCESSAMENTO\n       01  WS-AREA-AUX.\n           05  WS-CTLIDO              PIC 9(02).\n           05  WS-CTIMPR              PIC 9(02).\n           05  WS-CTLIN               PIC 9(02)  VALUE 65.\n           05  WS-DTSYS               PIC 9(06).\n           05  WS-HRSYS               PIC 9(08).\n           05  WS-DTEDI               PIC X(10).\n           05  WS-HREDI               PIC X(11).\n           05  WS-FS-ALU              PIC X(02).\n           05  WS-FS-REL              PIC X(02).\n           05  WS-MSG                 PIC X(30).\n           05  WS-FS-MSG              PIC X(02).\n\n      *-----> DADOS DE ENTRADA - CADASTRO DE ALUNOS\n       01  WS-REG-CADALU.\n           05 WS-NUMERO-E         PIC 9(04).\n           05 WS-NOME-E           PIC X(20).\n           05 WS-SEXO-E           PIC X(01).\n           05 WS-IDADE-E          PIC 9(02).\n           05 WS-CURSO-E          PIC X(12).\n           05 WS-NOTA1-E          PIC 9(02)V99.\n           05 WS-NOTA2-E          PIC 9(02)V99.\n           05 WS-MEDIA-E          PIC 9(02)V99.\n           05 FILLER              PIC X(19).\n\n      *-----> DADOS DE SAIDA - IMPRESSAO DO RELATORIO\n       01  WS-CAB1.\n           05 FILLER                PIC X(01) VALUE SPACES.\n           05 WS-DATA-CAB           PIC X(10).\n           05 FILLER                PIC X(04) VALUE SPACES.\n           05 FILLER                PIC X(38) VALUE\n              'RELATORIO DA AVALIACAO DOS ALUNOS DO: '.\n           05 WS-CURSO-CAB          PIC X(15).\n           05 FILLER                PIC X(04) VALUE SPACES.\n           05 FILLER                PIC X(05) VALUE 'PAG. '.\n           05 WS-PAG-CAB            PIC 9(02) VALUE ZEROS.\n           05 FILLER                PIC X(01) VALUE SPACES.\n\n       01  WS-CAB2.\n           05 FILLER                PIC X(01) VALUE SPACES.\n           05 FILLER                PIC X(07) VALUE 'NUM.   '.\n           05 FILLER                PIC X(04) VALUE 'NOME'.\n           05 FILLER                PIC X(19) VALUE SPACES.\n           05 FILLER                PIC X(05) VALUE 'CURSO'.\n           05 FILLER                PIC X(10) VALUE SPACES.\n           05 FILLER                PIC X(08) VALUE 'NOTA1   '.\n           05 FILLER                PIC X(08) VALUE 'NOTA2   '.\n           05 FILLER                PIC X(08) VALUE 'MEDIA   '.\n           05 FILLER                PIC X(09) VALUE 'AVALIACAO'.\n           05 FILLER                PIC X(01) VALUE SPACES.\n\n       01  WS-REG-RELALU.\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-NUMERO-R         PIC 9(04).\n           05 FILLER              PIC X(03) VALUE SPACES.\n           05 WS-NOME-R           PIC X(20).\n           05 FILLER              PIC X(03) VALUE SPACES.\n           05 WS-CURSO-R          PIC X(12).\n           05 FILLER              PIC X(03) VALUE SPACES.\n           05 WS-NOTA1-R          PIC Z9,99.\n           05 FILLER              PIC X(03) VALUE SPACES.\n           05 WS-NOTA2-R          PIC Z9,99.\n           05 FILLER              PIC X(03) VALUE SPACES.\n           05 WS-MEDIA-R          PIC Z9,99.\n           05 FILLER              PIC X(03) VALUE SPACES.\n           05 WS-AVALIACAO-R      PIC X(09).\n           05 FILLER              PIC X(01) VALUE SPACES.\n\n       01  WS-HIFEN               PIC X(80)       VALUE ALL '-'.\n\n       01  FILLER                 PIC X(35)       VALUE\n           '****** FIM DA WORKING-STORAGE *****'.\n      *\n\n       PROCEDURE DIVISION.\n      *==================*\n      *--------------------------------------------------------------*\n      *    PROCESSO PRINCIPAL\n      *--------------------------------------------------------------*\n       000-RSPRG004.\n\n           PERFORM 010-INICIAR\n           PERFORM 030-PROCESSAR UNTIL WS-FS-ALU = '10'\n           PERFORM 090-TERMINAR\n           STOP RUN\n           .\n      *--------------------------------------------------------------*\n      *    PROCEDIMENTOS INICIAIS\n      *--------------------------------------------------------------*\n       010-INICIAR.\n\n           PERFORM 015-DATA-HORA\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * INICIO : ' WS-DTEDI ' AS ' WS-HREDI\n           DISPLAY ' *----------------------------------------*'\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * PROGRAMA 10 - FATEC SCS                *'\n           DISPLAY ' * MIGUEL MORAIS                          *'\n           DISPLAY ' * JESSICA HOLANDA                        *'\n           DISPLAY ' *----------------------------------------*'\n\n           PERFORM 020-ABRIR-ARQ\n\n           PERFORM 025-LER-CADALU\n\n           IF WS-FS-ALU = '10'\n              MOVE 'ERRO - CADALU VAZIO'  TO  WS-MSG\n              MOVE WS-FS-ALU              TO  WS-FS-MSG\n              GO TO 999-ERRO\n           END-IF\n\n           ACCEPT WS-CURSO-CAB  FROM  SYSIN\n           MOVE   WS-DTEDI      TO    WS-DATA-CAB\n           .\n      *--------------------------------------------------------------*\n      *    OBTER A DATA E HORA DO SISTEMA\n      *--------------------------------------------------------------*\n       015-DATA-HORA.\n\n           ACCEPT  WS-DTSYS  FROM DATE\n           STRING  WS-DTSYS  (5:2) '/'\n                   WS-DTSYS  (3:2) '/20'\n                   WS-DTSYS  (1:2)\n           DELIMITED BY SIZE INTO WS-DTEDI\n\n           ACCEPT  WS-HRSYS  FROM TIME\n           STRING  WS-HRSYS  (1:2) ':'\n                   WS-HRSYS  (3:2) ':'\n                   WS-HRSYS  (5:2) ':'\n                   WS-HRSYS  (7:2)\n           DELIMITED BY SIZE INTO WS-HREDI\n           .\n      *--------------------------------------------------------------*\n      *    ABERTURA DOS ARQUIVOS\n      *--------------------------------------------------------------*\n       020-ABRIR-ARQ.\n\n           OPEN INPUT CADALU\n           IF WS-FS-ALU  NOT = '00'\n              MOVE  'ERRO AO ABRIR O CADALU'  TO WS-MSG\n              MOVE   WS-FS-ALU                TO WS-FS-MSG\n              GO TO  999-ERRO\n           END-IF\n\n           OPEN OUTPUT RELALU\n           IF WS-FS-REL  NOT = '00'\n              MOVE  'ERRO AO ABRIR O RELALU'  TO WS-MSG\n              MOVE   WS-FS-REL                TO WS-FS-MSG\n              GO TO  999-ERRO\n           END-IF\n           .\n      *--------------------------------------------------------------*\n      *    LEITURA DO CADASTRO DE ALUNOS\n      *--------------------------------------------------------------*\n       025-LER-CADALU.\n\n           READ CADALU  INTO  WS-REG-CADALU\n\n           IF WS-FS-ALU  NOT = '00' AND '10'\n              MOVE  'ERRO NA LEITURA DO CADALU'  TO WS-MSG\n              MOVE   WS-FS-ALU                   TO WS-FS-MSG\n              GO TO  999-ERRO\n           ELSE\n              IF WS-FS-ALU = '00'\n                 ADD   1  TO  WS-CTLIDO\n              END-IF\n           END-IF\n           .\n      *--------------------------------------------------------------*\n      *    PROCESSAR DADOS RECEBIDOS DO CADALU\n      *--------------------------------------------------------------*\n       030-PROCESSAR.\n\n           IF WS-MEDIA-E  <  7\n              MOVE 'REPROVADO' TO  WS-AVALIACAO-R\n           ELSE\n              MOVE 'APROVADO ' TO  WS-AVALIACAO-R\n           END-IF\n\n           MOVE  WS-NUMERO-E   TO  WS-NUMERO-R\n           MOVE  WS-NOME-E     TO  WS-NOME-R\n           MOVE  WS-CURSO-E    TO  WS-CURSO-R\n           MOVE  WS-CURSO-E    TO  WS-CURSO-R\n           MOVE  WS-NOTA1-E    TO  WS-NOTA1-R\n           MOVE  WS-NOTA2-E    TO  WS-NOTA2-R\n           MOVE  WS-MEDIA-E    TO  WS-MEDIA-R\n\n           PERFORM 035-IMPREL\n\n           PERFORM 025-LER-CADALU\n           .\n      *--------------------------------------------------------------*\n      *    IMPRIMIR LINHAS DO RELATORIO\n      *--------------------------------------------------------------*\n       035-IMPREL.\n\n           IF WS-CTLIN > 13\n              PERFORM 040-IMPCAB\n           END-IF\n\n           WRITE REG-RELALU   FROM  WS-REG-RELALU\n           IF  WS-FS-REL  NOT = '00'\n               MOVE 'ERRO NA GRAVACAO DO RELALU'   TO WS-MSG\n               MOVE  WS-FS-ALU                     TO WS-FS-MSG\n               GO TO 999-ERRO\n           ELSE\n               ADD 1 TO WS-CTIMPR\n               ADD 1 TO WS-CTLIN\n           END-IF\n           .\n      *--------------------------------------------------------------*\n      *    IMPRIMIR CABECALHO\n      *--------------------------------------------------------------*\n       040-IMPCAB.\n\n           ADD    1        TO WS-PAG-CAB\n\n           WRITE REG-RELALU FROM WS-CAB1 AFTER PAGE\n           IF WS-FS-REL NOT = '00'\n              MOVE 'ERRO GRAVACAO CAB1' TO WS-MSG\n              MOVE WS-FS-REL TO WS-FS-MSG\n              GO TO 999-ERRO\n           END-IF\n\n           WRITE REG-RELALU FROM WS-HIFEN\n           IF WS-FS-REL NOT = '00'\n              MOVE 'ERRO GRAVACAO HIFEN-1' TO WS-MSG\n              MOVE WS-FS-REL TO WS-FS-MSG\n              GO TO 999-ERRO\n           END-IF\n\n           WRITE REG-RELALU FROM WS-CAB2\n           IF WS-FS-REL NOT = '00'\n              MOVE 'ERRO GRAVACAO CAB2' TO WS-MSG\n              MOVE WS-FS-REL            TO WS-FS-MSG\n              GO TO 999-ERRO\n           END-IF\n\n           WRITE REG-RELALU FROM WS-HIFEN\n           IF WS-FS-REL NOT = '00'\n              MOVE 'ERRO GRAVACAO HIFEN-2' TO WS-MSG\n              MOVE WS-FS-REL               TO WS-FS-MSG\n              GO TO 999-ERRO\n           END-IF\n\n           MOVE 4 TO WS-CTLIN\n           .\n      *--------------------------------------------------------------*\n      *    PROCEDIMENTOS FINAIS\n      *--------------------------------------------------------------*\n       090-TERMINAR.\n\n           PERFORM 015-DATA-HORA.\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * TERMINO: ' WS-DTEDI ' AS ' WS-HREDI\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *========================================*'\n           DISPLAY ' *   TOTAIS DE CONTROLE - CGPRG010        *'\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * REGISTROS LIDOS     - CADALU = ' WS-CTLIDO\n           DISPLAY ' * REGISTROS IMPRESSOS - RELALU = ' WS-CTIMPR\n           DISPLAY ' *========================================*'\n\n           PERFORM 095-FECHAR-ARQ\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *      TERMINO NORMAL DO CGPRG010        *'\n           DISPLAY ' *----------------------------------------*'\n           .\n      *--------------------------------------------------------------*\n      *    FECHAR OS ARQUIVOS\n      *--------------------------------------------------------------*\n       095-FECHAR-ARQ.\n\n           CLOSE  CADALU\n           IF WS-FS-ALU  NOT = '00'\n              MOVE  'ERRO AO FECHAR O CADALU'  TO WS-MSG\n              MOVE   WS-FS-ALU                 TO WS-FS-MSG\n              GO TO  999-ERRO\n           END-IF\n\n           CLOSE  RELALU\n           IF WS-FS-REL  NOT = '00'\n              MOVE  'ERRO AO FECHAR O RELALU'  TO WS-MSG\n              MOVE   WS-FS-REL                 TO WS-FS-MSG\n              GO TO  999-ERRO\n           END-IF\n           .\n      *--------------------------------------------------------------*\n      *    ROTINA DE ERRO\n      *--------------------------------------------------------------*\n       999-ERRO.\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *           PROGRAMA CANCELADO           *'\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * MENSAGEM    = ' WS-MSG\n           DISPLAY ' * FILE STATUS = ' WS-FS-MSG\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *       TERMINO ANORMAL DO CGPRG010      *'\n           DISPLAY ' *----------------------------------------*'\n           STOP RUN\n           .\n      *---------------> FIM DO PROGRAMA CGPRG010 <--------------------\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": {
        "file_name": "sysin.txt",
        "content": "PROGRAMMING1  23091500000000\nMATHEMATICS1  23091600000000\nSCIENCE1      23091700000000\nHISTORY1      23091800000000\nENGLISH1      23091900000000\nART1          23092000000000\nPHYSICAL1     23092100000000\nMUSIC1        23092200000000\nEND           00000000000000"
    },
    "code_file": "successful_snippet_f280d69d.cob",
    "compiled_file": "successful_snippet_f280d69d",
    "expected_output": " *----------------------------------------*\n * INICIO : 05/10/2025 AS 21:40:00:23\n *----------------------------------------*\n *----------------------------------------*\n * PROGRAMA 10 - FATEC SCS                *\n * MIGUEL MORAIS                          *\n * JESSICA HOLANDA                        *\n *----------------------------------------*\n *----------------------------------------*\n *           PROGRAMA CANCELADO           *\n *----------------------------------------*\n * MENSAGEM    = ERRO AO ABRIR O CADALU        \n * FILE STATUS = 35\n *----------------------------------------*\n *       TERMINO ANORMAL DO CGPRG010      *\n *----------------------------------------*\n"
}