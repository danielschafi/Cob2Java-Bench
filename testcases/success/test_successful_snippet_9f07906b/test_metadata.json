{
    "cobol_file": {
        "file_name": "successful_snippet_9f07906b.cob",
        "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. max-licenses-in-use.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT license-file ASSIGN \"mlijobs.txt\"\n               ORGANIZATION LINE SEQUENTIAL\n               FILE STATUS file-status.\n\n       DATA DIVISION.\n       FILE SECTION.\n       FD  license-file.\n       01  license-record.\n           03  FILLER                   PIC X(8).\n           03  action                   PIC X(3).\n               88  license-out          VALUE \"OUT\".\n           03  FILLER                   PIC X(3).\n           03  license-timestamp        PIC X(19).\n           03  FILLER                   PIC X(13).\n\n       WORKING-STORAGE SECTION.\n       01  file-status                  PIC XX.\n           88  file-ok                  VALUE \"00\".\n\n       01  max-licenses-out             PIC 9(6).\n       01  num-max-times                PIC 99.\n       01  max-license-times-area.\n           03  max-timestamps           PIC X(19) OCCURS 1 TO 50 TIMES\n               DEPENDING ON num-max-times.\n       01  current-licenses-out         PIC 9(6).\n\n       01  i                            PIC 99.\n\n       PROCEDURE DIVISION.\n       DECLARATIVES.\n       license-file-error SECTION.\n           USE AFTER ERROR ON license-file.\n\n           DISPLAY \"An unexpected error has occurred. Error \"\n               file-status \". The program will close.\"\n           GOBACK\n           .\n       END DECLARATIVES.\n\n       main-line.\n           OPEN INPUT license-file\n           IF NOT file-ok\n               DISPLAY \"File could not be opened. Error \" file-status\n                   \".\"\n               GOBACK\n           END-IF\n\n           PERFORM FOREVER\n               READ license-file\n                   AT END\n                       EXIT PERFORM\n               END-READ\n\n               IF license-out\n                   ADD 1 TO current-licenses-out\n\n                   EVALUATE TRUE\n                       WHEN current-licenses-out > max-licenses-out\n                           MOVE 1 TO num-max-times\n                           MOVE current-licenses-out TO max-licenses-out\n                           MOVE license-timestamp\n                               TO max-timestamps (num-max-times)\n\n                       WHEN current-licenses-out = max-licenses-out\n                           ADD 1 TO num-max-times\n                           MOVE license-timestamp\n                               TO max-timestamps (num-max-times)\n                   END-EVALUATE\n               ELSE\n                   SUBTRACT 1 FROM current-licenses-out\n               END-IF\n           END-PERFORM\n\n           CLOSE license-file\n\n           DISPLAY \"License count at log end: \" current-licenses-out\n           DISPLAY \"Maximum simulataneous licenses: \" max-licenses-out\n           DISPLAY \"Time(s):\"\n           PERFORM VARYING i FROM 1 BY 1 UNTIL num-max-times < i\n               DISPLAY max-timestamps (i)\n           END-PERFORM\n\n           GOBACK\n           .\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_9f07906b.cob",
    "compiled_file": "successful_snippet_9f07906b",
    "expected_output": "An unexpected error has occurred. Error 35. The program will close.\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}