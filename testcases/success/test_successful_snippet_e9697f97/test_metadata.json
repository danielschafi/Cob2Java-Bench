{
    "cobol_file": {
        "file_name": "successful_snippet_e9697f97.cob",
        "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. Test-Prompt-Menu.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n\n       01  Num-Options    USAGE UNSIGNED-INT VALUE 4.\n       01  Example-Menu.\n           03  Example-Options-Data.\n               05  FILLER PIC X(30) VALUE \"fee fie\".\n               05  FILLER PIC X(30) VALUE \"huff and puff\".\n               05  FILLER PIC X(30) VALUE \"mirror mirror\".\n               05  FILLER PIC X(30) VALUE \"tick tock\".\n\n           03  Example-Options-Values REDEFINES Example-Options-Data.\n               05  Example-Options PIC X(30) OCCURS 4 TIMES.\n\n       01  Chosen-Option PIC X(30).\n\n       PROCEDURE DIVISION.\n           CALL \"Prompt-Menu\" USING BY CONTENT Num-Options\n               BY CONTENT Example-Menu\n               BY REFERENCE Chosen-Option\n\n           DISPLAY \"You chose: \" Chosen-Option\n\n           GOBACK\n           .\n\n       END PROGRAM Test-Prompt-Menu.\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. Prompt-Menu.\n\n       DATA DIVISION.\n       LOCAL-STORAGE SECTION.\n       01  User-Input        USAGE UNSIGNED-INT.\n       01  Input-Flag        PIC X.\n           88  Valid-Input   VALUE \"Y\".\n\n       01  Options-Index     USAGE UNSIGNED-INT.\n       01  Index-Display     PIC Z(10).\n\n       LINKAGE SECTION.\n\n       01  Num-Options       USAGE UNSIGNED-INT.\n       01  Menu-Options.\n           03  Options-Table PIC X(30) OCCURS 0 TO 10000000 TIMES\n               DEPENDING ON Num-Options.\n\n       01  Chosen-Option     PIC X(30).\n\n       PROCEDURE DIVISION USING Num-Options Menu-Options Chosen-Option.\n       Main.\n           IF Num-Options = 0\n               MOVE SPACES TO Chosen-Option\n               GOBACK\n           END-IF\n\n           PERFORM UNTIL Valid-Input\n               PERFORM Display-Menu-Options\n\n               DISPLAY \"Choose an option: \" WITH NO ADVANCING\n               ACCEPT User-Input\n\n               PERFORM Validate-Input\n           END-PERFORM\n\n           MOVE Options-Table (User-Input) TO Chosen-Option\n\n           GOBACK\n           .\n\n       Display-Menu-Options.\n           PERFORM VARYING Options-Index FROM 1 BY 1\n                   UNTIL Num-Options < Options-Index\n               MOVE Options-Index TO Index-Display\n               DISPLAY\n                   Index-Display \". \" Options-Table (Options-Index)\n               END-DISPLAY\n           END-PERFORM\n           .\n\n       Validate-Input.\n           IF User-Input = 0 OR > Num-Options\n               DISPLAY \"Invalid input.\"\n           ELSE\n               SET Valid-Input TO TRUE\n           END-IF\n           .\n\n       END PROGRAM Prompt-Menu.\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": {
        "file_name": "sysin.txt",
        "content": "1\n2\n3\n4\n1\n3\n2\n4\n1"
    },
    "code_file": "successful_snippet_e9697f97.cob",
    "compiled_file": "successful_snippet_e9697f97",
    "expected_output": "         1. fee fie                       \n         2. huff and puff                 \n         3. mirror mirror                 \n         4. tick tock                     \nChoose an option: You chose: fee fie                       \n",
    "expected_output_files": [],
    "expected_input_output_files": []
}