{
    "cobol_file": {
        "file_name": "successful_snippet_10158c1b.cob",
        "content": "      ******************************************************************\n      * Author: Jay Moseley\n      * Date: November 10, 2019\n      * Purpose: Testing various subprograms/ functions.\n      * Tectonics: cobc -xj testSubs.cbl\n      ******************************************************************\n       IDENTIFICATION DIVISION.\n\n       PROGRAM-ID. testSubs.\n       ENVIRONMENT DIVISION.\n\n       CONFIGURATION SECTION.\n       REPOSITORY.\n           FUNCTION ALL INTRINSIC\n           FUNCTION validISBN13.\n\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n\n       DATA DIVISION.\n\n       FILE SECTION.\n\n       WORKING-STORAGE SECTION.\n\n       01  IX                          PIC S9(4) COMP.\n       01  TEST-ISBNS.\n           02  FILLER                  PIC X(14) VALUE '978-1734314502'.\n           02  FILLER                  PIC X(14) VALUE '978-1734314509'.\n           02  FILLER                  PIC X(14) VALUE '978-1788399081'.\n           02  FILLER                  PIC X(14) VALUE '978-1788399083'.\n       01  TEST-ISBN                   REDEFINES TEST-ISBNS\n                                       OCCURS 4 TIMES\n                                       PIC X(14).\n\n       PROCEDURE DIVISION.\n\n       MAIN-PROCEDURE.\n\n           PERFORM \n             VARYING IX \n             FROM 1\n             BY 1\n             UNTIL IX > 4\n\n             DISPLAY TEST-ISBN (IX) '   ' WITH NO ADVANCING\n             END-DISPLAY\n             IF validISBN13(TEST-ISBN (IX)) = -1\n               DISPLAY '(bad)'\n             ELSE\n               DISPLAY '(good)'\n             END-IF\n\n           END-PERFORM.\n\n           GOBACK.\n\n       END PROGRAM testSubs.\n\n      ******************************************************************\n      * Author: Jay Moseley\n      * Date: May 19, 2016\n      * Purpose: validate ISBN-13 (International Standard\n      *          Book Number).\n      ******************************************************************\n       IDENTIFICATION DIVISION.\n\n       FUNCTION-ID. validISBN13.\n       ENVIRONMENT DIVISION.\n\n       CONFIGURATION SECTION.\n       REPOSITORY.\n           FUNCTION ALL INTRINSIC.\n\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n\n       DATA DIVISION.\n\n       FILE SECTION.\n\n       WORKING-STORAGE SECTION.\n\n       01  PASSED-SIZE                 PIC S9(6) COMP-5.\n       01  IX                          PIC S9(4) COMP.\n\n       01  WORK-FIELDS.\n           02  WF-DIGIT                PIC X.\n           02  WF-COUNT                PIC 9(2).\n               88  WEIGHT-1  VALUE 1, 3, 5, 7, 9, 11, 13.\n               88  WEIGHT-3  VALUE 2, 4, 6, 8, 10, 12.\n           02  WF-SUM                  PIC S9(8) COMP.\n\n       LINKAGE SECTION.\n\n       01  PASSED-ISBN                 PIC X ANY LENGTH.\n       01  RETURN-VALUE                PIC S9.\n\n       PROCEDURE DIVISION USING PASSED-ISBN\n                          RETURNING RETURN-VALUE.\n\n           CALL 'C$PARAMSIZE'\n             USING 1\n             GIVING PASSED-SIZE\n           END-CALL.\n       \n       COMPUTE-CKDIGIT.\n\n           INITIALIZE WORK-FIELDS.\n           PERFORM \n             VARYING IX \n             FROM 1 \n             BY 1\n             UNTIL IX GREATER THAN PASSED-SIZE\n\n               MOVE PASSED-ISBN (IX:1) TO WF-DIGIT\n               IF WF-DIGIT IS NUMERIC\n                 ADD 1 TO WF-COUNT\n                 IF WEIGHT-1\n                   ADD NUMVAL(WF-DIGIT) TO WF-SUM\n                 ELSE\n                   COMPUTE WF-SUM = WF-SUM + \n                     (NUMVAL(WF-DIGIT) * 3)\n                   END-COMPUTE\n                 END-IF\n               END-IF\n\n           END-PERFORM.\n\n           IF MOD(WF-SUM, 10) = 0\n             MOVE +0 TO RETURN-VALUE\n           ELSE\n             MOVE -1 TO RETURN-VALUE\n           END-IF.\n\n           GOBACK.\n      * - - - - - - - - - - - - - - - - - - - - - - PROGRAM EXIT POINT\n\n       END FUNCTION validISBN13.\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_10158c1b.cob",
    "compiled_file": "successful_snippet_10158c1b",
    "expected_output": "978-1734314502   (good)\n978-1734314509   (bad)\n978-1788399081   (good)\n978-1788399083   (bad)\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}