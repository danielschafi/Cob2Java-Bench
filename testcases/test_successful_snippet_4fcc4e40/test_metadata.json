{
  "cobol_file": {
    "file_name": "successful_snippet_4fcc4e40.cob",
    "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs10a.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(4096).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008)      VALUE 'cs10a'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(009) COMP    VALUE 0.\n           05  STACK-PTR          PIC 9(009) COMP    VALUE 0.\n           05  CHAR-PTR           PIC 9(009) COMP    VALUE 0.\n           05  FILE-SCORE         PIC 9(009) COMP    VALUE 0.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n           05  THE-CHAR           PIC X(001)         VALUE SPACE.\n               88  THE-CHAR-IS-OPEN                  VALUES\n                                                     '(' '[' '{' '<'.\n               88  THE-CHAR-IS-CLOSE                 VALUES\n                                                     ')' ']' '}' '>'.\n\n       01  WS-INPT-DATA GLOBAL.\n           05  WS-INPT            PIC X(4096)        VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n           05  BAD-RECORD-SW      PIC X(001)         VALUE 'N'.\n               88  BAD-RECORD                        VALUE 'Y'\n                                                     FALSE 'N'.\n\n       01  STACK-TABLE.\n           05  STACK OCCURS 256   PIC X(001).\n\n       Procedure Division.\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n\n           ACCEPT PROCESS-TYPE FROM COMMAND-LINE\n\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM 1000-PROCESS-INPUT UNTIL INPT-DATA-EOF\n\n           CLOSE INPT-DATA\n\n           DISPLAY MYNAME ' file score      ' FILE-SCORE\n           DISPLAY MYNAME ' records read    ' WS-REC-COUNT\n\n           GOBACK.\n\n       1000-PROCESS-INPUT.\n           SET BAD-RECORD TO FALSE\n           PERFORM VARYING CHAR-PTR FROM 1 BY 1\n           UNTIL WS-INPT(CHAR-PTR:1) = SPACE\n           OR BAD-RECORD\n             MOVE WS-INPT(CHAR-PTR:1) TO THE-CHAR\n             IF THE-CHAR-IS-OPEN\n                 ADD 1 TO STACK-PTR\n                 MOVE WS-INPT(CHAR-PTR:1) TO STACK(STACK-PTR)\n             ELSE\n                 IF STACK-PTR = 0\n                     DISPLAY MYNAME ' stack pointer 0'\n                     SET BAD-RECORD TO TRUE\n                 ELSE\n                     IF PROCESS-TEST\n                         DISPLAY \n                           MYNAME SPACE THE-CHAR SPACE STACK(STACK-PTR)\n                     END-IF\n                     EVALUATE STACK(STACK-PTR) ALSO THE-CHAR\n                       WHEN   '('              ALSO ')'\n                       WHEN   '['              ALSO ']'\n                       WHEN   '{'              ALSO '}'\n                       WHEN   '<'              ALSO '>'\n                              SUBTRACT 1 FROM STACK-PTR\n                       WHEN OTHER\n                            SET BAD-RECORD TO TRUE\n                     END-EVALUATE\n                 END-IF\n             END-IF\n           END-PERFORM\n\n           IF BAD-RECORD\n               IF PROCESS-TEST\n                   DISPLAY MYNAME ' expected close for '\n                     STACK(STACK-PTR) ' but found ' THE-CHAR ' instead'\n               END-IF\n               EVALUATE THE-CHAR\n                 WHEN ')' ADD 3     TO FILE-SCORE\n                 WHEN ']' ADD 57    TO FILE-SCORE\n                 WHEN '}' ADD 1197  TO FILE-SCORE\n                 WHEN '>' ADD 25137 TO FILE-SCORE\n               END-EVALUATE\n           ELSE\n               continue\n           END-IF\n\n           PERFORM 8010-READ-INPT-DATA\n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n           END-READ\n\n           .\n\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "LOAD\nDUMP\nINIT\nEXIT"
  }
}