{
  "cobol_file": {
    "file_name": "successful_snippet_5ac1c45e.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AOC-2020-22-1.\n       AUTHOR ANNA KOSIERADZKA.\n      \n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT INPUTFILE ASSIGN TO \"d22.input\"\n           ORGANIZATION IS LINE SEQUENTIAL.\n           \n       DATA DIVISION.\n       FILE SECTION.\n         FD INPUTFILE\n         RECORD IS VARYING IN SIZE FROM 1 to 9\n         DEPENDING ON REC-LEN.\n         01 INPUTRECORD PIC X(9).\n         \n       WORKING-STORAGE SECTION.\n         01 FILE-STATUS PIC 9 VALUE 0.\n         01 REC-LEN PIC 9(2) COMP.\n         01 INPUT-BUFFER PIC X(9) OCCURS 54 TIMES.\n         01 WS-CARDS OCCURS 52 TIMES.\n           05 WS-CARDS-1 PIC 99 VALUE 0.\n           05 WS-CARDS-2 PIC 99 VALUE 0.\n         01 C1 PIC 99 VALUE 0.\n         01 C2 PIC 99 VALUE 0.\n\n       LOCAL-STORAGE SECTION.\n         01 RESULT UNSIGNED-INT VALUE 0.\n         01 N UNSIGNED-INT VALUE 25.\n         01 N1 UNSIGNED-INT VALUE 1.\n         01 N2 UNSIGNED-INT VALUE 1.\n         01 I UNSIGNED-INT VALUE 1.\n         01 K UNSIGNED-INT VALUE 1.\n\n       PROCEDURE DIVISION.\n       001-MAIN.\n           OPEN INPUT INPUTFILE.\n           PERFORM 002-READ UNTIL FILE-STATUS = 1.\n           CLOSE INPUTFILE.\n           PERFORM 004-INIT-DATA.\n           PERFORM 005-PLAY-GAME UNTIL N1 = 0 OR N2 = 0.\n           PERFORM 007-TALLY-RESULT.\n           DISPLAY RESULT.\n           STOP RUN.\n\n       002-READ.\n            READ INPUTFILE\n                AT END MOVE 1 TO FILE-STATUS\n                NOT AT END PERFORM 003-PROCESS-RECORD\n            END-READ.\n       \n       003-PROCESS-RECORD.\n           MOVE INPUTRECORD TO INPUT-BUFFER(I).\n           ADD 1 TO I.\n\n       004-INIT-DATA.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > N\n              MOVE INPUT-BUFFER(I + 1) TO WS-CARDS-1(I)\n              COMPUTE K = I + N + 3\n              MOVE INPUT-BUFFER(K) TO WS-CARDS-2(I)\n           END-PERFORM.\n           COMPUTE N1 = N.\n           COMPUTE N2 = N.\n\n       005-PLAY-GAME.\n           MOVE WS-CARDS-1(1) TO C1.\n           MOVE WS-CARDS-2(1) TO C2.\n           IF C1 > C2 THEN\n              MOVE C1 TO WS-CARDS-1(N1 + 1)\n              MOVE C2 TO WS-CARDS-1(N1 + 2)\n              ADD 1 TO N1\n              SUBTRACT 1 FROM N2\n           ELSE \n              MOVE C2 TO WS-CARDS-2(N2 + 1)\n              MOVE C1 TO WS-CARDS-2(N2 + 2)\n              ADD 1 TO N2\n              SUBTRACT 1 FROM N1\n           END-IF.\n           PERFORM 006-SHIFT-CARDS.\n\n       006-SHIFT-CARDS.\n           MOVE 0 TO WS-CARDS-1(N1 + 2).\n           MOVE 0 TO WS-CARDS-2(N2 + 2).\n           COMPUTE K = FUNCTION MAX(N1, N2).\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > K\n              MOVE WS-CARDS-1(I + 1) TO WS-CARDS-1(I)\n              MOVE WS-CARDS-2(I + 1) TO WS-CARDS-2(I)\n           END-PERFORM.\n\n       007-TALLY-RESULT.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 2 * N\n              COMPUTE K = (WS-CARDS-1(I) + WS-CARDS-2(I))\n                  * (2 * N + 1 - I)\n              ADD K TO RESULT\n           END-PERFORM.\n\n\n\n"
  },
  "input_files": [
    {
      "file_name": "d22.input",
      "content": "Player 1:\n9\n2\n6\n3\n1\nPlayer 2:\n5\n8\n4\n7\n10"
    }
  ],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}