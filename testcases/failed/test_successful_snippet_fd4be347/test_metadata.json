{
  "cobol_file": {
    "file_name": "successful_snippet_fd4be347.cob",
    "content": "       >> SOURCE FORMAT IS FIXED\n      \nHISTO *> COBOL edition of historisch-gewachsen.de\nRICAL *>\nLY    *> Usage:\n      *>   - Use as a CGI binary\nGROWN *>   - Directly run from command line\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. historisch-gewachsen.\n       AUTHOR. Simon Biewald.\n       INSTALLATION. \"The cloud\".\n       DATE-WRITTEN. 16/09/2019.\n       SECURITY. NON-CONFIDENTIAL.\n\n       ENVIRONMENT DIVISION.\n       CONFIGURATION SECTION.\n           SOURCE-COMPUTER. Thinkpad-T480.\n           SPECIAL-NAMES.\n\n       INPUT-OUTPUT SECTION.\n           FILE-CONTROL.\n           SELECT QuoteDb ASSIGN TO \"quotes_cobol.txt\"\n               ORGANIZATION IS LINE SEQUENTIAL.\n\nDATA   DATA DIVISION.\n       FILE SECTION.\n           FD QuoteDb.\n           01 QuoteDetails.\n               02  quote-line          PIC X(80).\n\n       WORKING-STORAGE SECTION.\n           01 NEWLINE                  PIC X       VALUE x\"0a\".\n\n      *>   Get request type.\n           01 request-method           PIC X(5)    VALUE \"GET\".\n           01 request-method-var       PIC X(20) \n               VALUE Z\"REQUEST_METHOD\".\n\n      *>   Current line in file.\n           01 line-count               PIC 9(3)    VALUE 0.\n\n      *>   The line to print (0-indexed)\n           01 chosen-line              PIC 999.\n\n      *>   Check for EOF when determining lines in quotes file.\n           01 file-status              PIC X.\n               88 file-eof                         VALUE HIGH-VALUES.\n\n      *>   Wether the program runs as CGI or not.\n           01 cgi-status               PIC X(1)    VALUE \"N\".\n               88 cgi-enabled                      VALUE \"Y\".\n               88 cgi-disabled                     VALUE \"N\".\n\n      *>   Structure for CURRENT-DATE\n           01 current-date-data.\n               02 current-date.\n                   03 current-year         PIC 9(4).\n                   03 current-month        PIC 9(2).\n                   03 current-day          PIC 9(2).\n               02 current-time.\n                   03 current-hours        PIC 9(2).\n                   03 current-minute       PIC 9(2).\n                   03 current-seconds      PIC 9(2).\n                   03 current-milliseconds PIC 9(2).\n\n      *>   Numeric variable for RNG seed, based on the current date\n           01 data-rng-seed            PIC 9(16).\n\n      *>   Format date in ISO format\n           01 today-formatted.\n               02  formatted-year      PIC 9(4).\n               02  filler              PIC X(1)    VALUE \"-\".\n               02  formatted-month     PIC 9(2).\n               02  filler              PIC X(1)    VALUE \"-\".\n               02  formatted-day       PIC 9(2).\n\n      *>   Temporary variable to output\n           01 display-row              PIC x(80).\n\nCODE   PROCEDURE DIVISION.\n\n       0000-MAIN SECTION.\n           PERFORM 1000-SETUP-TIME\n           PERFORM 2000-CGI-CHECK\n           PERFORM 3000-GET-QUOTE\n\n           IF cgi-enabled\n               CALL \"HTMLSTART\" END-CALL\n           END-IF\n\n           PERFORM 4000-DISPLAY-OUTPUT.\n\n           IF cgi-enabled\n               CALL \"HTMLSTOP\" END-CALL\n           END-IF\n\n           STOP RUN.\n\n       *> Fill 'current-date-data' with the current date and time,\n       *> set the RNG seed and fill 'today-formatted' with today's date.\n       *>\n       *> CALLED BY: 0000-MAIN.\n       1000-SETUP-TIME SECTION.\n              MOVE FUNCTION CURRENT-DATE   TO current-date-data\n              MOVE current-date-data       TO data-rng-seed\n              MOVE current-year            TO formatted-year\n              MOVE current-month           TO formatted-month\n              MOVE current-day             TO formatted-day\n       .\n\n      *> Check if the program runs as CGI and print HTTP headers\n      *> if it does.\n      *> \n      *> CALLED BY: 0000-MAIN.\n       2000-CGI-CHECK SECTION.\n           CALL \"CGIHEADER\" USING\n      *>       cgi-status         \n               by reference cgi-status\n      *>       restrict-request-methods (to GET/HEAD only)\n               \"Y\"\n      *>       content-type\n               by content \"text/html; charset=us-ascii   \"\n      *>       request-method\n               by reference request-method\n           END-CALL\n       \n           IF cgi-enabled THEN\n               DISPLAY \"X-Powered-By: COBOL\" NEWLINE\n               IF request-method = \"HEAD\" THEN\n                   GOBACK\n               END-IF\n           END-IF\n       .\n    \n      *> Seek the 'QuoteDb' to a random line,\n      *> making 'quote-line' a random quote.\n      *> \n      *> CALLED BY: 0000-MAIN.\n       3000-GET-QUOTE SECTION.\n      *>   Get Linecount\n           OPEN INPUT QuoteDb\n           PERFORM UNTIL file-eof\n               ADD 1 TO line-count END-ADD\n    \n               READ QuoteDb\n                   AT END SET file-eof TO TRUE\n               END-READ\n           END-PERFORM\n           CLOSE QuoteDb\n    \n      *>   Get 'random' quote number.\n           COMPUTE\n               chosen-line =\n                   (line-count - 1) * FUNCTION RANDOM(data-rng-seed)\n           END-COMPUTE\n    \n           SET line-count TO 0\n    \n      *>   Read correct quote.\n           OPEN INPUT QuoteDb\n           PERFORM UNTIL line-count = (chosen-line + 1)\n               READ QuoteDb END-READ\n               ADD 1 TO line-count END-ADD\n           END-PERFORM\n           CLOSE QuoteDb\n       .\n\n      *> 4000-DISPLAY-OUTPUT\n      *> Displays some text, the current date and the chosen quote.\n      *> All output has a fixed width of 80 characters.\n      *>\n      *> CALLED BY: 0000-MAIN.\n      *> CALLS: CLEAR-ROW.\n       4000-DISPLAY-OUTPUT SECTION.\n           MOVE \"=== REPORT OF cobol.historisch-gewachsen.de ===\" \n                               TO display-row\n           DISPLAY display-row  \n\n           PERFORM CLEAR-ROW\n           STRING\n               \"Date:       \" today-formatted\n                               INTO display-row\n           END-STRING\n           DISPLAY display-row\n\n           PERFORM CLEAR-ROW\n           STRING\n               \"Web-CGI:    \" cgi-status\n                               INTO display-row\n           END-STRING\n           DISPLAY display-row\n\n           PERFORM CLEAR-ROW\n           DISPLAY display-row\n\n           MOVE \"-- PROBLEM\"   TO display-row\n           DISPLAY display-row\n\n           STRING \n               \"Project has historically grown, \" \n               \"it is still written in COBOL.\"\n                               INTO  display-row\n           END-STRING\n           DISPLAY display-row\n\n           PERFORM CLEAR-ROW\n           DISPLAY display-row\n\n           MOVE \"-- REASON\"    TO display-row\n           DISPLAY display-row\n\n           PERFORM CLEAR-ROW\n           STRING\n               \"#\" chosen-line \n                               INTO display-row \n           END-STRING\n           DISPLAY display-row\n\n      *>   Finally print the quote, prepared by '3000-GET-QUOTE'\n           DISPLAY quote-line\n\n      *>   Empty line with an 80 character width \n           PERFORM CLEAR-ROW\n           DISPLAY display-row\n       .\n\n      *> Clear 'display-row' with spaces.\n      *>\n      *> CALLED BY: 4000-DISPLAY-OUTPUT\n       CLEAR-ROW.\n           MOVE \" \"    TO display-row\n       .\n            \n\n       END PROGRAM historisch-gewachsen.\n\n      *> CGI \"header\" function \n"
  },
  "input_files": [
    {
      "file_name": "quotes_cobol.txt",
      "content": "Q1234567APPLE IPHONE 12 64GB BLACK 000007999920230915\nQ2345678SAMSUNG GALAXY S21 128GB BLUE 000009999920230916\nQ3456789GOOGLE PIXEL 6 128GB WHITE 000005999920230917\nQ4567890HUAWEI P40 PRO 256GB SILVER 000012999920230918\nQ5678901SONY XPERIA 1 III 128GB PURPLE 000011999920230919\nQ6789012LG V60 THINQ 5G 128GB GREEN 000008999920230920\nQ7890123ONEPLUS 9 128GB GRAY 000009999920230921\nQ8901234XIAOMI 11T PRO 128GB BLACK 000007999920230922\nQ9012345ASUS ROG PHONE 5 128GB RED 000010999920230923\nQ0123456NOKIA G50 128GB BLUE 000004999920230924"
    }
  ],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}