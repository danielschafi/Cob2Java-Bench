{
  "cobol_file": {
    "file_name": "successful_snippet_a7425303.cob",
    "content": "       identification division.\n       program-id. calc.\n\n       data division.\n\n       working-storage section.\n\n       01 ws-args.\n           03 ws-fstnb             pic s9(9)v9(4)  usage packed-decimal.\n           03 ws-oper              pic x.\n               88 ws-addition                      value \"+\".\n               88 ws-subtraction                   value \"-\".\n               88 ws-multiplication                value \"*\" \"x\" \".\".\n               88 ws-division                      value \"/\".\n               88 ws-modulo                        value \"%\".\n           03 ws-sndnb             pic s9(9)v9(4)  usage packed-decimal.\n\n       77 ws-fstnb-edt             pic -z(10)9.9(4).\n       77 ws-sndnb-edt             pic -z(10)9.9(4).\n       77 ws-total                 pic -z(18)9.9(4).\n\n       77 ws-input                 pic x(80).\n       77 ws-output                pic x(80).\n\n       77 ws-error                 pic x           value \"0\".\n           88 ws-critical                          value \"1\".\n\n\n       procedure division.\n\n       set ws-critical to true\n\n       perform get-data.\n       perform compute-result.\n       perform display-result.\n\n       goback.\n\n\n       get-data.\n\n           accept ws-input from argument-value\n\n           unstring ws-input delimited by space\n               into    ws-fstnb-edt\n                       ws-oper\n                       ws-sndnb-edt\n           end-unstring\n\n           compute ws-fstnb = function numval(ws-fstnb-edt)\n               on size error\n                   move \"first arg cannot be processed (too big?)\"\n                       to ws-output\n                   call \"error-handler\" using by content\n                           ws-output ws-error\n           end-compute\n\n           compute ws-sndnb = function numval(ws-sndnb-edt)\n               on size error\n                   move \"second arg cannot be processed (too big?)\"\n                       to ws-output\n                   call \"error-handler\" using by content\n                           ws-output ws-error\n           end-compute\n           .\n\n       compute-result.\n\n           evaluate true\n       \n               when ws-addition\n                   compute ws-total = ws-fstnb + ws-sndnb\n                       on size error\n                           move \"error during addition\"\n                               to ws-output\n                           call \"error-handler\" using by content\n                               ws-output ws-error\n                   end-compute\n       \n               when ws-subtraction\n                   compute ws-total = ws-fstnb - ws-sndnb\n                       on size error\n                           move \"error during subtraction\"\n                               to ws-output\n                           call \"error-handler\" using by content\n                               ws-output ws-error\n                   end-compute\n\n               when ws-multiplication\n                   compute ws-total = ws-fstnb * ws-sndnb\n                       on size error\n                           move \"error during multiplication\"\n                               to ws-output\n                           call \"error-handler\" using by content\n                               ws-output ws-error\n                   end-compute\n       \n               when ws-division\n                   compute ws-total = ws-fstnb / ws-sndnb\n                       on size error\n                           move \"error during division\"\n                               to ws-output\n                           call \"error-handler\" using by content\n                               ws-output ws-error\n                   end-compute\n\n               when ws-modulo\n                   compute ws-total = function mod(ws-fstnb ws-sndnb)\n                       on size error\n                           move \"error during modulo\"\n                               to ws-output\n                           call \"error-handler\" using by content\n                               ws-output ws-error\n                   end-compute\n\n               when other\n                   move \"sorry, unknown operator\"\n                       to ws-output\n                   call \"error-handler\" using by content\n                       ws-output ws-error\n\n           end-evaluate\n           .\n\n\n       display-result.\n           \n           move ws-fstnb to ws-fstnb-edt\n           move ws-sndnb to ws-sndnb-edt\n\n           string\n               ws-fstnb-edt space ws-oper space ws-sndnb-edt\n               \" = \" ws-total into ws-output\n               on overflow display \"error in string operation\"\n           end-string\n\n           display ws-output\n           .\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "EMPLOYEE-001 JOHN DOE 50000 SALES 2020-01-15\nEMPLOYEE-002 JANE SMITH 65000 ENGINEERING 2019-03-22\nEMPLOYEE-003 ROBERT JOHNSON 55000 MARKETING 2021-06-10\nEMPLOYEE-004 MARIA GARCIA 72000 FINANCE 2018-11-05\nEMPLOYEE-005 DAVID WILSON 48000 OPERATIONS 2022-02-28\nEMPLOYEE-006 SARAH BROWN 61000 HUMAN-RESOURCES 2020-09-14\nEMPLOYEE-007 MICHAEL DAVIS 58000 IT 2019-07-19"
  }
}