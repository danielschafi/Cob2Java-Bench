{
  "cobol_file": {
    "file_name": "successful_snippet_72c48f7d.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AOC-2020-04-2.\n       AUTHOR. ANNA KOSIERADZKA.\n      \n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT INPUTFILE ASSIGN TO \"d4.input\"\n           ORGANIZATION IS LINE SEQUENTIAL.\n           \n       DATA DIVISION.\n       FILE SECTION.\n         FD INPUTFILE\n         RECORD IS VARYING IN SIZE FROM 1 to 99\n         DEPENDING ON REC-LEN.\n         01 INPUTRECORD PIC X(99).\n       WORKING-STORAGE SECTION.\n         01 FILE-STATUS PIC 9 VALUE 0.\n         01 REC-LEN PIC 9(2) COMP.\n         01 WS-ROW PIC X(16) OCCURS 8 TIMES.\n         01 WS-CHAR PIC X.\n         01 WS-FIELD PIC X(3).\n         01 WS-EYE-COLOR PIC X(3).\n         01 WS-NUM PIC 9(9).\n\n       LOCAL-STORAGE SECTION.\n         01 CORRECT-PASSPORTS UNSIGNED-INT VALUE 0.\n         01 VALID-FIELDS UNSIGNED-INT VALUE 0.\n         01 STRING-PTR UNSIGNED-INT VALUE 1.\n         01 I UNSIGNED-INT VALUE 1.\n         01 YEAR UNSIGNED-INT VALUE 0.\n         01 N1 UNSIGNED-INT VALUE 0.\n         01 N2 UNSIGNED-INT VALUE 0.\n\n       PROCEDURE DIVISION.\n       001-MAIN.\n           OPEN INPUT INPUTFILE.\n           PERFORM 002-READ UNTIL FILE-STATUS = 1.\n           CLOSE INPUTFILE.\n           PERFORM 004-NEXT-PASSPORT.\n           DISPLAY CORRECT-PASSPORTS.\n           STOP RUN.\n\n       002-READ.\n            READ INPUTFILE\n                AT END MOVE 1 TO FILE-STATUS\n                NOT AT END PERFORM 003-PROCESS-RECORD\n            END-READ.\n       \n       003-PROCESS-RECORD.\n           IF REC-LEN = 0 THEN\n              PERFORM 004-NEXT-PASSPORT\n           ELSE \n              PERFORM 005-PROCESS-ROW\n           END-IF.\n          \n       004-NEXT-PASSPORT.\n           IF VALID-FIELDS = 7 THEN\n              ADD 1 TO CORRECT-PASSPORTS\n           END-IF.\n           MOVE 0 TO VALID-FIELDS.\n           \n       005-PROCESS-ROW.\n           MOVE 1 TO STRING-PTR.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 8\n             UNSTRING INPUTRECORD DELIMITED BY SPACE INTO WS-ROW(I)\n             WITH POINTER STRING-PTR\n           END-PERFORM.\n           \n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 8\n              MOVE WS-ROW(I)(1:1) TO WS-CHAR\n              IF NOT WS-CHAR ='c' AND NOT WS-CHAR = ' ' THEN\n                 PERFORM 006-VALIDATE-FIELD\n              END-IF\n           END-PERFORM.\n              \n       006-VALIDATE-FIELD.\n           MOVE WS-ROW(I)(1:3) TO WS-FIELD.\n           MOVE 0 TO N1.\n           MOVE 0 TO N2.\n\n      * byr (Birth Year) - four digits; at least 1920 and at most 2002.\n           IF WS-FIELD = 'byr' THEN\n              COMPUTE YEAR = FUNCTION NUMVAL(WS-ROW(I)(5:4))\n              IF YEAR >= 1920 AND YEAR <= 2002 THEN\n                  ADD 1 TO VALID-FIELDS\n              END-IF\n           END-IF.\n\n      * iyr (Issue Year) - four digits; at least 2010 and at most 2020.\n           IF WS-FIELD = 'iyr' THEN\n              COMPUTE YEAR = FUNCTION NUMVAL(WS-ROW(I)(5:4))\n              IF YEAR >= 2010 AND YEAR <= 2020 THEN\n                  ADD 1 TO VALID-FIELDS\n              END-IF\n           END-IF.\n\n      * eyr (Expiration Year) - 4 digits; at least 2020 and at most 2030\n           IF WS-FIELD = 'eyr' THEN\n              COMPUTE YEAR = FUNCTION NUMVAL(WS-ROW(I)(5:4))\n              IF YEAR >= 2020 AND YEAR <= 2030 THEN\n                  ADD 1 TO VALID-FIELDS\n              END-IF\n           END-IF.\n\n      *hgt (Height) - a number followed by either cm or in\n      * - If cm, the number must be at least 150 and at most 193.\n      * - If in, the number must be at least 59 and at most 76\n           IF WS-FIELD = 'hgt' THEN\n              MOVE WS-ROW(I)(5:10) TO WS-NUM\n              INSPECT WS-ROW(I)(5:10) TALLYING N1 FOR ALL 'cm'\n              INSPECT WS-ROW(I)(5:10) TALLYING N2 FOR ALL 'in'\n              IF N1 = 1 AND WS-NUM >= 150 AND WS-NUM <= 193 THEN\n                ADD 1 TO VALID-FIELDS\n              END-IF\n              IF N2 = 1 AND WS-NUM >= 59 AND WS-NUM <= 76 THEN\n                ADD 1 TO VALID-FIELDS\n              END-IF\n           END-IF.\n\n      * hcl (Hair Color) - # followed by exactly 6 characters 0-9 or a-f\n           IF WS-FIELD = 'hcl' THEN\n              IF WS-ROW(I)(5:1) = '#' AND WS-ROW(I)(12:1) = ' ' THEN\n                 ADD 1 TO VALID-FIELDS\n              END-IF\n           END-IF.\n\n      * ecl (Eye Color) - exactly one of: amb blu brn gry grn hzl oth\n           IF WS-FIELD = 'ecl' THEN\n              MOVE WS-ROW(I)(5:3) TO WS-EYE-COLOR\n              IF WS-EYE-COLOR = 'amb' OR WS-EYE-COLOR = 'blu' \n                  OR WS-EYE-COLOR = 'brn' OR WS-EYE-COLOR = 'gry' \n                  OR WS-EYE-COLOR = 'grn' OR WS-EYE-COLOR = 'hzl' \n                  OR WS-EYE-COLOR = 'oth' THEN\n                ADD 1 TO VALID-FIELDS\n              END-IF\n           END-IF.\n\n      * pid (Passport ID) - a 9-digit number, including leading zeroes.\n           IF WS-FIELD = 'pid' THEN\n              MOVE WS-ROW(I)(5:9) TO WS-NUM\n              COMPUTE N1 = FUNCTION NUMVAL(WS-ROW(I)(5:9))\n\n              IF WS-ROW(I)(14:1) = ' ' AND WS-ROW(I)(5:9) = WS-NUM \n                    AND WS-NUM = N1 THEN\n                 ADD 1 TO VALID-FIELDS\n              END-IF \n           END-IF.\n"
  },
  "input_files": [
    {
      "file_name": "d4.input",
      "content": "ecl:blu pid:123456789 eyr:2025 hcl:#623a2f iyr:2015 byr:1985 hgt:175cm\n\nbyr:1992 iyr:2018 eyr:2028 hgt:165cm hcl:#a97842 ecl:grn pid:456789012\n\necl:amb pid:789012345 eyr:2022 hcl:#cfa07d\niyr:2012 byr:1978 hgt:68in\n\nbyr:1995 iyr:2019 eyr:2029 hgt:183cm\nhcl:#18171d ecl:brn pid:234567890\n\necl:hzl pid:345678901 eyr:2024 hcl:#888785 iyr:2016 byr:1988 hgt:72in\n\nbyr:1920 iyr:2010 eyr:2020 hgt:150cm hcl:#ffffed ecl:gry pid:567890123\n\necl:oth pid:678901234 eyr:2030 hcl:#000000\niyr:2020 byr:2002 hgt:193cm\n\nbyr:1965 iyr:2014 eyr:2026 hgt:59in hcl:#b6652a ecl:blu pid:890123456\n\necl:brn pid:901234567 eyr:2027 hcl:#341e13 iyr:2017 byr:1982 hgt:76in\n\nbyr:1999 iyr:2013 eyr:2023 hgt:160cm\nhcl:#7d3b0c ecl:amb pid:012345678"
    }
  ],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}