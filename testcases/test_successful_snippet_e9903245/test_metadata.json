{
  "cobol_file": {
    "file_name": "successful_snippet_e9903245.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. ATTRACTIVE-NUMBERS.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       77 MAXIMUM         PIC 999 VALUE 120.\n       01 SIEVE-DATA      VALUE SPACES.\n          03 MARKER       PIC X OCCURS 120 TIMES.\n             88 PRIME     VALUE SPACE.\n          03 SIEVE-MAX    PIC 999.\n          03 COMPOSITE    PIC 999.\n          03 CANDIDATE    PIC 999.\n\n       01 FACTORIZE-DATA.\n          03 FACTOR-NUM   PIC 999.\n          03 FACTORS      PIC 999.\n          03 FACTOR       PIC 999.\n          03 QUOTIENT     PIC 999V999.\n          03 FILLER       REDEFINES QUOTIENT.\n             05 FILLER    PIC 999.\n             05 DECIMAL   PIC 999.\n\n       01 OUTPUT-FORMAT.\n          03 OUT-NUM      PIC ZZZ9.\n          03 OUT-LINE     PIC X(72) VALUE SPACES.\n          03 COL-PTR      PIC 99 VALUE 1.\n\n       PROCEDURE DIVISION.\n       BEGIN.\n           PERFORM SIEVE.\n           PERFORM CHECK-ATTRACTIVE\n               VARYING CANDIDATE FROM 2 BY 1\n               UNTIL CANDIDATE IS GREATER THAN MAXIMUM.\n           PERFORM WRITE-LINE.\n           STOP RUN.\n\n       CHECK-ATTRACTIVE.\n           MOVE CANDIDATE TO FACTOR-NUM.\n           PERFORM FACTORIZE.\n           IF PRIME(FACTORS), PERFORM ADD-TO-OUTPUT.\n\n       ADD-TO-OUTPUT.\n           MOVE CANDIDATE TO OUT-NUM.\n           STRING OUT-NUM DELIMITED BY SIZE INTO OUT-LINE\n               WITH POINTER COL-PTR.\n           IF COL-PTR IS EQUAL TO 73, PERFORM WRITE-LINE.\n\n       WRITE-LINE.\n           DISPLAY OUT-LINE.\n           MOVE SPACES TO OUT-LINE.\n           MOVE 1 TO COL-PTR.\n\n       FACTORIZE SECTION.\n       BEGIN.\n           MOVE ZERO TO FACTORS.\n           PERFORM DIVIDE-PRIME\n               VARYING FACTOR FROM 2 BY 1\n               UNTIL FACTOR IS GREATER THAN MAXIMUM.\n           GO TO DONE.\n\n       DIVIDE-PRIME.\n           IF PRIME(FACTOR),\n               DIVIDE FACTOR-NUM BY FACTOR GIVING QUOTIENT,\n               IF DECIMAL IS EQUAL TO ZERO,\n                   ADD 1 TO FACTORS,\n                   MOVE QUOTIENT TO FACTOR-NUM,\n                   GO TO DIVIDE-PRIME.\n       DONE.\n           EXIT.\n\n       SIEVE SECTION.\n       BEGIN.\n           MOVE 'X' TO MARKER(1).\n           DIVIDE MAXIMUM BY 2 GIVING SIEVE-MAX.\n           PERFORM SET-COMPOSITES THRU SET-COMPOSITES-LOOP\n               VARYING CANDIDATE FROM 2 BY 1\n               UNTIL CANDIDATE IS GREATER THAN SIEVE-MAX.\n           GO TO DONE.\n\n       SET-COMPOSITES.\n           MULTIPLY CANDIDATE BY 2 GIVING COMPOSITE.\n       SET-COMPOSITES-LOOP.\n           IF COMPOSITE IS NOT GREATER THAN MAXIMUM,\n               MOVE 'X' TO MARKER(COMPOSITE),\n               ADD CANDIDATE TO COMPOSITE,\n               GO TO SET-COMPOSITES-LOOP.\n       DONE.\n           EXIT.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}