{
  "cobol_file": {
    "file_name": "successful_snippet_2025656c.cob",
    "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs08a.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(4096).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs08a'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(009) COMP    VALUE 0.\n           05  ENTRY-COUNT        PIC 9(009) COMP    VALUE 0.\n           05  UNSTRING-PTR       PIC 9(009) COMP    VALUE 0.\n           05  SEGMENT-COUNT      PIC 9(009) COMP    VALUE 0.\n           05  UNIQUE-COUNT       PIC 9(009) COMP    VALUE 0.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n           05  IN-SIGNAL-PATTERNS PIC X(060)         VALUE SPACES.\n           05  IN-FOUR-DIGITS     PIC X(032)         VALUE SPACES.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT            PIC X(4096)        VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n\n       01  ENTRY-TABLE.\n           05  ENTRY-TBL\n               OCCURS 200\n               INDEXED ENTRY-INDX.\n               10  SIGNAL-PATTERN\n                   OCCURS 10\n                   INDEXED SIGNAL-INDX\n                   PIC X(008).\n               10  DIGIT\n                   OCCURS 4\n                   INDEXED DIGIT-INDX\n                   PIC X(008).\n\n       Procedure Division.\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n\n           ACCEPT PROCESS-TYPE FROM COMMAND-LINE\n\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           INITIALIZE ENTRY-TABLE\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM 1000-LOAD-INPUT UNTIL INPT-DATA-EOF\n\n           CLOSE INPT-DATA\n\n           PERFORM 2000-PROCESS-ENTRIES\n             VARYING ENTRY-INDX FROM 1 BY 1\n             UNTIL ENTRY-INDX > ENTRY-COUNT\n\n           DISPLAY MYNAME ' unique count    ' UNIQUE-COUNT\n           DISPLAY MYNAME ' records read    ' WS-REC-COUNT\n\n           DISPLAY MYNAME SPACE FUNCTION CURRENT-DATE\n           GOBACK.\n\n       1000-LOAD-INPUT.\n           ADD 1 TO ENTRY-COUNT\n           SET ENTRY-INDX TO ENTRY-COUNT\n           UNSTRING WS-INPT DELIMITED ' | '\n             INTO\n               IN-SIGNAL-PATTERNS\n               IN-FOUR-DIGITS\n           END-UNSTRING\n\n           MOVE 1 TO UNSTRING-PTR\n           PERFORM VARYING SIGNAL-INDX FROM 1 BY 1\n           UNTIL SIGNAL-INDX > 10\n             UNSTRING IN-SIGNAL-PATTERNS DELIMITED SPACE\n               INTO SIGNAL-PATTERN(ENTRY-INDX,SIGNAL-INDX)\n               POINTER UNSTRING-PTR\n             END-UNSTRING\n           END-PERFORM\n\n           MOVE 1 TO UNSTRING-PTR\n           PERFORM VARYING DIGIT-INDX FROM 1 BY 1\n           UNTIL DIGIT-INDX > 4\n             UNSTRING IN-FOUR-DIGITS DELIMITED SPACE\n               INTO DIGIT(ENTRY-INDX,DIGIT-INDX)\n               POINTER UNSTRING-PTR\n             END-UNSTRING\n           END-PERFORM\n\n           PERFORM 8010-READ-INPT-DATA\n           .\n\n       2000-PROCESS-ENTRIES.\n           IF PROCESS-TEST\n               RESET TRACE\n           END-IF          \n\n           PERFORM VARYING DIGIT-INDX FROM 1 BY 1\n           UNTIL DIGIT-INDX > 4\n             INITIALIZE SEGMENT-COUNT\n             INSPECT DIGIT(ENTRY-INDX,DIGIT-INDX)\n               TALLYING SEGMENT-COUNT\n                 FOR ALL CHARACTERS BEFORE SPACE\n             EVALUATE SEGMENT-COUNT\n               WHEN 2\n               WHEN 3\n               WHEN 4\n               WHEN 7\n                    ADD 1 TO UNIQUE-COUNT\n               WHEN OTHER\n                    CONTINUE\n             END-EVALUATE\n           END-PERFORM\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF          \n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n           END-READ\n\n           .\n\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "LOAD\nDUMP\nINIT\nEXIT"
  }
}