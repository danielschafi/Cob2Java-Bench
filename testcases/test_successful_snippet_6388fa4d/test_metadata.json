{
  "cobol_file": {
    "file_name": "successful_snippet_6388fa4d.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. data-munging.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT input-file ASSIGN TO INPUT-FILE-PATH\n               ORGANIZATION LINE SEQUENTIAL\n               FILE STATUS file-status.\n\n       DATA DIVISION.\n       FILE SECTION.\n       FD  input-file.\n       01  input-record.\n           03  date-stamp          PIC X(10).\n           03  FILLER              PIC X.\n           *> Curse whoever decided to use tabs and variable length\n           *> data in the file!\n           03  input-data-pairs    PIC X(300).\n\n       WORKING-STORAGE SECTION.\n       78  INPUT-FILE-PATH         VALUE \"readings.txt\".\n\n       01  file-status             PIC 99.\n           88 file-is-ok           VALUE 0.\n           88 end-of-file          VALUE 10.\n\n       01  data-pair.\n           03  val                 PIC 9(3)V9(3).\n           03  flag                PIC S9.\n               88  invalid-flag    VALUE -9 THRU 0.\n\n       01  val-length              PIC 9.\n       01  flag-length             PIC 9.\n       01  offset                  PIC 99.\n\n       01  day-total               PIC 9(5)V9(3).\n       01  grand-total             PIC 9(8)V9(3).\n       01  mean-val                PIC 9(8)V9(3).\n\n       01  day-rejected            PIC 9(5).\n       01  day-accepted            PIC 9(5).\n\n       01  total-rejected          PIC 9(8).\n       01  total-accepted          PIC 9(8).\n\n       01  current-data-gap        PIC 9(8).\n       01  max-data-gap            PIC 9(8).\n       01  max-data-gap-end        PIC X(10).\n\n       PROCEDURE DIVISION.\n       DECLARATIVES.\n       *> Terminate the program if an error occurs on input-file.\n       input-file-error SECTION.\n           USE AFTER STANDARD ERROR ON input-file.\n\n           DISPLAY\n               \"An error occurred while reading input.txt. \"\n               \"File error: \" file-status\n               \". The program will terminate.\"\n           END-DISPLAY\n\n           GOBACK\n           .\n\n       END DECLARATIVES.\n\n       main-line.\n           *> Terminate the program if the file cannot be opened.\n           OPEN INPUT input-file\n           IF NOT file-is-ok\n               DISPLAY \"File could not be opened. The program will \"\n                   \"terminate.\"\n               GOBACK\n           END-IF\n\n           *> Process the data in the file.\n           PERFORM FOREVER\n               *> Stop processing if at the end of the file.\n               READ input-file\n                   AT END\n                       EXIT PERFORM\n               END-READ\n\n               *> Split the data up and process the value-flag pairs.\n               PERFORM UNTIL input-data-pairs = SPACES\n                   *> Split off the value-flag pair at the front of the\n                   *> record.\n                   UNSTRING input-data-pairs DELIMITED BY X\"09\"\n                       INTO val COUNT val-length, flag COUNT flag-length\n\n                   COMPUTE offset = val-length + flag-length + 3\n                   MOVE input-data-pairs (offset:) TO input-data-pairs\n\n                   *> Process according to flag.\n                   IF NOT invalid-flag\n                       ADD val TO day-total, grand-total\n\n                       ADD 1 TO day-accepted, total-accepted\n\n                       IF max-data-gap < current-data-gap\n                           MOVE current-data-gap TO max-data-gap\n                           MOVE date-stamp TO max-data-gap-end\n                       END-IF\n\n                       MOVE ZERO TO current-data-gap\n                   ELSE\n                       ADD 1 TO current-data-gap, day-rejected,\n                           total-rejected\n                   END-IF\n               END-PERFORM\n\n               *> Display day stats.\n               DIVIDE day-total BY day-accepted GIVING mean-val\n               DISPLAY\n                   date-stamp\n                   \" Reject: \" day-rejected\n                   \" Accept: \" day-accepted\n                   \" Average: \" mean-val\n               END-DISPLAY\n\n               INITIALIZE day-rejected, day-accepted, mean-val,\n                   day-total\n           END-PERFORM\n\n           CLOSE input-file\n\n           *> Display overall stats.\n           DISPLAY SPACE\n           DISPLAY \"File:         \" INPUT-FILE-PATH\n           DISPLAY \"Total:        \" grand-total\n           DISPLAY \"Readings:     \" total-accepted\n\n           DIVIDE grand-total BY total-accepted GIVING mean-val\n           DISPLAY \"Average:      \" mean-val\n\n           DISPLAY SPACE\n           DISPLAY \"Bad readings: \" total-rejected\n           DISPLAY \"Maximum number of consecutive bad readings is \"\n               max-data-gap\n           DISPLAY \"Ends on date \" max-data-gap-end\n\n           GOBACK\n           .\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}