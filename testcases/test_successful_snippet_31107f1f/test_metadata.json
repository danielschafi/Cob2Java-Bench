{
  "cobol_file": {
    "file_name": "successful_snippet_31107f1f.cob",
    "content": "      IDENTIFICATION DIVISION.\n      PROGRAM-ID.  UNROMAN.\n      DATA DIVISION.\n      WORKING-STORAGE SECTION.\n      01  filler.\n        03  i              pic 9(02) comp.\n        03  j              pic 9(02) comp.\n        03  k              pic 9(02) comp.\n        03  l              pic 9(02) comp.\n      01  inp-roman.\n        03  inp-rom-ch     pic x(01) occurs 20 times.\n      01  inp-roman-digits.\n        03  inp-rom-digit  pic 9(01) occurs 20 times.\n      01  ws-search-idx        pic 9(02) comp.\n      01  ws-tbl-table-def.\n        03  filler pic x(05) value '1000M'.\n        03  filler pic x(05) value '0500D'.\n        03  filler pic x(05) value '0100C'.\n        03  filler pic x(05) value '0050L'.\n        03  filler pic x(05) value '0010X'.\n        03  filler pic x(05) value '0005V'.\n        03  filler pic x(05) value '0001I'.\n      01  filler redefines ws-tbl-table-def.\n        03  ws-tbl-roman      occurs 07 times indexed by rx.\n          05  ws-tbl-rom-val  pic 9(04).\n          05  ws-tbl-rom-ch   pic x(01).\n      01  ws-number           pic s9(05) value 0.\n      01  ws-number-pic       pic zzzz9-.\n\n      PROCEDURE DIVISION.\n          accept inp-roman\n          perform\n          until inp-roman = ' '\n            move zeroes to inp-roman-digits\n            perform\n            varying i from 1 by +1 until inp-rom-ch (i) = ' '\n              set rx to 1\n              search ws-tbl-roman\n                at end\n                  move 0 to inp-rom-digit (i)\n                when ws-tbl-rom-ch (rx) = inp-rom-ch (i)\n                  set inp-rom-digit (i) to rx\n              end-search\n            end-perform\n            compute l = i - 1\n            move 0 to ws-number\n            perform\n            varying i from 1 by +1\n            until i > l or inp-rom-digit (i) = 0\n              compute j = inp-rom-digit (i)\n              compute k = inp-rom-digit (i + 1)\n              if ws-tbl-rom-val (k)\n              >  ws-tbl-rom-val (j)\n                compute ws-number\n                =      ws-number\n                -      ws-tbl-rom-val (j)\n              else\n                compute ws-number\n                =      ws-number\n                +      ws-tbl-rom-val (j)\n              end-if\n            end-perform\n            move ws-number to ws-number-pic\n            display '----------'\n            display 'roman=' inp-roman\n            display 'arabic=' ws-number-pic\n            if i < l or ws-number = 0\n              display 'invalid/incomplete roman numeral at pos 'i\n                      ' found ' inp-rom-ch (i)\n            end-if\n            accept inp-roman\n          end-perform\n          stop run\n          .\n      END PROGRAM UNROMAN.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "III\nIV\nIX\nXLII\nMCMXCIV\nMMXXIII\nCDXLIV"
  }
}