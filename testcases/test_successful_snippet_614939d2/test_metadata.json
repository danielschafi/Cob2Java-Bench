{
  "cobol_file": {
    "file_name": "successful_snippet_614939d2.cob",
    "content": "      **** sndxtest *********************************************\n      * Demonstrate the soundex encoding functions.\n      ***************************************************************\n       Identification division.\n       Program-id. sndxtest.\n\n       Data division.\n       Working-storage section.\n       01 sample-word-list.\n           05 sample-words.\n               10 filler pic x(15) value \"soundex\".\n               10 filler pic x(15) value \"example\".\n               10 filler pic x(15) value \"sownteks\".\n               10 filler pic x(15) value \"ekzampul\".\n               10 filler pic x(15) value \"Euler\".\n               10 filler pic x(15) value \"Gauss\".\n               10 filler pic x(15) value \"Hilbert\".\n               10 filler pic x(15) value \"Knuth\".\n               10 filler pic x(15) value \"Lloyd\".\n               10 filler pic x(15) value \"Lukasiewicz\".\n               10 filler pic x(15) value \"Ellery\".\n               10 filler pic x(15) value \"ghosh\".\n               10 filler pic x(15) value \"Heilbronn\".\n               10 filler pic x(15) value \"Kand\".\n               10 filler pic x(15) value \"Ladd\".\n               10 filler pic x(15) value \"lissajous\".\n               10 filler pic x(15) value \"Wheaton\".\n               10 filler pic x(15) value \"Burroughs\".\n               10 filler pic x(15) value \"burrows\".\n               10 filler pic x(15) value \"O'Hara\".\n               10 filler pic x(15) value \"Washington\".\n               10 filler pic x(15) value \"lee\".\n               10 filler pic x(15) value \"Gutierrez\".\n               10 filler pic x(15) value \"Phister\".\n               10 filler pic x(15) value \"Jackson\".\n               10 filler pic x(15) value \"tymczak\".\n               10 filler pic x(15) value \"Vandeusen\".\n               10 filler pic x(15) value \"Ashcraft\".\n           05 sample-word redefines sample-words\n                         pic x(15) occurs 28 times indexed by wrd-idx.\n       01 wrd-code       pic x999.\n\n       Procedure division.\n           Perform varying wrd-idx from 1 by 1\n           until wrd-idx greater than 28\n               call \"sndxenc\" using\n                   by reference sample-word(wrd-idx)\n                   by reference wrd-code\n               display wrd-code \" \" sample-word(wrd-idx)\n           end-perform.\n           Stop run.\n\n       End program sndxtest.\n\n      *** sndxenc ********************************************\n      * Given a string return its soundex encoding.\n      ***************************************************************\n       Identification division.\n       Program-id. sndxenc.\n\n       Data division.\n       Local-storage section.\n       01 str-idx            pic 99.\n       01 let-code           pic  9.\n       01 prv-let-code       pic  9.\n       01 sdx-idx            pic  9  value 1.\n\n       Linkage section.\n       01 str-to-encode.\n           05 str-first-let  pic x.\n           05 str-rest-let   pic x  occurs 14 times.\n       01 sdx-code.\n           05 sdx-first-let  pic x.\n           05 sdx-nums       pic 9  occurs  3 times.\n\n       Procedure division using\n           by reference str-to-encode\n           by reference sdx-code.\n           Perform encode-start thru encode-done.\n           Goback.\n\n       Encode-start.\n           Move zeros to sdx-code.\n           Move function upper-case(str-first-let) to sdx-first-let.\n           Call \"sndxchar\" using\n               by reference str-first-let\n               by reference let-code.\n           Move let-code to prv-let-code.\n\n       Encode-string.\n           Perform varying str-idx from 1 by 1\n               until str-idx greater than 15\n               or str-rest-let(str-idx) = space\n               or sdx-idx greater than 3\n               call \"sndxchar\" using\n                   by reference str-rest-let(str-idx)\n                   by reference let-code\n               if let-code not equal 7 then\n                   if let-code not equal 0\n                   and let-code not equal prv-let-code\n                       move let-code to sdx-nums(sdx-idx)\n                       add 1 to sdx-idx\n                   end-if\n                   move let-code to prv-let-code\n               end-if\n           end-perform.\n\n       Encode-done.\n           continue.\n       End program sndxenc.\n\n\n      *** sndxchar **********************************************\n      * Given a character, return its soundex encoding.\n      * Code 7 is for h or w, which an encoder should ignore when\n      * either one separates double letters.\n      ***************************************************************\n       Identification division.\n       Program-id. sndxchar.\n\n       Data division.\n       Local-storage section.\n       01 lc-chr pic x.\n           88 code1 value \"b\", \"f\", \"p\", \"v\".\n           88 code2 value \"c\", \"g\", \"j\", \"k\", \"q\", \"s\", \"x\", \"z\".\n           88 code3 value \"d\", \"t\".\n           88 code4 value \"l\".\n           88 code5 value \"m\", \"n\".\n           88 code6 value \"r\".\n           88 code7 value \"h\", \"w\".\n\n       Linkage section.\n       01 char-to-encode pic x.\n       01 char-sdx-code  pic 9.\n\n       Procedure division using\n           by reference char-to-encode\n           by reference char-sdx-code.\n           Move function lower-case(char-to-encode) to lc-chr.\n           If          code1 then move 1 to char-sdx-code\n               else if code2 then move 2 to char-sdx-code\n               else if code3 then move 3 to char-sdx-code\n               else if code4 then move 4 to char-sdx-code\n               else if code5 then move 5 to char-sdx-code\n               else if code6 then move 6 to char-sdx-code\n               else if code7 then move 7 to char-sdx-code\n               else               move 0 to char-sdx-code\n           end-if.\n       End program sndxchar.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}