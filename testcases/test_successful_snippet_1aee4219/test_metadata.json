{
  "cobol_file": {
    "file_name": "successful_snippet_1aee4219.cob",
    "content": "IDENTIFICATION DIVISION.\nPROGRAM-ID. Cobolbrot.\nAUTHOR. Ker2x.\n\nDATA DIVISION.\nWORKING-STORAGE SECTION.\n01 MaxIter CONSTANT 50.\n01 sizeX CONSTANT 1024.\n01 sizeY CONSTANT 1024.\n01 Rmin CONSTANT -2.0.\n01 Rmax CONSTANT 1.0.\n01 Imin CONSTANT -1.3.\n01 Imax CONSTANT 1.3.\n01 OrbitEscape CONSTANT 16.0.\n\n01 screenX PIC 9999 value zero.\n01 screenY PIC 9999 value zero.\n01 iter PIC 99999 value zero.\n\n01 screenR PIC S999V9(16) value zero.\n01 screenI PIC S999V9(16) value zero.\n\n01 pX PIC S9(4)V9(16) value zero.\n01 pY PIC S9(4)V9(16) value zero.\n01 tmp PIC S9(4)V9(16) value zero.\n\n\nPROCEDURE DIVISION.\n\n*> PPM header (P2 = Greyscale; P3 = RGB)\nDISPLAY \"P2\".\nDISPLAY sizeX \" \" sizeY.\nDISPLAY \"# Max iter : \" MaxIter.\nDISPLAY MaxIter.\n\nPERFORM VARYING screenX FROM 0 BY 1 UNTIL screenX IS EQUAL TO sizeX\n    PERFORM VARYING screenY FROM 0 BY 1 UNTIL screenY IS EQUAL TO sizeY\n        MOVE ZERO TO pX\n        MOVE ZERO TO pY\n        COMPUTE screenR = Rmin + (((Rmax - Rmin) / (sizeX - 1)) * screenX)\n        COMPUTE screenI = Imin + (((Imax - Imin) / (sizeY - 1)) * screenY)\n*>        DISPLAY screenX \" \" screenY \" : \" screenI \" \" screenR\n\n        PERFORM WITH test AFTER VARYING iter FROM 0 BY 1 UNTIL iter >= maxIter OR pX**2 + pY**2 >= OrbitEscape\n            COMPUTE tmp = pX**2 - pY**2 + screenR\n            COMPUTE pY = 2.0 * pX * pY + screenI\n            MOVE tmp TO pX\n        END-PERFORM\n\n        IF iter >= maxIter\n            DISPLAY 0 \" \" WITH NO ADVANCING\n        ELSE\n            DISPLAY iter \" \" WITH NO ADVANCING\n        END-IF\n    END-PERFORM\n    DISPLAY \" \"\nEND-PERFORM\nDISPLAY \" \"\nSTOP RUN.\n\nEND PROGRAM Cobolbrot.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}