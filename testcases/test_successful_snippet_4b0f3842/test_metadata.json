{
  "cobol_file": {
    "file_name": "successful_snippet_4b0f3842.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AdventOfCode2019-5b.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n       SELECT InputFile ASSIGN TO InputPath\n       ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n       FILE SECTION.\n       FD InputFile RECORD IS VARYING IN SIZE FROM 1 TO 9999 CHARACTERS.\n       01 InputRec  PIC X(9999).\n\n       WORKING-STORAGE SECTION.\n       01 InputPath PIC X(50).\n       01 Seps      PIC 9(5).\n       01 Str       PIC X(15).\n       01 Ptr       PIC 9(5).\n\n       01 Opcode.\n          02 FILLER       OCCURS 9999 TIMES.\n             03 Cell      PIC S9(15).\n             03 FILLER    REDEFINES Cell.\n                04 Params PIC 9(1) OCCURS 13 TIMES.\n                04 Op     PIC 9(2).\n          02 OpPtr        PIC 9(5).\n          02 OpLen        PIC 9(5).\n          02 Inputs       PIC S9(15) OCCURS 99 TIMES.\n          02 InputPtr     PIC 9(2).\n          02 Outputs      PIC S9(15) OCCURS 99 TIMES.\n          02 OutputPtr    PIC 9(2).\n          02 RA           PIC S9(15).\n          02 RB           PIC S9(15).\n          02 RX           PIC S9(15).\n          02 RY           PIC S9(15).\n          02 RZ           PIC S9(15).\n\n       PROCEDURE DIVISION.\n       Begin.\n           ACCEPT InputPath FROM ARGUMENT-VALUE.\n           OPEN INPUT InputFile.\n           READ InputFile.\n           CLOSE InputFile.\n\n           INSPECT InputRec TALLYING Seps FOR ALL \",\".\n           IF Seps < 9999\n            ADD 1 TO Seps\n           ELSE\n            MOVE 9999 TO Seps\n           END-IF.\n\n           MOVE 1 TO Ptr.\n           MOVE 0 TO OpLen.\n           PERFORM Seps TIMES\n            UNSTRING InputRec DELIMITED BY \",\" INTO Str\n             WITH POINTER Ptr\n            END-UNSTRING\n            ADD 1 TO OpLen\n            COMPUTE Cell(OpLen) = FUNCTION NUMVAL(Str)\n           END-PERFORM.\n\n           MOVE 1 TO OpPtr.\n           MOVE 1 TO InputPtr.\n           MOVE 1 TO OutputPtr.\n\n           MOVE 5 TO Inputs(1).\n\n           DISPLAY \"Execution\".\n           PERFORM UNTIL OpPtr IS GREATER THAN OpLen\n            DISPLAY \" \"\n            DISPLAY \"Instruction \" OpPtr \" \" Op(OpPtr) \" \" Cell(OpPtr)\n            COMPUTE RX = OpPtr + 1\n            MOVE 13 TO RY\n            EVALUATE Op(OpPtr)\n             WHEN 1  PERFORM AddInstruction\n             WHEN 2  PERFORM MultiplyInstruction\n             WHEN 3  PERFORM InputInstruction\n             WHEN 4  PERFORM OutputInstruction\n             WHEN 5  PERFORM JumpIfTrueInstruction\n             WHEN 6  PERFORM JumpIfFalseInstruction\n             WHEN 7  PERFORM LessThanInstruction\n             WHEN 8  PERFORM EqualsInstruction\n             WHEN 99 EXIT PERFORM\n             WHEN OTHER DISPLAY \"ERROR\" STOP RUN\n            END-EVALUATE\n           END-PERFORM.\n\n           DISPLAY \" \".\n           DISPLAY \" \".\n           DISPLAY \"Output\".\n\n           PERFORM VARYING Ptr FROM 1 \n            UNTIL Ptr IS GREATER OR EQUAL TO OutputPtr\n            DISPLAY Ptr \" \" Outputs(Ptr)\n           END-PERFORM.\n           STOP RUN.\n\n       AddInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           ADD Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           MOVE RB TO Cell(RZ).\n           ADD 4 TO OpPtr.\n\n       MultiplyInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           MULTIPLY Cell(RZ) BY RB.\n           PERFORM ReadParam.\n           MOVE RB TO Cell(RZ).\n           ADD 4 TO OpPtr.\n\n       InputInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Inputs(InputPtr) TO Cell(RZ).\n           ADD 1 TO InputPtr.\n           ADD 2 TO OpPtr.\n\n       OutputInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO Outputs(OutputPtr)\n           ADD 1 TO OutputPtr.\n           ADD 2 TO OpPtr.\n\n       JumpIfTrueInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           IF RB IS NOT EQUAL TO 0\n            COMPUTE OpPtr = Cell(RZ) + 1\n           ELSE\n            ADD 3 TO OpPtr\n           END-IF.\n\n       JumpIfFalseInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           IF RB IS EQUAL TO 0\n            COMPUTE OpPtr = Cell(RZ) + 1\n           ELSE\n            ADD 3 TO OpPtr\n           END-IF.\n\n       LessThanInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RA.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           IF RA IS LESS THAN RB\n            MOVE 1 TO Cell(RZ)\n           ELSE\n            MOVE 0 TO Cell(RZ)\n           END-IF.\n           ADD 4 TO OpPtr.\n\n       EqualsInstruction SECTION.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RA.\n           PERFORM ReadParam.\n           MOVE Cell(RZ) TO RB.\n           PERFORM ReadParam.\n           IF RA IS EQUAL TO RB\n            MOVE 1 TO Cell(RZ)\n           ELSE\n            MOVE 0 TO Cell(RZ)\n           END-IF.\n           ADD 4 TO OpPtr.\n\n       ReadParam SECTION.\n           DISPLAY \"Reading param \" RY \": \" Params(OpPtr, RY)\n           IF Params(OpPtr, RY) IS EQUAL TO 0\n            COMPUTE RZ = Cell(RX) + 1\n           ELSE \n            MOVE RX TO RZ\n           END-IF.\n           ADD 1 TO RX.\n           SUBTRACT 1 FROM RY.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "/data/input/transactions.dat"
  }
}