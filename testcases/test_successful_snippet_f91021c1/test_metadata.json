{
  "cobol_file": {
    "file_name": "successful_snippet_f91021c1.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AOC-2021-09-1.\n       AUTHOR. ANNA KOSIERADZKA.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT INPUTFILE ASSIGN TO \"d09.input\"\n           ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n       FILE SECTION.\n         FD INPUTFILE.\n         01 INPUTRECORD PIC X(100).\n\n       WORKING-STORAGE SECTION.\n         01 FILE-STATUS PIC 9 VALUE 0.\n         01 WS-MAP OCCURS 0 TO 100 TIMES DEPENDING ON M.\n           05 WS-POINT PIC 9 OCCURS 100 TIMES.\n         01 WS-RESULT PIC 9(6) VALUE 0.\n         77 M PIC 9(3) VALUE 100.\n         77 N PIC 9(3) VALUE 100.\n         77 I PIC 9(3) VALUE 1.\n         77 J PIC 9(3) VALUE 1.\n         77 IS-LOW PIC 9.\n\n       PROCEDURE DIVISION.\n       001-MAIN.\n           OPEN INPUT INPUTFILE.\n           PERFORM 002-READ UNTIL FILE-STATUS = 1.\n           CLOSE INPUTFILE.\n           PERFORM 004-COUNT-LOWS.\n           DISPLAY WS-RESULT.\n           STOP RUN.\n\n       002-READ.\n           READ INPUTFILE\n             AT END MOVE 1 TO FILE-STATUS\n             NOT AT END PERFORM 003-PROCESS-RECORD\n           END-READ.\n\n       003-PROCESS-RECORD.\n           MOVE INPUTRECORD TO WS-MAP(I).\n           ADD 1 TO I.\n\n       004-COUNT-LOWS.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > M\n             PERFORM VARYING J FROM 1 BY 1 UNTIL J > N\n               MOVE 1 TO IS-LOW\n               IF I > 1 AND WS-POINT(I - 1, J) <= WS-POINT(I, J) THEN\n                 MOVE 0 TO IS-LOW\n               END-IF\n               IF J > 1 AND WS-POINT(I, J - 1) <= WS-POINT(I, J) THEN\n                 MOVE 0 TO IS-LOW\n               END-IF\n               IF I < M AND WS-POINT(I + 1, J) <= WS-POINT(I, J) THEN\n                 MOVE 0 TO IS-LOW\n               END-IF\n               IF J < N AND WS-POINT(I, J + 1) <= WS-POINT(I, J) THEN\n                 MOVE 0 TO IS-LOW\n               END-IF\n               IF IS-LOW = 1 THEN \n                 COMPUTE WS-RESULT = WS-RESULT + WS-POINT(I, J) + 1\n               END-IF\n             END-PERFORM\n           END-PERFORM.\n    "
  },
  "input_files": [
    {
      "file_name": "d09.input",
      "content": "2199943210\n3987894921\n9856789892\n8767896789\n9899965678\n1298943234\n0987654013\n1094321012\n2343210123\n3456543456"
    }
  ],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}