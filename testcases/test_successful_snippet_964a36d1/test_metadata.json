{
  "cobol_file": {
    "file_name": "successful_snippet_964a36d1.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. REVERSAL.\n       AUTHOR.  Bill Gunshannon\n       INSTALLATION.  Home.\n       DATE-WRITTEN.  11 December 2021\n      ****************************************************************\n      ** Program Abstract:\n      **   Use a Knuth Shuffle to reate our out ot sort array.\n      **   Use a procedure called \"reverse\" to pancake sort the array.\n      ****************************************************************\n       \n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       \n       01 RNUM               PIC 9.\n       01 TRIES              PIC 99    VALUE 0.\n       01 ANSWER     PIC 9(9)  VALUE 123456789.\n       01 TBL-LEN    PIC 9    VALUE 9.\n       01 TBL.\n          05  TZ PIC 9(9). \n          05  TA REDEFINES TZ \n                 PIC 9 OCCURS 9 TIMES.\n       \n       PROCEDURE DIVISION.\n       \n       MAIN-pROGRAM.\n           MOVE ANSWER TO TBL\n\n           CALL 'KNUTH-SHUFFLE'\n                 USING  BY REFERENCE TBL\n           END-CALL.\n\n           DISPLAY \"TABLE after shuffle: \" TBL.\n\n           PERFORM UNTIL TBL = ANSWER\n               ADD 1 TO TRIES\n               DISPLAY \"How many to reverse? \"\n               ACCEPT RNUM\n\n               CALL 'REVERSE' USING BY CONTENT RNUM, \n                      BY REFERENCE TBL\n               END-CALL\n\n               DISPLAY \"Try #\" TRIES \"  \" TBL\n           END-PERFORM.\n           DISPLAY \"Congratulations.  You did it!\"\n\n           STOP RUN.\n       END PROGRAM REVERSAL.\n\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. KNUTH-SHUFFLE.\n \n       DATA DIVISION.\n       LOCAL-STORAGE SECTION.\n       01  I                       PIC 9(9).\n       01  J                       PIC 9(9).\n       01  TEMP                    PIC 9(9).\n       01  tABLE-lEN               PIC 9  value 9.\n\n       LINKAGE SECTION.\n       01  TTABLE-AREA.\n           03  TTABLE              PIC 9 OCCURS 9 TIMES.\n \n       PROCEDURE DIVISION USING ttable-area.\n           MOVE FUNCTION RANDOM(FUNCTION CURRENT-DATE (11:6)) TO I\n \n           PERFORM VARYING i FROM Table-Len BY -1 UNTIL i = 0\n               COMPUTE j =\n                   FUNCTION MOD(FUNCTION RANDOM * 10000, Table-Len) + 1\n \n               MOVE ttable (i) TO temp\n               MOVE ttable (j) TO ttable (i)\n               MOVE temp TO ttable (j)\n           END-PERFORM.\n\n           GOBACK.\n           END PROGRAM KNUTH-SHUFFLE.\n\n\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. REVERSE.\n\n       DATA DIVISION.\n       LOCAL-STORAGE SECTION.\n       01  I                       PIC 9.\n       01  J                       PIC 9.\n       01  X                       PIC 9.\n       01  LOOP-IDX                PIC 9.\n\n\n       LINKAGE SECTION.\n       01  IDX        PIC 9.\n       01  TTABLE-AREA.\n           03  TTABLE              PIC 9 OCCURS 9 TIMES.\n\n       PROCEDURE DIVISION USING IDX, TTABLE-AREA.\n\n       DIVIDE IDX BY 2 GIVING LOOP-IDx\n\n\n       MOVE 1 TO I\n       MOVE IDX TO J\n\n       PERFORM LOOP-IDX TIMES\n           MOVE TTABLE(I) TO X\n           MOVE TTABLE(J) TO TTABLE(I)\n           MOVE X TO TTABLE(J)\n           ADD 1 TO I\n           SUBTRACT 1 FROM J\n       END-PERFORM.\n\n       GOBACK.\n       END PROGRAM REVERSE.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "3\n5\n7\n2\n8\n4\n1\n0"
  }
}