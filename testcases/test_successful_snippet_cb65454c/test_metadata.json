{
  "cobol_file": {
    "file_name": "successful_snippet_cb65454c.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. ALMOST-PRIME.\n       \n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01 CONTROL-VARS.\n          03 K              PIC 9.\n          03 I              PIC 999.\n          03 SEEN           PIC 99.\n          03 N              PIC 999.\n          03 P              PIC 99.\n          03 P-SQUARED      PIC 9(4).\n          03 F              PIC 99.\n          03 N-DIV-P        PIC 999V999.\n          03 FILLER         REDEFINES N-DIV-P.\n             05 NEXT-N      PIC 999.\n             05 FILLER      PIC 999.\n                88 N-DIVS-P VALUE ZERO.\n          \n       01 OUT-VARS.\n          03 K-LN           PIC X(70).\n          03 K-LN-PTR       PIC 99.\n          03 LN-HDR.\n             05 FILLER      PIC X(4) VALUE \"K = \".\n             05 K-OUT       PIC 9.\n             05 FILLER      PIC X VALUE \":\".\n          03 I-FMT.\n             05 FILLER      PIC X VALUE SPACE.\n             05 I-OUT       PIC ZZ9.\n       \n       PROCEDURE DIVISION.\n       BEGIN.\n           PERFORM K-ALMOST-PRIMES VARYING K FROM 1 BY 1\n               UNTIL K IS GREATER THAN 5.\n           STOP RUN.\n           \n       K-ALMOST-PRIMES.\n           MOVE SPACES TO K-LN.\n           MOVE 1 TO K-LN-PTR.\n           MOVE ZERO TO SEEN.\n           MOVE K TO K-OUT.\n           STRING LN-HDR DELIMITED BY SIZE INTO K-LN \n               WITH POINTER K-LN-PTR.\n           PERFORM I-K-ALMOST-PRIME VARYING I FROM 2 BY 1\n               UNTIL SEEN IS EQUAL TO 10.\n           DISPLAY K-LN.\n       \n       I-K-ALMOST-PRIME.\n           MOVE ZERO TO F, P-SQUARED.\n           MOVE I TO N.\n           PERFORM PRIME-FACTOR VARYING P FROM 2 BY 1\n               UNTIL F IS NOT LESS THAN K \n               OR P-SQUARED IS GREATER THAN N. \n           IF N IS GREATER THAN 1, ADD 1 TO F.\n           IF F IS EQUAL TO K,\n               MOVE I TO I-OUT,\n               ADD 1 TO SEEN,\n               STRING I-FMT DELIMITED BY SIZE INTO K-LN\n                   WITH POINTER K-LN-PTR.\n       \n       PRIME-FACTOR.\n           MULTIPLY P BY P GIVING P-SQUARED.\n           DIVIDE N BY P GIVING N-DIV-P.\n           PERFORM DIVIDE-FACTOR UNTIL NOT N-DIVS-P.\n           \n       DIVIDE-FACTOR.\n           MOVE NEXT-N TO N.\n           ADD 1 TO F.\n           DIVIDE N BY P GIVING N-DIV-P.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}