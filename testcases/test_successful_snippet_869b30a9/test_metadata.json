{
  "cobol_file": {
    "file_name": "successful_snippet_869b30a9.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. TAU-FUNCTION.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01 TAU-VARS.\n          03 TOTAL              PIC 999.\n          03 N                  PIC 999.\n          03 FILLER             REDEFINES N.\n             05 FILLER          PIC 99.\n             05 FILLER          PIC 9.\n                88 N-EVEN       VALUES 0, 2, 4, 6, 8.\n          03 P                  PIC 999.\n          03 P-SQUARED          PIC 999.\n          03 N-DIV-P            PIC 999V999.\n          03 FILLER             REDEFINES N-DIV-P.\n             05 NEXT-N          PIC 999.\n             05 FILLER          PIC 999.\n                88 DIVISIBLE    VALUE ZERO.\n          03 F-COUNT            PIC 999.\n       01 CONTROL-VARS.\n          03 I                  PIC 999.\n       01 OUT-VARS.\n          03 OUT-ITM            PIC ZZ9.\n          03 OUT-STR            PIC X(80) VALUE SPACES.\n          03 OUT-PTR            PIC 99 VALUE 1.\n\n       PROCEDURE DIVISION.\n       BEGIN.\n           PERFORM SHOW-TAU VARYING I FROM 1 BY 1\n               UNTIL I IS GREATER THAN 100.\n           STOP RUN.\n       \n       SHOW-TAU.\n           MOVE I TO N.\n           PERFORM TAU.\n           MOVE TOTAL TO OUT-ITM.\n           STRING OUT-ITM DELIMITED BY SIZE INTO OUT-STR\n               WITH POINTER OUT-PTR.\n           IF OUT-PTR IS EQUAL TO 61,\n               DISPLAY OUT-STR,\n               MOVE 1 TO OUT-PTR.\n\n       TAU.\n           MOVE 1 TO TOTAL.\n           PERFORM POWER-OF-2 UNTIL NOT N-EVEN.\n           MOVE ZERO TO P-SQUARED.\n           PERFORM ODD-FACTOR THRU ODD-FACTOR-LOOP\n               VARYING P FROM 3 BY 2\n               UNTIL P-SQUARED IS GREATER THAN N.\n           IF N IS GREATER THAN 1,\n               MULTIPLY 2 BY TOTAL.\n       POWER-OF-2.\n           ADD 1 TO TOTAL.\n           DIVIDE 2 INTO N.\n       ODD-FACTOR.\n           MULTIPLY P BY P GIVING P-SQUARED.\n           MOVE 1 TO F-COUNT.\n       ODD-FACTOR-LOOP.\n           DIVIDE N BY P GIVING N-DIV-P.\n           IF DIVISIBLE,\n               MOVE NEXT-N TO N,\n               ADD 1 TO F-COUNT,\n               GO TO ODD-FACTOR-LOOP.\n           MULTIPLY F-COUNT BY TOTAL.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}