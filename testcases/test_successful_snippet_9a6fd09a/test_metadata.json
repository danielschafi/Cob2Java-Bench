{
  "cobol_file": {
    "file_name": "successful_snippet_9a6fd09a.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AOC-2021-04-1.\n       AUTHOR. ANNA KOSIERADZKA.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT INPUTFILE ASSIGN TO \"d04.input\"\n           ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n\n       FILE SECTION.\n         FD INPUTFILE\n         RECORD IS VARYING IN SIZE FROM 1 to 299\n         DEPENDING ON REC-LEN.\n         01 INPUTRECORD PIC X(299).\n\n       WORKING-STORAGE SECTION.\n         01 FILE-STATUS PIC 9 VALUE 0.\n         01 REC-LEN PIC 9(2) COMP.\n         01 WS-DRAWN PIC 99 VALUE 0 OCCURS 99 TIMES.         \n         01 WS-TMP PIC XX.\n      * number of boards\n         01 M PIC 9(3) VALUE 0.\n         01 WS-BOARDS OCCURS 100 TIMES.\n           03 WS-BOARD-ROWS OCCURS 5 TIMES.\n             05 WS-BOARD-NUMS PIC 99 OCCURS 5 TIMES.\n\n         01 WS-MARKED OCCURS 100 TIMES.\n           03 WS-MARKED-ROWS OCCURS 5 TIMES.\n             05 WS-MARKED-NUMS PIC 9 VALUE 0 OCCURS 5 TIMES.\n        \n         01 WS-SUM PIC 9(6) VALUE 0.\n         01 WS-PROD PIC 9 VALUE 1.\n         01 WS-RESULT PIC 9(8).    \n\n       LOCAL-STORAGE SECTION. \n         01 STRING-PTR UNSIGNED-INT VALUE 1.\n         01 I UNSIGNED-INT VALUE 1.\n         01 J UNSIGNED-INT VALUE 1.\n         01 K UNSIGNED-INT VALUE 1.\n         01 X UNSIGNED-INT VALUE 1.\n         01 Y UNSIGNED-INT VALUE 1.\n\n       PROCEDURE DIVISION.\n       001-MAIN.\n           OPEN INPUT INPUTFILE. \n           PERFORM 002-READ UNTIL FILE-STATUS = 1.\n           CLOSE INPUTFILE.          \n           PERFORM 007-DRAW-NUMBERS.\n           STOP RUN.\n\n       002-READ.\n            READ INPUTFILE\n                AT END MOVE 1 TO FILE-STATUS\n                NOT AT END PERFORM 003-PROCESS-RECORD\n            END-READ.           \n\n       003-PROCESS-RECORD.\n           IF REC-LEN > 14 THEN\n              PERFORM 004-READ-NUMBERS\n           ELSE IF REC-LEN > 0 THEN\n              PERFORM 005-READ-BOARDS\n           ELSE\n              PERFORM 006-NEXT-BOARD\n           END-IF.\n\n       004-READ-NUMBERS.\n           MOVE 1 TO STRING-PTR.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 99\n             UNSTRING INPUTRECORD DELIMITED BY ',' INTO WS-TMP\n             WITH POINTER STRING-PTR\n             COMPUTE WS-DRAWN(I) = FUNCTION NUMVAL(WS-TMP)\n           END-PERFORM.\n\n       005-READ-BOARDS.\n           MOVE 1 TO STRING-PTR.\n           PERFORM VARYING X FROM 1 BY 1 UNTIL X > 5\n             MOVE INPUTRECORD(X * 3 - 2 : X * 3 - 1) TO WS-TMP\n             COMPUTE WS-BOARD-NUMS(M,Y,X) = FUNCTION NUMVAL(WS-TMP)\n           END-PERFORM.\n           ADD 1 TO Y.\n\n       006-NEXT-BOARD.\n           ADD 1 TO M.\n           MOVE 1 TO Y.\n\n       007-DRAW-NUMBERS.\n           PERFORM VARYING I FROM 1 BY 1 UNTIL I > 99\n             PERFORM VARYING J FROM 1 BY 1 UNTIL J > M\n               PERFORM VARYING X FROM 1 BY 1 UNTIL X > 5\n                 PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 5\n                   IF WS-BOARD-NUMS(J, X, Y) = WS-DRAWN(I) THEN\n                      MOVE 1 TO WS-MARKED-NUMS(J, X, Y)\n                   END-IF\n                 END-PERFORM\n               END-PERFORM \n             END-PERFORM\n             PERFORM 008-CHECK-IF-BINGO\n           END-PERFORM.\n\n       008-CHECK-IF-BINGO.\n           PERFORM VARYING K FROM 1 BY 1 UNTIL K > M\n      * columns\n           PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 5\n               MOVE 1 TO WS-PROD\n               PERFORM VARYING X FROM 1 BY 1 UNTIL X > 5\n                   COMPUTE WS-PROD = WS-PROD * WS-MARKED-NUMS(K, X, Y)\n               END-PERFORM\n               PERFORM 009-CHECK\n           END-PERFORM      \n      * rows\n           PERFORM VARYING X FROM 1 BY 1 UNTIL X > 5\n               MOVE 1 TO WS-PROD\n               PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 5\n                   COMPUTE WS-PROD = WS-PROD * WS-MARKED-NUMS(K, X, Y)\n               END-PERFORM\n               PERFORM 009-CHECK\n           END-PERFORM\n\n           END-PERFORM.\n\n       009-CHECK.\n           IF WS-PROD = 1 THEN\n             PERFORM VARYING X FROM 1 BY 1 UNTIL X > 5\n               PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > 5\n                 IF WS-MARKED-NUMS(K, X, Y) = 0 THEN\n                   COMPUTE WS-SUM = WS-SUM + WS-BOARD-NUMS(K, X, Y) \n                 END-IF\n               END-PERFORM\n             END-PERFORM\n             COMPUTE WS-RESULT = WS-SUM * WS-DRAWN(I)\n             DISPLAY 'Bingo! ' WS-RESULT\n             STOP RUN\n           END-IF.\n"
  },
  "input_files": [
    {
      "file_name": "d04.input",
      "content": "07,04,09,05,11,17,23,02,00,14,21,24,10,16,13,06,15,25,12,22,18,20,08,19,03,26,01\n22 13 17 11  0\n 8  2 23  4 24\n21  9 14 16  7\n 6 10  3 18  5\n 1 12 20 15 19\n\n 3 15  0  2 22\n 9 18 13 17  5\n19  8  7 25 23\n20 11 10 24  4\n14 21 16 12  6\n\n14 21 17 24  4\n10 16 15  9 19\n18  8 23 26 20\n22 11 13  6  5\n 2  0 12  3  7\n\n 1 14 16 18 10\n22  5 20  3 15\n 0  8 21  6 24\n13 25 12 19  7\n 4  9 23 11  2\n\n18  8 23 26 20\n22 11 13  6  5\n 2  0 12  3  7\n21 10 24 17 15\n 9 19 25 14 16"
    }
  ],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}