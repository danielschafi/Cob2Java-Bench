{
  "cobol_file": {
    "file_name": "successful_snippet_bf67e8cb.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. 99-Bottles-of-Beer-On-The-Wall.\n       AUTHOR. Joseph James Frantz.\n      *COMMENTS.\n      ******************************************************************\n      * PURPOSE:\n      *   This is a sample COBOL program to display the lyrics of the\n      *   song \"99 Bottles of Beer on the Wall.\"\n      *   This version of the COBOL 99 beers program demonstrates a few\n      *   features of COBOL:\n      *\n      *   1. PERFORM VARYING, Cobol's version of a Loop.\n      *   2. ADD/SUBTRACT with GIVING for math calculations.\n      *   3. EVALUATE/WHEN, Cobol's version of Case.\n      *   4. INSPECT/TALLYING, which finds the number of specified\n      *      characters in a variable.\n      *   5. Reference Modification:\n      *      Var-name(Start character:Number of characters)\n      *      which is essentially Cobol's version of text subscripting.\n      *   6. Long descriptive variable names.\n      *   7. Use of SPACES and ZEROES for field/display values.\n      *   8. Highlight the self documenting nature of COBOL.\n      ******************************************************************\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01 Keeping-Track-Variables.\n          05 Bottles                      PIC S99   VALUE 0.\n          05 Remaining-Bottles            PIC S99   VALUE 0.\n          05 Counting                     PIC 99    VALUE 0.\n          05 Start-Position               PIC 99    VALUE 0.\n          05 Positions                    PIC 99    VALUE 0.\n       PROCEDURE DIVISION.\n       PASS-AROUND-THOSE-BEERS.\n        PERFORM VARYING Bottles FROM 99 BY -1 UNTIL Bottles = -1\n          DISPLAY SPACES\n          SUBTRACT 1 FROM Bottles GIVING Remaining-Bottles\n          EVALUATE Bottles\n            WHEN 0\n              DISPLAY \"No more bottles of beer on the wall, \"\n                      \"no more bottles of beer.\"\n              DISPLAY \"Go to the store and buy some more, \"\n                      \"99 bottles of beer on the wall.\"\n            WHEN 1\n              DISPLAY \"1 bottle of beer on the wall, \"\n                      \"1 bottle of beer.\"\n              DISPLAY \"Take one down and pass it around, \"\n                      \"no more bottles of beer on the wall.\"\n            WHEN 2 Thru 99\n              MOVE ZEROES TO Counting\n              INSPECT Bottles,\n                TALLYING Counting FOR LEADING ZEROES\n              ADD 1 TO Counting GIVING Start-Position\n              SUBTRACT Counting FROM 2 GIVING Positions\n              DISPLAY Bottles(Start-Position:Positions)\n                      \" bottles of beer on the wall, \"\n                      Bottles(Start-Position:Positions)\n                      \" bottles of beer.\"\n              MOVE ZEROES TO Counting\n              INSPECT Remaining-Bottles TALLYING\n                Counting FOR LEADING ZEROES\n              ADD 1 TO Counting GIVING Start-Position\n              SUBTRACT Counting FROM 2 GIVING Positions\n              DISPLAY \"Take one down and pass it around, \"\n                      Remaining-Bottles(Start-Position:Positions)\n                      \" bottles of beer on the wall.\"\n          END-EVALUATE\n        END-PERFORM\n       STOP RUN."
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}