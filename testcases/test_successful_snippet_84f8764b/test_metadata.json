{
  "cobol_file": {
    "file_name": "successful_snippet_84f8764b.cob",
    "content": "        IDENTIFICATION DIVISION.\n        PROGRAM-ID. FLOYD-TRIANGLE.\n        \n        DATA DIVISION.\n        WORKING-STORAGE SECTION.\n        01 VARIABLES        COMP.\n           02 NUM-LINES     PIC 99.\n           02 CUR-LINE      PIC 99.\n           02 CUR-COL       PIC 99.\n           02 CUR-NUM       PIC 999.\n           02 ZERO-SKIP     PIC 9.\n           02 LINE-PTR      PIC 99.\n           02 MAX-NUM       PIC 999.\n        \n        01 OUTPUT-FORMAT.\n           02 OUT-LINE      PIC X(72).\n           02 ONE-DIGIT     PIC B9.\n           02 TWO-DIGITS    PIC BZ9.\n           02 THREE-DIGITS  PIC BZZ9.\n           02 MAX-COL-NUM   PIC 999.\n        \n        PROCEDURE DIVISION.\n        BEGIN.\n            MOVE 5 TO NUM-LINES. PERFORM FLOYD.\n            DISPLAY ' '.\n            MOVE 14 TO NUM-LINES. PERFORM FLOYD.\n            STOP RUN.\n            \n        FLOYD.\n            MOVE 1 TO CUR-NUM.\n            COMPUTE MAX-NUM = NUM-LINES * (NUM-LINES + 1) / 2.\n            PERFORM FLOYD-LINE \n                VARYING CUR-LINE FROM 1 BY 1\n                UNTIL CUR-LINE IS GREATER THAN NUM-LINES.\n            \n        FLOYD-LINE.\n            MOVE ' ' TO OUT-LINE.\n            MOVE 1 TO LINE-PTR.\n            PERFORM FLOYD-NUM\n                VARYING CUR-COL FROM 1 BY 1\n                UNTIL CUR-COL IS GREATER THAN CUR-LINE.\n            DISPLAY OUT-LINE.\n\n        FLOYD-NUM.\n            COMPUTE MAX-COL-NUM = MAX-NUM - NUM-LINES + CUR-COL.\n            MOVE 0 TO ZERO-SKIP.\n            INSPECT MAX-COL-NUM TALLYING ZERO-SKIP FOR LEADING '0'.\n            IF ZERO-SKIP IS EQUAL TO ZERO\n                PERFORM FLOYD-THREE-DIGITS\n            ELSE IF ZERO-SKIP IS EQUAL TO 1\n                PERFORM FLOYD-TWO-DIGITS\n            ELSE IF ZERO-SKIP IS EQUAL TO 2\n                PERFORM FLOYD-ONE-DIGIT.\n            ADD 1 TO CUR-NUM.\n                \n        FLOYD-ONE-DIGIT.\n            MOVE CUR-NUM TO ONE-DIGIT.\n            STRING ONE-DIGIT DELIMITED BY SIZE INTO OUT-LINE\n                WITH POINTER LINE-PTR.\n        \n        FLOYD-TWO-DIGITS.\n            MOVE CUR-NUM TO TWO-DIGITS.\n            STRING TWO-DIGITS DELIMITED BY SIZE INTO OUT-LINE\n                WITH POINTER LINE-PTR.\n        \n        FLOYD-THREE-DIGITS.\n            MOVE CUR-NUM TO THREE-DIGITS.\n            STRING THREE-DIGITS DELIMITED BY SIZE INTO OUT-LINE\n                WITH POINTER LINE-PTR.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}