{
  "cobol_file": {
    "file_name": "successful_snippet_3037d246.cob",
    "content": "      ******************************************************************\n      * COBOL solution to Anti-primes challange\n      * The program was run on OpenCobolIDE\n      ******************************************************************\n       IDENTIFICATION DIVISION.\n       PROGRAM-ID. ANGLE-PRIMES.\n\n       ENVIRONMENT DIVISION.\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       77  ANTI-PRIMES-CTR              PIC 9(3) VALUE 0.\n       77  FACTORS-CTR                  PIC 9(3) VALUE 0.\n       77  WS-INTEGER                   PIC 9(5) VALUE 1.\n       77  WS-MAX                       PIC 9(5) VALUE 0.\n       77  WS-I                         PIc 9(5) VALUE 0.\n       77  WS-LIMIT                     PIC 9(5) VALUE 1.\n       77  WS-REMAINDER                 PIC 9(5).\n\n       01  OUT-HDR         PIC X(23)    VALUE 'SEQ ANTI-PRIME FACTORS'.\n       01  OUT-LINE.\n           05 OUT-SEQ      PIC 9(3).\n           05 FILLER       PIC X(3)     VALUE SPACES.\n           05 OUT-ANTI     PIC ZZZZ9.\n           05 FILLER       PIC X(4)     VALUE SPACES.\n           05 OUT-FACTORS  PIC ZZZZ9.\n\n       PROCEDURE DIVISION.\n       000-MAIN.\n           DISPLAY OUT-HDR.\n           PERFORM 100-GET-ANTI-PRIMES\n               VARYING WS-INTEGER FROM 1 By 1\n               UNTIL ANTI-PRIMES-CTR >= 20.\n           STOP RUN.\n\n       100-GET-ANTI-PRIMES.\n           SET FACTORS-CTR TO 0.\n           COMPUTE WS-LIMIT = 1 + WS-INTEGER ** .5.\n           PERFORM 200-COUNT-FACTORS\n               VARYING WS-I FROM 1 BY 1\n               UNTIL WS-I >= WS-LIMIT.\n           IF FACTORS-CTR > WS-MAX\n               ADD 1 TO ANTI-PRIMES-CTR\n               COMPUTE WS-MAX = FACTORS-CTR\n               MOVE ANTI-PRIMES-CTR TO OUT-SEQ\n               MOVE WS-INTEGER TO OUT-ANTI\n               MOVE FACTORS-CTR TO OUT-FACTORS\n               DISPLAY OUT-LINE\n           END-IF.\n\n       200-COUNT-FACTORS.\n           COMPUTE WS-REMAINDER =\n               FUNCTION MOD(WS-INTEGER WS-I).\n           IF WS-REMAINDER = ZERO\n               ADD 1 TO FACTORS-CTR\n               IF WS-INTEGER NOT = WS-I ** 2\n                   ADD 1 TO FACTORS-CTR\n               END-IF\n           END-IF.\n\n      ******************************************************************\n      *    OUTPUT:\n      ******************************************************************\n      *     SEQ ANTI-PRIME FACTORS\n      *     001       1        1\n      *     002       2        2\n      *     003       4        3\n      *     004       6        4\n      *     005      12        6\n      *     006      24        8\n      *     007      36        9\n      *     008      48       10\n      *     009      60       12\n      *     010     120       16\n      *     011     180       18\n      *     012     240       20\n      *     013     360       24\n      *     014     720       30\n      *     015     840       32\n      *     016    1260       36\n      *     017    1680       40\n      *     018    2520       48\n      *     019    5040       60\n      *     020    7560       64\n      ******************************************************************\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}