{
  "cobol_file": {
    "file_name": "successful_snippet_cb29b7f6.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID.  HAMMING.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01 POPCOUNT-VARIABLES.\n          03 POPCOUNT-IN       PIC 9(15)V9.\n          03 FILLER            REDEFINES POPCOUNT-IN.\n             05 POPCOUNT-REST  PIC 9(15).\n             05 FILLER         PIC 9.\n                88 BIT-IS-SET  VALUE 5.\n          03 POPCOUNT-OUT      PIC 99.\n          03 FILLER            REDEFINES POPCOUNT-OUT.\n             05 FILLER         PIC 9.\n             05 FILLER         PIC 9.\n                88 EVIL        VALUES 0, 2, 4, 6, 8.\n                88 ODIOUS      VALUES 1, 3, 5, 7, 9.\n\n       01 STATE-VARIABLES.\n          03 CUR-POWER-3       PIC 9(15) VALUE 1.\n          03 CUR-EVIL-NUM      PIC 99 VALUE 0.\n          03 CUR-ODIOUS-NUM    PIC 99 VALUE 0.\n          03 LINE-INDEX        PIC 99 VALUE 1.\n\n       01 OUTPUT-FORMAT.\n          03 LINENO            PIC Z9.\n          03 FILLER            PIC X VALUE '.'.\n          03 FILLER            PIC XX VALUE SPACES.\n          03 OUT-POW3          PIC Z9.\n          03 FILLER            PIC X(4) VALUE SPACES.\n          03 OUT-EVIL          PIC Z9.\n          03 FILLER            PIC X(4) VALUE SPACES.\n          03 OUT-ODIOUS        PIC Z9.\n\n       PROCEDURE DIVISION.\n       BEGIN.\n           DISPLAY \"     3^   EVIL   ODD\"\n           PERFORM MAKE-LINE 30 TIMES.\n           STOP RUN.\n      \n       MAKE-LINE.\n           MOVE LINE-INDEX TO LINENO.\n           MOVE CUR-POWER-3 TO POPCOUNT-IN.\n           PERFORM FIND-POPCOUNT.\n           MOVE POPCOUNT-OUT TO OUT-POW3.\n           PERFORM FIND-EVIL.\n           MOVE CUR-EVIL-NUM TO OUT-EVIL.\n           PERFORM FIND-ODIOUS.\n           MOVE CUR-ODIOUS-NUM TO OUT-ODIOUS.\n           DISPLAY OUTPUT-FORMAT.\n           MULTIPLY 3 BY CUR-POWER-3.\n           ADD 1 TO CUR-EVIL-NUM.\n           ADD 1 TO CUR-ODIOUS-NUM.\n           ADD 1 TO LINE-INDEX.\n \n       FIND-EVIL.\n           MOVE CUR-EVIL-NUM TO POPCOUNT-IN.\n           PERFORM FIND-POPCOUNT.\n           IF NOT EVIL, ADD 1 TO CUR-EVIL-NUM, GO TO FIND-EVIL.\n\n       FIND-ODIOUS.\n           MOVE CUR-ODIOUS-NUM TO POPCOUNT-IN.\n           PERFORM FIND-POPCOUNT.\n           IF NOT ODIOUS, ADD 1 TO CUR-ODIOUS-NUM, GO TO FIND-ODIOUS.\n       \n       FIND-POPCOUNT.\n           MOVE 0 TO POPCOUNT-OUT.\n           PERFORM PROCESS-BIT UNTIL POPCOUNT-IN IS EQUAL TO ZERO.\n\n       PROCESS-BIT.\n           DIVIDE 2 INTO POPCOUNT-IN.\n           IF BIT-IS-SET, ADD 1 TO POPCOUNT-OUT.\n           MOVE POPCOUNT-REST TO POPCOUNT-IN.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}