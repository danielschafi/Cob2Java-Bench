{
  "cobol_file": {
    "file_name": "successful_snippet_4cee33ec.cob",
    "content": "       >>SOURCE FREE\nIDENTIFICATION DIVISION.\nPROGRAM-ID. expand-range.\n\nDATA DIVISION.\nWORKING-STORAGE SECTION.\n01  comma-pos                           PIC 99 COMP VALUE 1.\n01  dash-pos                            PIC 99 COMP.\n01  end-num                             PIC S9(3).\n01  Max-Part-Len                        CONSTANT 10.\n01  num                                 PIC S9(3).\n01  edited-num                          PIC -(3)9.\n01  part                                PIC X(10).\n\n01  part-flag                           PIC X.\n    88 last-part                        VALUE \"Y\".\n\n01  range-str                           PIC X(80).\n01  Range-Str-Len                       CONSTANT 80.\n01  start-pos                           PIC 99 COMP.\n01  start-num                           PIC S9(3).\n\nPROCEDURE DIVISION.\n    ACCEPT range-str\n\n    PERFORM WITH TEST AFTER UNTIL last-part\n        UNSTRING range-str DELIMITED BY \",\" INTO part WITH POINTER comma-pos\n        PERFORM check-if-last\n\n        PERFORM find-range-dash\n\n        IF dash-pos > Max-Part-Len\n            PERFORM display-num\n        ELSE\n            PERFORM display-range\n        END-IF\n    END-PERFORM\n\n    DISPLAY SPACES\n\n    GOBACK\n    .\ncheck-if-last SECTION.\n    IF comma-pos > Range-Str-Len\n        SET last-part TO TRUE\n    END-IF\n    .\nfind-range-dash SECTION.\n    IF part (1:1) <> \"-\"\n        MOVE 1 TO start-pos\n    ELSE\n        MOVE 2 TO start-pos\n    END-IF\n\n    MOVE 1 TO dash-pos\n    INSPECT part (start-pos:) TALLYING dash-pos FOR CHARACTERS BEFORE \"-\"\n    COMPUTE dash-pos = dash-pos + start-pos - 1\n    .\ndisplay-num SECTION.\n    MOVE part TO edited-num\n    CALL \"display-edited-num\" USING CONTENT part-flag, edited-num\n    .\ndisplay-range SECTION.\n    MOVE part (1:dash-pos - 1) TO start-num\n    MOVE part (dash-pos + 1:) TO end-num\n\n    PERFORM VARYING num FROM start-num BY 1 UNTIL num = end-num\n        MOVE num TO edited-num\n        CALL \"display-edited-num\" USING CONTENT \"N\", edited-num\n    END-PERFORM\n\n    MOVE end-num TO edited-num\n    CALL \"display-edited-num\" USING CONTENT part-flag, edited-num\n    .\nEND PROGRAM expand-range.\n\n\nIDENTIFICATION DIVISION.\nPROGRAM-ID. display-edited-num.\n\nDATA DIVISION.\nLINKAGE SECTION.\n01  hide-comma-flag                     PIC X.\n    88  hide-comma                      VALUE \"Y\".\n01  edited-num                          PIC -(3)9.\n\nPROCEDURE DIVISION USING hide-comma-flag, edited-num.\n    DISPLAY FUNCTION TRIM(edited-num) NO ADVANCING\n    IF NOT hide-comma\n        DISPLAY \", \" NO ADVANCING\n    END-IF\n    .\nEND PROGRAM display-edited-num.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "1,3,7,9,11\n-6,-3,-1,1,4\n10-15,20-22,30\n-10--5,0-3,8-12\n1-3,5,7-9,11-15\n100,200-205,300\n-25--20,-10--5,0,5-10"
  }
}