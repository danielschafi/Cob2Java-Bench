{
  "cobol_file": {
    "file_name": "successful_snippet_fc34d039.cob",
    "content": "000010 @OPTIONS MAIN,TEST\n000020 Identification Division.\n000030 Program-Id.  Chapt21g.\n000040* Convert Local Time To Gmt\n000041 Environment Division.\n000050 Configuration Section.\n000051 Source-Computer.  IBM-PC.\n000056 Object-Computer.  IBM-PC.\n000147 Data Division.\n000148 Working-Storage Section.\n000157 01  Current-Date-Group.\n000167     03  Todays-Date.\n000177         05  Today-YYYY       Pic 9(4).\n000187         05  Today-MM         Pic 9(2).\n000197         05  Today-DD         Pic 9(2).\n000198     03  Todays-Date-N Redefines Todays-Date Pic 9(8).\n000207     03  Time-Now.\n000217         05  Time-Hour        Pic 99.\n000227         05  Time-Minutes     Pic 99.\n000237         05  Time-Seconds     Pic 99.\n000247         05  Time-Hundredths  Pic 99.\n000257     03  GMT-Offset.\n000258         05  GMT-Direction    Pic X.\n000259         05  GMT-Hours        Pic 99.\n000260         05  GMT-Minutes      Pic 99.\n000270 01  Display-Date.\n000280     03  Today-MM             Pic 9(2).\n000290     03  Filler               Pic X Value \"/\".\n000300     03  Today-DD             Pic 9(2).\n000310     03  Filler               Pic X Value \"/\".\n000320     03  Today-YYYY           Pic 9(4).\n000321 01  Display-Time.\n000322     03  Time-Hour            Pic 99.\n000323     03  Filler               Pic X Value \":\".\n000324     03  Time-Minutes         Pic 99.\n000325     03  Filler               Pic X Value \":\".\n000326     03  Time-Seconds         Pic 99.\n000330 01  Total-Seconds            Pic 9(15) Value Zeros.\n000340 01  Work-Number              Pic 9(15) Value Zeros.\n000350 01  Work-Remainder           Pic 9(15) Value Zeros.\n000390 01  GMT-Offset               Pic 9(15) Value Zeros.\n000391 Procedure Division.\n000392 Chapt21g.\n000401     Move Function Current-Date To Current-Date-Group\n000411* Convert Today To Seconds\n000421     Compute Work-Number =\n000422             Function Integer-Of-Date (Todays-Date-N)\n000431     Compute Total-Seconds = (Work-Number * 86400) +\n000441                             (Time-Hour Of Time-Now * 3600) +\n000451                             (Time-Minutes Of Time-Now * 60) +\n000461                             Time-Seconds Of Time-Now\n000471     Compute Work-Number = (GMT-Hours * 3600) +\n000481                           (GMT-Minutes * 60)\n000491* Remember We Need To Change By The Opposite Of The Direction From Gmt\n000492     If GMT-Direction = \"+\"\n000501        Subtract Work-Number From Total-Seconds\n000511     Else\n000521        Add Work-Number To Total-Seconds\n000531     End-If\n000541* Convert The Time In Seconds Back To A Date And Time\n000551     Divide Total-Seconds By 86400 Giving Work-Number\n000561                             Remainder Work-Remainder\n000571     Compute Todays-Date-N =\n000572             Function Date-Of-Integer (Work-Number)\n000581     Divide Work-Remainder By 3600 Giving Time-Hour Of Time-Now\n000591                              Remainder Work-Number\n000601     Divide Work-Number By 60 Giving Time-Minutes Of Time-Now\n000611                              Remainder Time-Seconds Of Time-Now\n000621     Move Corresponding Todays-Date To Display-Date\n000631     Move Corresponding Time-Now To Display-Time\n000641     Display \"Current GMT \" Display-Date \" \" Display-Time\n000651     Stop Run\n000661     .\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}