{
  "cobol_file": {
    "file_name": "successful_snippet_04c7987d.cob",
    "content": "      *=========================== TESTIO1 ============================*\n      * Authors: Brian D Pead\n      *\n      * License: MIT\n      *\n      * Description: Test reading a line sequential file. This shows\n      *     that GnuCOBOL treats CR/LF (on Windows) as end of record\n      *     markers, whereas those are ignored on regular sequential\n      *     files. Try removing the LINE from \"ORGANIZATION LINE \n      *     SEQUENTIAL\" to see the difference with the provided input\n      *     file.\n      *\n      * Date        Version  Description\n      * ----        -------  -----------\n      * 2020-02-08  1.0      First release\n      *================================================================*\n\n       IDENTIFICATION DIVISION.\n      *========================\n\n       PROGRAM-ID.                 TESTIO1.\n\n       ENVIRONMENT DIVISION.\n      *=====================\n\n       CONFIGURATION SECTION.\n      *----------------------\n\n       SOURCE-COMPUTER.\n           IBM-Z15.\n      *    IBM-Z15 DEBUGGING MODE.\n\n       INPUT-OUTPUT SECTION.\n      *---------------------\n\n       FILE-CONTROL.\n           SELECT TESTIO1I         ASSIGN 'Data\\TESTIO1.Input.txt'\n      *                             ORGANIZATION SEQUENTIAL. \n                                   ORGANIZATION LINE SEQUENTIAL. \n      /\n       DATA DIVISION.\n      *==============\n\n       FILE SECTION.\n      *-------------\n\n       FD  TESTIO1I\n           RECORD VARYING 1 TO 80  *> Comment out if switching to\n           DEPENDING W-RECLEN      *> ORGANIZATION SEQUENTIAL\n           .\n\n       01  TESTIO1I-REC            PIC X(80).\n\n       WORKING-STORAGE SECTION.\n      *------------------------\n\n       01  W-TESTIO1I-RECS         PIC 9(09)  COMP VALUE 0.\n       01  W-RECLEN                PIC S9(09) COMP.\n       01  W-DISP-NUM-1            PIC Z,ZZ9.\n       01  W-DISP-NUM-2            PIC Z9.\n\n       01  FILLER                  PIC X(01)       VALUE 'N'.\n           88  W-EOF                               VALUE 'Y'.\n\n       01  W-COMPILED-DATE.\n           05  W-COMPILED-DATE-YYYY\n                                   PIC X(04).\n           05  W-COMPILED-DATE-MM  PIC X(02).\n           05  W-COMPILED-DATE-DD  PIC X(02).\n           05  W-COMPILED-TIME-HH  PIC X(02).\n           05  W-COMPILED-TIME-MM  PIC X(02).\n           05  W-COMPILED-TIME-SS  PIC X(02).\n           05  FILLER              PIC X(07).\n      /\n       PROCEDURE DIVISION.\n      *===================\n\n       MAIN.\n      *-----\n\n           PERFORM SUB-1000-START-UP THRU SUB-1000-EXIT\n\n           PERFORM SUB-9100-READ-TESTIO1I THRU SUB-9100-EXIT\n\n           PERFORM SUB-2000-PROCESS THRU SUB-2000-EXIT\n               UNTIL W-EOF\n\n           PERFORM SUB-3000-SHUT-DOWN THRU SUB-3000-EXIT\n           .\n       MAIN-EXIT.\n           STOP RUN.\n      /\n       SUB-1000-START-UP.\n      *------------------\n\n           MOVE FUNCTION WHEN-COMPILED \n                                   TO W-COMPILED-DATE\n\n           DISPLAY 'TESTIO1 compiled on '\n               W-COMPILED-DATE-YYYY '/'\n               W-COMPILED-DATE-MM   '/'\n               W-COMPILED-DATE-DD   ' at '\n               W-COMPILED-TIME-HH   ':'\n               W-COMPILED-TIME-MM   ':'\n               W-COMPILED-TIME-SS\n\n           OPEN INPUT TESTIO1I\n           .\n       SUB-1000-EXIT.\n           EXIT.\n      /\n       SUB-2000-PROCESS.\n      *-----------------\n\n           MOVE W-TESTIO1I-RECS    TO W-DISP-NUM-1\n           MOVE W-RECLEN           TO W-DISP-NUM-2\n\n           DISPLAY 'Record '\n                   W-DISP-NUM-1\n                   ' ('\n                   W-DISP-NUM-2\n                   ' bytes): '\n                   TESTIO1I-REC\n\n           PERFORM SUB-9100-READ-TESTIO1I THRU SUB-9100-EXIT\n           .\n       SUB-2000-EXIT.\n           EXIT.\n      /\n       SUB-3000-SHUT-DOWN.\n      *-------------------\n      \n           CLOSE TESTIO1I\n\n           MOVE W-TESTIO1I-RECS    TO W-DISP-NUM-1\n           DISPLAY 'TESTIO1I records read: '\n                   W-DISP-NUM-1\n           DISPLAY 'TESTIO1 Completed'\n           .\n       SUB-3000-EXIT.\n           EXIT.\n      /\n       SUB-9100-READ-TESTIO1I.\n      *-----------------------\n      \n           READ TESTIO1I\n               AT END\n                   SET  W-EOF      TO TRUE\n               NOT AT END\n                   ADD  1          TO W-TESTIO1I-RECS\n           END-READ\n           .\n       SUB-9100-EXIT.\n           EXIT.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}