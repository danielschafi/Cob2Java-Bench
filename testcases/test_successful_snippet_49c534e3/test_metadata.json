{
  "cobol_file": {
    "file_name": "successful_snippet_49c534e3.cob",
    "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs01b.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(008).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs01b'.\n\n       01  WORK-AREAS.\n           05  WS-COUNT           PIC 9(008) COMP    VALUE 0.\n           05  WS-REC-COUNT       PIC 9(008) COMP    VALUE 0.\n           05  DEPTH-LEN          PIC 9(008) COMP    VALUE 0.\n           05  DEPTH-INDX-MAX     PIC 9(008) COMP    VALUE 0.\n           05  SLIDING-COUNT      PIC 9(008) COMP    VALUE 0.\n           05  WHICH-INDX         PIC 9(008) COMP    VALUE 0.\n           05  WHICH-INDX-R       PIC 9(008) COMP    VALUE 0.\n           05  HOLD-DEPTH         PIC 9(008)         VALUE 0.\n           05  CURR-DEPTH         PIC 9(008)         VALUE 0.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT-DEPTH      PIC X(008)         VALUE SPACES.\n           05  WS-INPT-DEPTH-R\n               REDEFINES\n               WS-INPT-DEPTH.\n               10                 PIC X(003).\n               10  WS-INPT-BYTE-4 PIC X(001).\n               10                 PIC X(004).\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n\n       01  SLIDING-WINDOW-TABLE.\n          05  DEPTH-TBL\n              OCCURS 2000\n              INDEXED\n                DEPTH-INDX\n                DEPTH-INDX1\n                DEPTH-INDX2\n                DEPTH-INDX3.\n              10  SLIDING-DEPTH   PIC 9(008) COMP.\n\n       Procedure Division.\n           ACCEPT PROCESS-TYPE FROM COMMAND-LINE\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n           SET DEPTH-INDX1 TO 1\n           SET DEPTH-INDX2 TO 2\n           SET DEPTH-INDX3 TO 3\n\n           PERFORM UNTIL INPT-DATA-EOF\n             ADD 1 TO SLIDING-COUNT\n             EVALUATE SLIDING-COUNT\n               WHEN 1 PERFORM 1010-INCREMENT-GROUP-1\n               WHEN 2 PERFORM 1020-INCREMENT-GROUP-2\n               WHEN 3 PERFORM 1030-INCREMENT-GROUP-3\n                      ADD 1 TO DEPTH-INDX-MAX\n               WHEN OTHER\n                          ADD 1 TO DEPTH-INDX-MAX\n                          DIVIDE SLIDING-COUNT BY 3\n                            GIVING WHICH-INDX\n                            REMAINDER WHICH-INDX-R\n                          EVALUATE WHICH-INDX-R\n                            WHEN 1 SET DEPTH-INDX1 UP BY 3\n                            WHEN 2 SET DEPTH-INDX2 UP BY 3\n                            WHEN 0 SET DEPTH-INDX3 UP BY 3\n                          END-EVALUATE\n                          IF DEPTH-INDX1 > 2000\n                              DISPLAY MYNAME ' internal table overflow'\n                              CLOSE INPT-DATA\n                              MOVE 8 TO RETURN-CODE\n                              GOBACK\n                          END-IF\n                          PERFORM 1030-INCREMENT-GROUP-3\n             END-EVALUATE\n             PERFORM 8010-READ-INPT-DATA\n           END-PERFORM\n\n           CLOSE INPT-DATA\n\n           MOVE SLIDING-DEPTH(1) TO HOLD-DEPTH\n\n           PERFORM VARYING DEPTH-INDX FROM 1 BY 1\n           UNTIL DEPTH-INDX > DEPTH-INDX-MAX\n             IF SLIDING-DEPTH(DEPTH-INDX) > HOLD-DEPTH\n                 ADD 1 TO WS-COUNT\n             END-IF\n             MOVE SLIDING-DEPTH(DEPTH-INDX) TO HOLD-DEPTH\n           END-PERFORM\n\n           DISPLAY\n            MYNAME\n            ' measurements larger than the previous measurement '\n             WS-COUNT\n\n           DISPLAY MYNAME ' records read ' WS-REC-COUNT\n           DISPLAY MYNAME ' sliding window groups ' DEPTH-INDX-MAX\n\n           IF PROCESS-TEST\n               RESET TRACE\n               PERFORM VARYING DEPTH-INDX FROM 1 BY 1\n               UNTIL DEPTH-INDX > DEPTH-INDX-MAX\n                 DISPLAY DEPTH-INDX ' ' SLIDING-DEPTH(DEPTH-INDX)\n               END-PERFORM\n           END-IF\n\n           GOBACK.\n\n       1010-INCREMENT-GROUP-1.\n           ADD CURR-DEPTH TO SLIDING-DEPTH(DEPTH-INDX1)\n           .\n\n       1020-INCREMENT-GROUP-2.\n           PERFORM 1010-INCREMENT-GROUP-1\n           ADD CURR-DEPTH TO SLIDING-DEPTH(DEPTH-INDX2)\n           .\n\n       1030-INCREMENT-GROUP-3.\n           PERFORM 1020-INCREMENT-GROUP-2\n           ADD CURR-DEPTH TO SLIDING-DEPTH(DEPTH-INDX3)\n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n               IF WS-INPT-BYTE-4 NUMERIC\n                   MOVE 4 TO DEPTH-LEN\n               ELSE\n                   MOVE 3 TO DEPTH-LEN\n               END-IF\n               MOVE FUNCTION NUMVAL(WS-INPT-DEPTH(1:DEPTH-LEN))\n                 TO CURR-DEPTH\n           END-READ\n\n           .\n\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "PROD\nTEST\nDEV \nQUAL\nPROD"
  }
}