{
  "cobol_file": {
    "file_name": "successful_snippet_10b6ffdf.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID.                      BUBBLESORT.\n       AUTHOR.                          DAVE STRATFORD.\n       DATE-WRITTEN.                    MARCH 2010.\n       INSTALLATION.                    HEXAGON SYSTEMS LIMITED.\n\n       ENVIRONMENT DIVISION.\n       CONFIGURATION SECTION.\n       SOURCE-COMPUTER.                 ICL VME.\n       OBJECT-COMPUTER.                 ICL VME.\n\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n           SELECT FA-INPUT-FILE  ASSIGN FL01.\n           SELECT FB-OUTPUT-FILE ASSIGN FL02.\n\n       DATA DIVISION.\n       FILE SECTION.\n\n       FD  FA-INPUT-FILE.\n       01  FA-INPUT-REC.\n         03  FA-DATA                    PIC S9(6).\n\n       FD  FB-OUTPUT-FILE.\n       01  FB-OUTPUT-REC                PIC S9(6).\n\n       WORKING-STORAGE SECTION.\n       01  WA-IDENTITY.\n         03  WA-PROGNAME                PIC X(10) VALUE \"BUBBLESORT\".\n         03  WA-VERSION                 PIC X(6) VALUE \"000001\".\n\n       01  WB-TABLE.\n         03  WB-ENTRY                   PIC 9(8) COMP SYNC OCCURS 100000\n                                                  INDEXED BY WB-IX-1.\n\n       01  WC-VARS.\n         03  WC-SIZE                    PIC S9(8) COMP SYNC.\n         03  WC-TEMP                    PIC S9(8) COMP SYNC.\n         03  WC-END                     PIC S9(8) COMP SYNC.\n         03  WC-LAST-CHANGE             PIC S9(8) COMP SYNC.\n\n       01  WF-CONDITION-FLAGS.\n         03  WF-EOF-FLAG                PIC X.\n           88  END-OF-FILE              VALUE \"Y\".\n         03  WF-EMPTY-FILE-FLAG         PIC X.\n           88  EMPTY-FILE               VALUE \"Y\".\n\n       PROCEDURE DIVISION.\n       A-MAIN SECTION.\n       A-000.\n           PERFORM B-INITIALISE.\n           IF NOT EMPTY-FILE\n              PERFORM C-SORT.\n           PERFORM D-FINISH.\n\n       A-999.\n           STOP RUN.\n\n       B-INITIALISE SECTION.\n       B-000.\n           DISPLAY \"*** \" WA-PROGNAME \" VERSION \"\n                          WA-VERSION \" STARTING ***\".\n\n           MOVE ALL \"N\" TO WF-CONDITION-FLAGS.\n           OPEN INPUT FA-INPUT-FILE.\n           SET WB-IX-1 TO 0.\n\n           READ FA-INPUT-FILE AT END MOVE \"Y\" TO WF-EOF-FLAG\n                                                 WF-EMPTY-FILE-FLAG.\n\n           PERFORM BA-READ-INPUT UNTIL END-OF-FILE.\n\n           CLOSE FA-INPUT-FILE.\n\n           SET WC-SIZE TO WB-IX-1.\n\n       B-999.\n           EXIT.\n\n       BA-READ-INPUT SECTION.\n       BA-000.\n           SET WB-IX-1 UP BY 1.\n           MOVE FA-DATA TO WB-ENTRY(WB-IX-1).\n\n           READ FA-INPUT-FILE AT END MOVE \"Y\" TO WF-EOF-FLAG.\n\n       BA-999.\n           EXIT.\n\n       C-SORT SECTION.\n       C-000.\n           DISPLAY \"SORT STARTING\".\n\n           MOVE WC-SIZE TO WC-END.\n           PERFORM E-BUBBLE UNTIL WC-END = 1.\n\n           DISPLAY \"SORT FINISHED\".\n\n       C-999.\n           EXIT.\n\n       D-FINISH SECTION.\n       D-000.\n           OPEN OUTPUT FB-OUTPUT-FILE.\n           SET WB-IX-1 TO 1.\n\n           PERFORM DA-WRITE-OUTPUT UNTIL WB-IX-1 > WC-SIZE.\n\n           CLOSE FB-OUTPUT-FILE.\n\n           DISPLAY \"*** \" WA-PROGNAME \" FINISHED ***\".\n\n       D-999.\n           EXIT.\n\n       DA-WRITE-OUTPUT SECTION.\n       DA-000.\n           WRITE FB-OUTPUT-REC FROM WB-ENTRY(WB-IX-1).\n           SET WB-IX-1 UP BY 1.\n\n       DA-999.\n           EXIT.\n\n       E-BUBBLE SECTION.\n       E-000.\n           MOVE 1 TO WC-LAST-CHANGE.\n\n           PERFORM F-PASS VARYING WB-IX-1 FROM 1 BY 1\n                          UNTIL WB-IX-1 = WC-END.\n\n           MOVE WC-LAST-CHANGE TO WC-END.\n\n       E-999.\n           EXIT.\n\n       F-PASS SECTION.\n       F-000.\n           IF WB-ENTRY(WB-IX-1) > WB-ENTRY(WB-IX-1 + 1)\n              SET  WC-LAST-CHANGE        TO WB-IX-1\n              MOVE WB-ENTRY(WB-IX-1)     TO WC-TEMP\n              MOVE WB-ENTRY(WB-IX-1 + 1) TO WB-ENTRY(WB-IX-1)\n              MOVE WC-TEMP               TO WB-ENTRY(WB-IX-1 + 1).\n\n       F-999.\n           EXIT.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}