{
  "cobol_file": {
    "file_name": "successful_snippet_318f5817.cob",
    "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs02b.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(024).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs02b'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(008) COMP    VALUE 0.\n           05  CURR-DEPTH         PIC 9(008) COMP    VALUE 0.\n           05  CURR-HPOSN         PIC 9(008) COMP    VALUE 0.\n           05  CURR-AIM           PIC 9(008) COMP    VALUE 0.\n           05  CURR-PRODUCT       PIC 9(016) COMP    VALUE 0.\n           05  OPERATION-ARG      PIC 9(008) COMP    VALUE 0.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n           05  OPERATION          PIC X(008)         VALUE LOW-VALUES.\n           05  OPERATION-ARG-X    PIC X(008)         VALUE LOW-VALUES.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT            PIC X(024)         VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n           05  OPERATION-SW       PIC X(008)         VALUE LOW-VALUES.\n               88  OPERATION-FORWARD                 VALUE 'FORWARD'.\n               88  OPERATION-DOWN                    VALUE 'DOWN'.\n               88  OPERATION-UP                      VALUE 'UP'.\n\n       Procedure Division.\n           ACCEPT PROCESS-TYPE FROM COMMAND-LINE\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM UNTIL INPT-DATA-EOF\n             EVALUATE TRUE\n               WHEN OPERATION-FORWARD\n                    ADD OPERATION-ARG TO CURR-HPOSN\n                    COMPUTE CURR-DEPTH =\n                      CURR-DEPTH + (OPERATION-ARG * CURR-AIM)\n               WHEN OPERATION-DOWN\n                    ADD OPERATION-ARG TO CURR-AIM\n               WHEN OPERATION-UP\n                    SUBTRACT OPERATION-ARG FROM CURR-AIM\n               WHEN OTHER\n                          DISPLAY\n                            MYNAME ' invalid operation ' OPERATION-SW\n             END-EVALUATE\n             PERFORM 8010-READ-INPT-DATA\n           END-PERFORM\n\n           CLOSE INPT-DATA\n\n           DISPLAY\n            MYNAME\n            ' current horizontal position '\n            CURR-HPOSN\n\n           DISPLAY\n            MYNAME\n            ' current depth '\n            CURR-DEPTH\n\n           COMPUTE CURR-PRODUCT = CURR-HPOSN * CURR-DEPTH\n           DISPLAY\n            MYNAME\n            ' product of position and depth '\n            CURR-PRODUCT\n\n           DISPLAY MYNAME ' records read ' WS-REC-COUNT\n\n           GOBACK.\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n               UNSTRING WS-INPT\n                 DELIMITED ALL SPACE OR ALL LOW-VALUE\n                 INTO OPERATION OPERATION-ARG-X\n               END-UNSTRING\n               MOVE FUNCTION NUMVAL(OPERATION-ARG-X)\n                 TO OPERATION-ARG\n               MOVE FUNCTION UPPER-CASE(OPERATION)\n                 TO OPERATION-SW\n           END-READ\n\n           .\n\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "PROD\nTEST\nDEV \nQUAL\nPROD"
  }
}