{
  "cobol_file": {
    "file_name": "successful_snippet_3a4b432c.cob",
    "content": "       IDENTIFICATION DIVISION.\n      *=======================*\n       PROGRAM-ID.   CGPRG008.\n      *AUTHOR.       MIGUEL MORAIS.\n      *DATE-WRITTEN. 08/10/2019.\n      *--------------------------------------------------------------*\n      * DISCIPLINA: PROGRAMACAO MAINFRAME\n      *--------------------------------------------------------------*\n      * OBJETIVO: RECEBER DADOS DA SYSIN(ACCEPT)\n      *           CALCULAR A MEDIA ARITMETICA BIMESTRAL\n      *           GRAVAR ARQUIVO FISICO SEQUENCIAL(WRITE)\n      *--------------------------------------------------------------*\n      *------------------> HISTORICO - MANUTENCAO <------------------*\n      * VERSAO  MES/ANO  NR.DOC  IDENT.  DESCRICAO\n      * ------  -------  ------  ------  -------------------------   *\n      *  V01    FEV/2013 010002  SISTEMA GERA ARQUIVO SEQUENCIAL     *\n      *--------------------------------------------------------------*\n       ENVIRONMENT DIVISION.\n      *====================*\n       CONFIGURATION SECTION.\n      *---------------------*\n       SPECIAL-NAMES.\n           DECIMAL-POINT IS COMMA\n           CURRENCY SIGN IS \"R$ \" WITH PICTURE SYMBOL \"$\"\n           .\n       INPUT-OUTPUT SECTION.\n      *---------------------*\n       FILE-CONTROL.\n           SELECT CADALU ASSIGN TO CADALUJ\n                  FILE STATUS   IS WS-FS-ALU\n           .\n       DATA DIVISION.\n      *=============*\n       FILE SECTION.\n      *------------*\n       FD  CADALU\n           LABEL RECORD STANDARD\n           RECORDING MODE  F\n           .\n       01  REG-CADALU             PIC X(70)\n           .\n       WORKING-STORAGE SECTION.\n      *-----------------------*\n       01  FILLER                 PIC X(35)        VALUE\n           '**** INICIO DA WORKING-STORAGE ****'.\n\n      *-----> VARIAVEIS AUXILIARES UTILIZADA NO PROCESSAMENTO\n       01  WS-AREA-AUX.\n           05  WS-FIM                 PIC X(01).\n           05  WS-CTLIDO              PIC 9(02).\n           05  WS-CTGRAV              PIC 9(02).\n           05  WS-FS-ALU              PIC X(02).\n           05  WS-MSG                 PIC X(30).\n           05  WS-FS-MSG              PIC X(02).\n           05  WS-MEDIA               PIC 9(02)V99.\n      *-----> ENTRADA - DADOS VIA SYSIN (NO JCL DE EXECUCAO)\n       01  WS-REG-SYSIN.\n           05 WS-NUMERO-IN        PIC 9(04).\n           05 WS-NOME-IN          PIC X(20).\n           05 WS-SEXO-IN          PIC X(01).\n           05 WS-IDADE-IN         PIC 9(02).\n           05 WS-CURSO-IN         PIC X(12).\n           05 WS-NOTA1-IN         PIC 9(02)V99.\n           05 WS-NOTA2-IN         PIC 9(02)V99.\n\n      *-----> SAIDA - ARQ. FISICO SEQUENCIAL\n       01  WS-REG-CADALU.\n           05 WS-NUMERO-S         PIC 9(04).\n           05 WS-NOME-S           PIC X(20).\n           05 WS-SEXO-S           PIC X(01).\n           05 WS-IDADE-S          PIC 9(02).\n           05 WS-CURSO-S          PIC X(12).\n           05 WS-NOTA1-S          PIC 9(02)V99.\n           05 WS-NOTA2-S          PIC 9(02)V99.\n           05 WS-MEDIA-S          PIC 9(02)V99.\n           05 FILLER              PIC X(19)       VALUE SPACES.\n\n      *-----> SAIDA - SYSOUT\n       01  WS-REG-SYSOUT.\n           05 WS-NUMERO-O         PIC 9(04).\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-NOME-O           PIC X(20).\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-SEXO-O           PIC X(01).\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-IDADE-O          PIC 9(02).\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-CURSO-O          PIC X(12).\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-NOTA1-O          PIC Z9,99.\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-NOTA2-O          PIC Z9,99.\n           05 FILLER              PIC X(01) VALUE SPACES.\n           05 WS-MEDIA-O          PIC Z9,99.\n\n       01  WS-HIFEN               PIC X(80)       VALUE ALL '-'.\n\n       01  FILLER                 PIC X(35)        VALUE\n           '****** FIM DA WORKING-STORAGE *****'.\n      *\n       PROCEDURE DIVISION.\n      *==================*\n      *--------------------------------------------------------------*\n      *    PROCESSO PRINCIPAL\n      *--------------------------------------------------------------*\n       000-RSPRG003.\n\n           PERFORM 010-INICIAR\n           PERFORM 030-PROCESSAR UNTIL WS-FIM = 'S'\n           PERFORM 090-TERMINAR\n           STOP RUN\n           .\n      *--------------------------------------------------------------*\n      *    PROCEDIMENTOS INICIAIS\n      *--------------------------------------------------------------*\n       010-INICIAR.\n\n           OPEN OUTPUT CADALU\n           IF WS-FS-ALU  NOT = '00'\n              MOVE  'ERRO AO ABRIR O CADALU'  TO WS-MSG\n              MOVE   WS-FS-ALU                TO WS-FS-MSG\n              GO TO  999-ERRO\n           END-IF\n\n           PERFORM 025-LER-SYSIN\n           .\n      *--------------------------------------------------------------*\n      *    LEITURA DADOS DA SYSIN\n      *--------------------------------------------------------------*\n       025-LER-SYSIN.\n\n           ACCEPT WS-REG-SYSIN  FROM SYSIN\n\n           IF WS-REG-SYSIN = ALL '9'\n              MOVE   'S'     TO  WS-FIM\n           ELSE\n              ADD 1  TO WS-CTLIDO\n           END-IF\n           .\n      *--------------------------------------------------------------*\n      *    PROCESSAR DADOS RECEBIDOS DA SYSIN ATE FIM DOS REGISTROS\n      *--------------------------------------------------------------*\n       030-PROCESSAR.\n\n           COMPUTE WS-MEDIA = (WS-NOTA1-IN + WS-NOTA2-IN) / 2\n           MOVE WS-REG-SYSIN   TO   WS-REG-CADALU\n           MOVE WS-MEDIA       TO   WS-MEDIA-S\n           WRITE REG-CADALU   FROM  WS-REG-CADALU\n           IF  WS-FS-ALU  NOT = '00'\n               MOVE 'ERRO NA GRAVACAO DO CADALUN'  TO WS-MSG\n               MOVE  WS-FS-ALU                     TO WS-FS-MSG\n               GO TO 999-ERRO\n           ELSE\n               ADD 1 TO WS-CTGRAV\n           END-IF\n\n           MOVE WS-NUMERO-S  TO WS-NUMERO-O\n           MOVE WS-NOME-S    TO WS-NOME-O\n           MOVE WS-SEXO-S    TO WS-SEXO-O\n           MOVE WS-IDADE-S   TO WS-IDADE-O\n           MOVE WS-CURSO-S   TO WS-CURSO-O\n           MOVE WS-NOTA1-S   TO WS-NOTA1-O\n           MOVE WS-NOTA2-S   TO WS-NOTA2-O\n           MOVE WS-MEDIA-S   TO WS-MEDIA-O\n           DISPLAY WS-REG-SYSOUT\n           PERFORM 025-LER-SYSIN\n           .\n      *--------------------------------------------------------------*\n      *    PROCEDIMENTOS FINAIS\n      *--------------------------------------------------------------*\n       090-TERMINAR.\n\n           DISPLAY ' *========================================*'\n           DISPLAY ' *   TOTAIS DE CONTROLE - RSPRG003        *'\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * REGISTROS LIDOS    - SYSIN  = ' WS-CTLIDO\n           DISPLAY ' * REGISTROS GRAVADOS - CADALU = ' WS-CTGRAV\n           DISPLAY ' *========================================*'\n\n           CLOSE  CADALU\n           IF WS-FS-ALU  NOT = '00'\n              MOVE  'ERRO AO FECHAR O CADALU'  TO WS-MSG\n              MOVE   WS-FS-ALU                 TO WS-FS-MSG\n              GO TO  999-ERRO\n           END-IF\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *      TERMINO NORMAL DO RSPRG003        *'\n           DISPLAY ' *----------------------------------------*'\n           .\n      *--------------------------------------------------------------*\n      *    ROTINA DE ERRO\n      *--------------------------------------------------------------*\n       999-ERRO.\n\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *           PROGRAMA CANCELADO           *'\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' * MENSAGEM    = ' WS-MSG\n           DISPLAY ' * FILE STATUS = ' WS-FS-MSG\n           DISPLAY ' *----------------------------------------*'\n           DISPLAY ' *       TERMINO ANORMAL DO RSPRG003      *'\n           DISPLAY ' *----------------------------------------*'\n           STOP RUN\n           .\n      *---------------> FIM DO PROGRAMA XXPRG002 <-------------------*\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "0001Maria Silva         F18Informatica  0850\n0002Jo\u00e3o Santos        M20Engenharia   0975\n0003Ana Costa          F19Administracao0785\n0004Pedro Oliveira     M21Informatica  0990\n0005Carla Souza        F22Engenharia   0865\n0006Lucas Ferreira     M19Administracao0780\n0007Julia Almeida      F20Informatica  0895\n9999999999999999999999999999999999999999999"
  }
}