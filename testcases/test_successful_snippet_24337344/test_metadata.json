{
  "cobol_file": {
    "file_name": "successful_snippet_24337344.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. \"SSS\".\n      *AUTHOR. ERIC CONRAD.\n      *DESCRIPTION. A program to solve a triangle given its three\n      *    sides.\n      *HISTORY.\n      *    4Jan2021 - Apply rounding in computations.\n\n       ENVIRONMENT DIVISION.\n       CONFIGURATION SECTION.\n       REPOSITORY.\n           FUNCTION ALL INTRINSIC.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       77  X                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  Y                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  Z                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  XI                           PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  ZETA                         PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n \n      * Sides\n       77  A                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  B                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  C                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n\n      * Semiperimeter\n       77  S                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n      * Angles\n       77  ALPHA                        PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  BETA                         PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  GAMMA                        PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  XVAL                         PIC Z(5).9(4)-.\n       77  INPUT-PROMPT                 PIC X(72).\n\n       01  OUTPUT-1.\n           02  OLABEL                       PIC X(15)\n                   JUSTIFIED RIGHT.\n           02  AVAL                         PIC Z(10).9(4).\n           02  BVAL                         PIC Z(10).9(4).\n           02  CVAL                         PIC Z(10).9(4).\n           02  OUNITS                       PIC X(15)\n                   JUSTIFIED RIGHT.\n\n       PROCEDURE DIVISION.\n       000-MAIN.\n           MOVE \"Enter the length of side a:\" TO INPUT-PROMPT.\n           PERFORM 100-GET-SIDE.\n           MOVE X TO A.\n\n           MOVE \"Enter the length of side b:\" TO INPUT-PROMPT.\n           PERFORM 100-GET-SIDE.\n           MOVE X TO B.\n\n           MOVE \"Enter the length of side c:\" TO INPUT-PROMPT.\n           PERFORM 100-GET-SIDE.\n           MOVE X TO C.\n\n           COMPUTE S ROUNDED = (A + B + C) / 2\n               ON SIZE ERROR\n                   DISPLAY \"Semiperimeter: Arithmetic overflow\"\n                   STOP RUN.\n           IF MAX(A, B, C) IS GREATER THAN S THEN\n               DISPLAY \"The triangle inequality is violated...\"\n               DISPLAY \"The triangle has no real solution.\"\n               STOP RUN\n           END-IF.\n\n           MOVE A TO X.\n           MOVE B TO Y.\n           MOVE C TO Z.\n           PERFORM 200-LAW-OF-COSINES.\n           MOVE ZETA TO GAMMA.\n\n           MOVE B TO X.\n           MOVE C TO Y.\n           MOVE A TO Z.\n           PERFORM 200-LAW-OF-COSINES.\n           MOVE ZETA TO ALPHA.\n\n           MOVE C TO X.\n           MOVE A TO Y.\n           MOVE B TO Z.\n           PERFORM 200-LAW-OF-COSINES.\n           MOVE ZETA TO BETA.\n\n           DISPLAY \"Solution:\".\n\n           MOVE \"Sides:\" TO OLABEL.\n           MOVE A TO AVAL.\n           MOVE B TO BVAL.\n           MOVE C TO CVAL.\n           MOVE SPACES TO OUNITS.\n           DISPLAY OUTPUT-1.\n\n           MOVE \"Angles:\" TO OLABEL.\n           MOVE ALPHA TO AVAL.\n           MOVE BETA TO BVAL.\n           MOVE GAMMA TO CVAL.\n           MOVE \"radians\" TO OUNITS.\n           DISPLAY OUTPUT-1.\n\n           MOVE SPACES TO OLABEL.\n           MOVE ALPHA TO XI.\n           PERFORM 300-TO-DEGREES.\n           MOVE ZETA TO AVAL.\n           MOVE BETA TO XI.\n           PERFORM 300-TO-DEGREES.\n           MOVE ZETA TO BVAL.\n           MOVE GAMMA TO XI.\n           PERFORM 300-TO-DEGREES.\n           MOVE ZETA TO CVAL.\n           MOVE \"degrees\" TO OUNITS.\n           DISPLAY OUTPUT-1.\n\n           GOBACK.\n\n       100-GET-SIDE.\n           DISPLAY INPUT-PROMPT.\n           ACCEPT X.\n           MOVE X TO XVAL.\n           DISPLAY \"  entered: \" XVAL.\n           IF X IS NOT GREATER THAN 0.0 THEN\n               DISPLAY \"  lengths must be positive, try again...\"\n               GO TO 100-GET-SIDE\n           END-IF.\n\n       200-LAW-OF-COSINES.\n           COMPUTE ZETA ROUNDED =\n               ACOS((X * X + Y * Y - Z * Z) / (2 * X * Y)).\n\n       300-TO-DEGREES.\n           COMPUTE ZETA ROUNDED = 180 * XI / PI.\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "+00012.34567890\n+00025.00000000\n+00100.50000000\n+00007.12345678\n-00015.75000000\n+00050.00000000\n+00000.00000000"
  }
}