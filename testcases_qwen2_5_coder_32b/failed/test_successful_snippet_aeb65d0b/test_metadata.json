{
  "cobol_file": {
    "file_name": "successful_snippet_aeb65d0b.cob",
    "content": "      *========================== TESTIO2 =============================*\n      * Authors: Brian D Pead\n      *\n      * License: MIT\n      *\n      * Description: Test writing a variable length record \n      *     sequential file. This shows that GnuCOBOL creates RDWs \n      *     similar to the IBM mainframe (when COB_VARSEQ_FORMAT=0, the\n      *     default), but the RDW record length does not include itself,\n      *     i.e. it is 4 less than the mainframe.\n      *\n      *     Environment name: COB_VARSEQ_FORMAT\n      *     Parameter name:   varseq_format\n      *     Purpose:          Declare format used for variable length\n      *                       sequential files\n      *                       - different types and lengths precede each\n      *                         record\n      *                       - length is the data length, does not\n      *                         include the prefix\n      *     Type: 0 means 2 byte record length (big-endian) + 2 NULs\n      *           1 means 4 byte record length (big-endian)\n      *           2 means 4 byte record length (local machine int)\n      *           3 means 2 byte record length (big-endian)\n      *     Default: 0\n      *\n      * Date        Version  Description\n      * ----        -------  -----------\n      * 2020-02-08  1.0      First release\n      *================================================================*\n\n       IDENTIFICATION DIVISION.\n      *========================\n\n       PROGRAM-ID.                 TESTIO2.\n\n       ENVIRONMENT DIVISION.\n      *=====================\n\n       CONFIGURATION SECTION.\n      *----------------------\n\n       SOURCE-COMPUTER.\n           IBM-Z15.\n      *    IBM-Z15 DEBUGGING MODE.\n\n       INPUT-OUTPUT SECTION.\n      *---------------------\n\n       FILE-CONTROL.\n           SELECT TESTIO2O         ASSIGN 'Data\\TESTIO2.Output.bin'\n                                   ORGANIZATION SEQUENTIAL. \n      /\n       DATA DIVISION.\n      *==============\n\n       FILE SECTION.\n      *-------------\n\n       FD  TESTIO2O\n           RECORD VARYING 25 TO 15000\n           DEPENDING W-RECLEN.\n\n       01  TESTIO2O-REC.\n           05  TO-CHAR             PIC X           OCCURS 25 TO 15000\n                                                   DEPENDING W-RECLEN\n                                                   INDEXED TO-DX.\n\n       WORKING-STORAGE SECTION.\n      *------------------------\n\n       01  W-TESTIO2O-RECS         PIC 9(09)  COMP VALUE 0.\n       01  W-RANDOM-NO             PIC V9(09) COMP.\n       01  W-RECLEN                PIC S9(09) COMP.\n       01  W-RECLEN-MIN            PIC S9(09) COMP VALUE 25.\n       01  W-RECLEN-MAX            PIC S9(09) COMP VALUE 15000.\n       01  W-OUTPUT-MIN            PIC 9(09)  COMP VALUE 999999999.\n       01  W-OUTPUT-MAX            PIC 9(09)  COMP VALUE 0.\n       01  W-QUOTIENT              PIC 9(09)  COMP.\n       01  W-REMAINDER             PIC 9(09)  COMP.\n       01  W-DISP-NUM              PIC ZZ,ZZZ,ZZ9.\n\n       01  W-ERROR-MSG             PIC X(21)       VALUE\n           '**** TESTIO2 error: '.\n\n       01  W-COMPILED-DATE.\n           05  W-COMPILED-DATE-YYYY\n                                   PIC X(04).\n           05  W-COMPILED-DATE-MM  PIC X(02).\n           05  W-COMPILED-DATE-DD  PIC X(02).\n           05  W-COMPILED-TIME-HH  PIC X(02).\n           05  W-COMPILED-TIME-MM  PIC X(02).\n           05  W-COMPILED-TIME-SS  PIC X(02).\n           05  FILLER              PIC X(07).\n\n       01  W-CHAR-BIN              PIC S9(04) COMP.\n       01  FILLER REDEFINES W-CHAR-BIN.\n           05  FILLER              PIC X.\n           05  W-CHAR              PIC X.\n      /\n       PROCEDURE DIVISION.\n      *===================\n\n       MAIN.\n      *-----\n\n           PERFORM SUB-1000-START-UP THRU SUB-1000-EXIT\n\n           PERFORM SUB-2000-PROCESS THRU SUB-2000-EXIT\n               UNTIL W-TESTIO2O-RECS >= 12345\n\n           PERFORM SUB-3000-SHUT-DOWN THRU SUB-3000-EXIT\n           .\n       MAIN-EXIT.\n           STOP RUN.\n      /\n       SUB-1000-START-UP.\n      *------------------\n\n           MOVE FUNCTION WHEN-COMPILED \n                                   TO W-COMPILED-DATE\n\n           DISPLAY 'TESTIO2 compiled on '\n               W-COMPILED-DATE-YYYY '/'\n               W-COMPILED-DATE-MM   '/'\n               W-COMPILED-DATE-DD   ' at '\n               W-COMPILED-TIME-HH   ':'\n               W-COMPILED-TIME-MM   ':'\n               W-COMPILED-TIME-SS\n\n           OPEN OUTPUT TESTIO2O\n           .\n       SUB-1000-EXIT.\n           EXIT.\n      /\n       SUB-2000-PROCESS.\n      *-----------------\n\n           MOVE FUNCTION RANDOM    TO W-RANDOM-NO\n           COMPUTE W-RECLEN        =  W-RANDOM-NO\n                                      * (W-RECLEN-MAX\n                                         - W-RECLEN-MIN\n                                         + 1)\n                                      + W-RECLEN-MIN     \n\n      *    DISPLAY 'W-RECLEN = '\n      *            W-RECLEN\n\n           IF      W-RECLEN < W-OUTPUT-MIN\n               MOVE W-RECLEN       TO W-OUTPUT-MIN\n           END-IF\n\n           IF      W-RECLEN > W-OUTPUT-MAX\n               MOVE W-RECLEN       TO W-OUTPUT-MAX\n           END-IF\n\n           PERFORM SUB-2100-POPULATE-REC THRU SUB-2100-EXIT\n\n           PERFORM SUB-9100-WRITE-TESTIO2O THRU SUB-9100-EXIT\n           .\n       SUB-2000-EXIT.\n           EXIT.\n      /\n       SUB-2100-POPULATE-REC.\n      *----------------------\n\n           COMPUTE W-CHAR-BIN      =  FUNCTION RANDOM * 256\n                             \n           PERFORM VARYING TO-DX FROM 1 BY 1 \n                     UNTIL TO-DX > W-RECLEN\n               MOVE W-CHAR         TO TO-CHAR(TO-DX)\n           END-PERFORM\n           .\n       SUB-2100-EXIT.\n           EXIT.\n      /\n       SUB-3000-SHUT-DOWN.\n      *-------------------\n      \n           CLOSE TESTIO2O\n\n           MOVE W-OUTPUT-MIN       TO W-DISP-NUM\n           DISPLAY 'Shortest record length:   '\n                   W-DISP-NUM\n\n           MOVE W-OUTPUT-MAX       TO W-DISP-NUM\n           DISPLAY 'Longest  record length:   '\n                   W-DISP-NUM\n\n           MOVE W-TESTIO2O-RECS    TO W-DISP-NUM\n           DISPLAY 'TESTIO2O records written: '\n                   W-DISP-NUM\n\n           DISPLAY 'TESTIO2 completed'\n           .\n       SUB-3000-EXIT.\n           EXIT.\n      /\n       SUB-9100-WRITE-TESTIO2O.\n      *------------------------\n      \n           WRITE TESTIO2O-REC\n\n           ADD  1                  TO W-TESTIO2O-RECS\n\n           DIVIDE W-TESTIO2O-RECS  BY 10000\n                               GIVING W-QUOTIENT\n                            REMAINDER W-REMAINDER\n\n           IF      W-REMAINDER = 0\n               MOVE W-TESTIO2O-RECS\n                                   TO W-DISP-NUM\n               DISPLAY '    Records written: '\n                       W-DISP-NUM\n           END-IF\n           .\n       SUB-9100-EXIT.\n           EXIT.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": null
}