{
  "cobol_file": {
    "file_name": "successful_snippet_3fd4493b.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AdventOfCode2019-12b.\n\n       ENVIRONMENT DIVISION.\n       CONFIGURATION SECTION.\n       REPOSITORY.\n           FUNCTION ALL INTRINSIC.\n\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n       SELECT InputFile ASSIGN TO InputPath\n       ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n       FILE SECTION.\n       FD InputFile RECORD IS VARYING IN SIZE FROM 1 TO 9999 CHARACTERS.\n      * The input file has been aligned by hand so the parsing is easier,\n      * because I'm lazy.\n       01 InputRec.\n        02 FILLER PIC X(3).\n        02 InputPosX PIC X(3).\n        02 FILLER PIC X(4).\n        02 InputPosY PIC X(3).\n        02 FILLER PIC X(4).\n        02 InputPosZ PIC X(3).\n\n       WORKING-STORAGE SECTION.\n       01 InputPath PIC X(50).\n\n       01 Idx       PIC 9(10).\n       01 Jdx       PIC 9(10).\n\n       01 Moons OCCURS 4 TIMES.\n        02 PosX PIC S9(5).\n        02 PosY PIC S9(5).\n        02 PosZ PIC S9(5).\n        02 VelX PIC S9(5) VALUE 0.\n        02 VelY PIC S9(5) VALUE 0.\n        02 VelZ PIC S9(5) VALUE 0.\n\n       01 OrigSystem.\n        02 FILLER OCCURS 4 TIMES.\n           03 OrigPos PIC S9(5).\n           03 OrigVel PIC S9(5).\n       01 System.\n        02 FILLER OCCURS 4 TIMES.\n         03 Pos PIC S9(5).\n         03 Vel PIC S9(5).\n       01 Steps PIC 9(20).\n       01 Diff PIC S9(1).\n\n       01 Prime PIC 9(20).\n       01 Factors.\n        02 Factor PIC 9(20) OCCURS 99 TIMES.\n       01 FactorsLen PIC 9(2).\n       01 TotalFactors.\n        02 TotalFactor PIC 9(20) OCCURS 99 TIMES.\n       01 TotalFactorsLen PIC 9(2).\n       01 MergedFactors.\n        02 MergedFactor PIC 9(20) OCCURS 99 TIMES.\n       01 MergedFactorsLen PIC 9(2).\n\n       PROCEDURE DIVISION.\n       Begin.\n           ACCEPT InputPath FROM ARGUMENT-VALUE.\n           OPEN INPUT InputFile.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            READ InputFile\n            COMPUTE PosX(Idx) = NUMVAL(InputPosX)\n            COMPUTE PosY(Idx) = NUMVAL(InputPosY)\n            COMPUTE PosZ(Idx) = NUMVAL(InputPosZ)\n           END-PERFORM.\n\n           CLOSE InputFile.\n\n           MOVE 0 TO TotalFactorsLen.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            MOVE PosX(Idx) TO Pos(Idx)\n            MOVE VelX(Idx) TO Vel(Idx)\n           END-PERFORM.\n           PERFORM FindLoop.\n           DISPLAY \"X \" Steps.\n           PERFORM FactorizeLoop.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            MOVE PosY(Idx) TO Pos(Idx)\n            MOVE VelY(Idx) TO Vel(Idx)\n           END-PERFORM.\n           PERFORM FindLoop.\n           DISPLAY \"Y \" Steps.\n           PERFORM FactorizeLoop.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            MOVE PosZ(Idx) TO Pos(Idx)\n            MOVE VelZ(Idx) TO Vel(Idx)\n           END-PERFORM.\n           PERFORM FindLoop.\n           DISPLAY \"Z \" Steps.\n           PERFORM FactorizeLoop.\n\n           MOVE 1 TO Steps.\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > TotalFactorsLen\n            MULTIPLY Steps BY TotalFactor(Idx) GIVING Steps\n           END-PERFORM.\n           DISPLAY Steps.\n\n           STOP RUN.\n\n       FindLoop SECTION.\n           MOVE System TO OrigSystem.\n\n           MOVE 0 TO Steps.\n           PERFORM FOREVER\n            ADD 1 TO Steps\n            PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n             COMPUTE Jdx = Idx + 1\n             PERFORM VARYING Jdx FROM Jdx UNTIL Jdx > 4\n              COMPUTE Diff = SIGN (Pos(Idx) - Pos(Jdx))\n              SUBTRACT Diff FROM Vel(Idx)\n              ADD Diff TO Vel(Jdx)\n             END-PERFORM\n            END-PERFORM\n\n            PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n             COMPUTE Pos(Idx) = Pos(Idx) + Vel(Idx)\n            END-PERFORM\n\n            IF System IS EQUAL TO OrigSystem\n             EXIT PERFORM\n            END-IF\n           END-PERFORM.\n\n       FactorizeLoop SECTION.\n           MOVE 2 TO Prime.\n           MOVE 0 TO FactorsLen.\n\n           PERFORM UNTIL Steps IS EQUAL TO 1\n            IF MOD(Steps, Prime) IS NOT EQUAL TO 0\n             ADD 1 TO Prime\n             EXIT PERFORM CYCLE\n            END-IF\n\n            ADD 1 TO FactorsLen\n            MOVE Prime TO Factor(FactorsLen)\n            DIVIDE Steps BY Prime GIVING Steps\n           END-PERFORM.\n\n           MOVE 1 TO Idx.\n           MOVE 1 TO Jdx.\n           MOVE 0 TO MergedFactorsLen.\n           PERFORM UNTIL Idx > FactorsLen OR Jdx > TotalFactorsLen\n            ADD 1 TO MergedFactorsLen\n            IF Factor(Idx) IS EQUAL TO TotalFactor(Jdx)\n             MOVE Factor(Idx) TO MergedFactor(MergedFactorsLen)\n             ADD 1 TO Idx\n             ADD 1 TO Jdx\n            ELSE IF Factor(Idx) IS LESS THAN TotalFactor(Jdx)\n             MOVE Factor(Idx) TO MergedFactor(MergedFactorsLen)\n             ADD 1 TO Idx\n            ELSE\n             MOVE TotalFactor(Jdx) TO MergedFactor(MergedFactorsLen)\n             ADD 1 TO Jdx\n            END-IF\n           END-PERFORM.\n\n           PERFORM VARYING Idx FROM Idx UNTIL Idx > FactorsLen\n            ADD 1 TO MergedFactorsLen\n            MOVE Factor(Idx) TO MergedFactor(MergedFactorsLen)\n           END-PERFORM.\n\n           PERFORM VARYING Jdx FROM Jdx UNTIL Jdx > TotalFactorsLen\n            ADD 1 TO MergedFactorsLen\n            MOVE TotalFactor(Jdx) TO MergedFactor(MergedFactorsLen)\n           END-PERFORM.\n\n           MOVE MergedFactors TO TotalFactors.\n           MOVE MergedFactorsLen TO TotalFactorsLen.\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "/data/files/input1.txt                                        \n/data/files/input2.txt                                        \n/data/files/input3.txt                                        \n/data/files/input4.txt                                        \n/data/files/input5.txt                                        \n/data/files/input6.txt                                        \n/data/files/input7.txt                                        \n/data/files/input8.txt"
  }
}