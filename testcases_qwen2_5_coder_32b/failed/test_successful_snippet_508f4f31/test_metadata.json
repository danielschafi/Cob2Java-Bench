{
  "cobol_file": {
    "file_name": "successful_snippet_508f4f31.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AdventOfCode2019-12a.\n\n       ENVIRONMENT DIVISION.\n       CONFIGURATION SECTION.\n       REPOSITORY.\n           FUNCTION ALL INTRINSIC.\n\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n       SELECT InputFile ASSIGN TO InputPath\n       ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n       FILE SECTION.\n       FD InputFile RECORD IS VARYING IN SIZE FROM 1 TO 9999 CHARACTERS.\n      * The input file has been aligned by hand so the parsing is easier,\n      * because I'm lazy.\n       01 InputRec.\n        02 FILLER PIC X(3).\n        02 InputPosX PIC X(3).\n        02 FILLER PIC X(4).\n        02 InputPosY PIC X(3).\n        02 FILLER PIC X(4).\n        02 InputPosZ PIC X(3).\n\n\n       WORKING-STORAGE SECTION.\n       01 InputPath PIC X(50).\n\n       01 Idx PIC 9(4).\n       01 Jdx PIC 9(4).\n       01 Kdx PIC 9(4).\n\n       01 Moons OCCURS 4 TIMES.\n        02 PosX PIC S9(4).\n        02 PosY PIC S9(4).\n        02 PosZ PIC S9(4).\n        02 VelX PIC S9(4) VALUE 0.\n        02 VelY PIC S9(4) VALUE 0.\n        02 VelZ PIC S9(4) VALUE 0.\n       01 Diff PIC S9(1).\n       01 Pot PIC 9(4).\n       01 Kin PIC 9(4).\n       01 Energy PIC 9(4).\n       01 TotalEnergy PIC 9(8).\n       01 Steps PIC 9(4).\n\n       PROCEDURE DIVISION.\n       Begin.\n           ACCEPT InputPath FROM ARGUMENT-VALUE.\n           ACCEPT Steps FROM ARGUMENT-VALUE.\n           OPEN INPUT InputFile.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            READ InputFile\n            COMPUTE PosX(Idx) = NUMVAL(InputPosX)\n            COMPUTE PosY(Idx) = NUMVAL(InputPosY)\n            COMPUTE PosZ(Idx) = NUMVAL(InputPosZ)\n           END-PERFORM.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            DISPLAY \"x=\" PosX(Idx) \",y=\" PosY(Idx) \",z=\" PosZ(Idx)\n                  \" vx=\" VelX(Idx) \",vy=\" VelY(Idx) \",vz=\" VelZ(Idx)\n           END-PERFORM\n           DISPLAY \" \"\n\n           PERFORM VARYING Kdx FROM 1 UNTIL Kdx > Steps\n            PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n             COMPUTE Jdx = Idx + 1\n             PERFORM VARYING Jdx FROM Jdx UNTIL Jdx > 4\n              COMPUTE Diff = SIGN (PosX(Idx) - PosX(Jdx))\n              SUBTRACT Diff FROM VelX(Idx)\n              Add Diff TO VelX(Jdx)\n\n              COMPUTE Diff = SIGN (PosY(Idx) - PosY(Jdx))\n              SUBTRACT Diff FROM VelY(Idx)\n              Add Diff TO VelY(Jdx)\n\n              COMPUTE Diff = SIGN (PosZ(Idx) - PosZ(Jdx))\n              SUBTRACT Diff FROM VelZ(Idx)\n              Add Diff TO VelZ(Jdx)\n             END-PERFORM\n            END-PERFORM\n\n            PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n             COMPUTE PosX(Idx) = PosX(Idx) + VelX(Idx)\n             COMPUTE PosY(Idx) = PosY(Idx) + VelY(Idx)\n             COMPUTE PosZ(Idx) = PosZ(Idx) + VelZ(Idx)\n            END-PERFORM\n\n            DISPLAY \"Step \" Kdx\n            PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n             DISPLAY \"x=\" PosX(Idx) \",y=\" PosY(Idx) \",z=\" PosZ(Idx)\n                   \" vx=\" VelX(Idx) \",vy=\" VelY(Idx) \",vz=\" VelZ(Idx)\n            END-PERFORM\n            DISPLAY \" \"\n           END-PERFORM.\n\n           PERFORM VARYING Idx FROM 1 UNTIL Idx > 4\n            COMPUTE Pot = ABS(PosX(Idx))\n                        + ABS(PosY(Idx))\n                        + ABS(PosZ(Idx))\n            COMPUTE Kin = ABS(VelX(Idx))\n                        + ABS(VelY(Idx))\n                        + ABS(VelZ(Idx))\n            COMPUTE Energy = Pot * Kin\n            DISPLAY Pot \" \" Kin \" \" Energy\n            ADD Energy TO TotalEnergy\n           END-PERFORM.\n           DISPLAY TotalEnergy\n\n           CLOSE InputFile.\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "C:\\data\\file1.txt0010\nC:\\data\\file2.txt0025\nC:\\data\\file3.txt0100\nC:\\data\\file4.txt0050\nC:\\data\\file5.txt0075\nC:\\data\\file6.txt0030\nC:\\data\\file7.txt0040\nC:\\data\\file8.txt0060"
  }
}