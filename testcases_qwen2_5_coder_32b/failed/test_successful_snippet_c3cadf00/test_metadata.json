{
  "cobol_file": {
    "file_name": "successful_snippet_c3cadf00.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. \"SSS\".\n      *AUTHOR. ERIC CONRAD.\n      *DESCRIPTION. A program to solve a triangle given the length\n      *    of two sides and the measure of the angle contained by\n      *    them.\n\n       ENVIRONMENT DIVISION.\n       CONFIGURATION SECTION.\n       REPOSITORY.\n           FUNCTION ALL INTRINSIC.\n\n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       77  X                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  Y                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  Z                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  XI                           PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  ZETA                         PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  ANGLE-MEASURE                PIC X VALUE SPACE.\n           88  VALID-MEASURE                VALUES \"r\", \"d\".\n           88  RADIAN-MEASURE               VALUE \"r\".\n           88  DEGREE-MEASURE               VALUE \"d\".\n\n      * Sides\n       77  A                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  B                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  C                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n\n      * Semiperimeter\n       77  S                            PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n      * Angles\n       77  ALPHA                        PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  BETA                         PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  GAMMA                        PIC S9(5)V9(8)\n               SIGN TRAILING SEPARATE CHARACTER.\n       77  XVAL                         PIC Z(5).9(4)-.\n       77  INPUT-PROMPT                 PIC X(72).\n\n       01  OUTPUT-1.\n           02  OLABEL                       PIC X(15)\n                   JUSTIFIED RIGHT.\n           02  AVAL                         PIC Z(10).9(4).\n           02  BVAL                         PIC Z(10).9(4).\n           02  CVAL                         PIC Z(10).9(4).\n           02  OUNITS                       PIC X(15)\n                   JUSTIFIED RIGHT.\n\n       PROCEDURE DIVISION.\n       000-MAIN.\n           MOVE \"Enter the length of side a:\" TO INPUT-PROMPT.\n           PERFORM 100-GET-SIDE.\n           MOVE X TO A.\n\n           MOVE \"Enter the length of side b:\" TO INPUT-PROMPT.\n           PERFORM 100-GET-SIDE.\n           MOVE X TO B.\n\n           MOVE \"Enter the measure of angle C:\" TO INPUT-PROMPT.\n           PERFORM 150-GET-ANGLE.\n           MOVE XI TO GAMMA.\n\n           MOVE A TO X.\n           MOVE B TO Y.\n           MOVE GAMMA TO ZETA.\n           PERFORM 210-LAW-OF-COSINES-SIDE.\n           MOVE Z TO C.\n\n           MOVE B TO X.\n           MOVE C TO Y.\n           MOVE A TO Z.\n           PERFORM 200-LAW-OF-COSINES-ANGLE.\n           MOVE ZETA TO ALPHA.\n\n           MOVE C TO X.\n           MOVE A TO Y.\n           MOVE B TO Z.\n           PERFORM 200-LAW-OF-COSINES-ANGLE.\n           MOVE ZETA TO BETA.\n\n           DISPLAY \"Solution:\".\n\n           MOVE \"Sides:\" TO OLABEL.\n           MOVE A TO AVAL.\n           MOVE B TO BVAL.\n           MOVE C TO CVAL.\n           MOVE SPACES TO OUNITS.\n           DISPLAY OUTPUT-1.\n\n           MOVE \"Angles:\" TO OLABEL.\n           MOVE ALPHA TO AVAL.\n           MOVE BETA TO BVAL.\n           MOVE GAMMA TO CVAL.\n           MOVE \"radians\" TO OUNITS.\n           DISPLAY OUTPUT-1.\n\n           MOVE SPACES TO OLABEL.\n           MOVE ALPHA TO XI.\n           PERFORM 300-TO-DEGREES.\n           MOVE ZETA TO AVAL.\n           MOVE BETA TO XI.\n           PERFORM 300-TO-DEGREES.\n           MOVE ZETA TO BVAL.\n           MOVE GAMMA TO XI.\n           PERFORM 300-TO-DEGREES.\n           MOVE ZETA TO CVAL.\n           MOVE \"degrees\" TO OUNITS.\n           DISPLAY OUTPUT-1.\n\n           GOBACK.\n\n       100-GET-SIDE.\n           DISPLAY INPUT-PROMPT.\n           ACCEPT X.\n           MOVE X TO XVAL.\n           DISPLAY \"  entered: \" XVAL.\n           IF X IS NOT GREATER THAN 0.0 THEN\n               DISPLAY \"  lengths must be positive, try again...\"\n               GO TO 100-GET-SIDE\n           END-IF.\n\n       150-GET-ANGLE.\n           PERFORM 155-GET-MEASURE UNTIL VALID-MEASURE.\n           DISPLAY INPUT-PROMPT.\n           ACCEPT XI.\n           MOVE XI TO XVAL.\n           DISPLAY \"  entered: \" XVAL.\n           IF XI IS NOT GREATER THAN 0.0 THEN\n               DISPLAY \"  angle measure must be positive...\"\n               GO TO 150-GET-ANGLE\n           END-IF.\n           IF RADIAN-MEASURE AND XI IS NOT LESS THAN PI THEN\n               DISPLAY \"  angles must be less than straight...\"\n               GO TO 150-GET-ANGLE\n           END-IF.\n           IF DEGREE-MEASURE AND XI IS NOT LESS THAN 180 THEN\n               DISPLAY \"  angles must be less than straight...\"\n               GO TO 150-GET-ANGLE\n           END-IF.\n           IF DEGREE-MEASURE THEN\n               COMPUTE XI ROUNDED = XI * PI / 180.\n\n       155-GET-MEASURE.\n           DISPLAY \"Enter units for angle measure \"\n               \"(r-radians, d-degrees):\".\n           ACCEPT ANGLE-MEASURE.\n           IF NOT VALID-MEASURE THEN\n               DISPLAY \"ERROR: Valid responses are 'r' and 'd'\".\n\n       200-LAW-OF-COSINES-ANGLE.\n      *    This computes an angle given three sides\n           COMPUTE ZETA ROUNDED =\n               ACOS((X * X + Y * Y - Z * Z) / (2 * X * Y)).\n\n       210-LAW-OF-COSINES-SIDE.\n      *    This computes a side given an angle and the sides\n      *    that enclose it.\n           COMPUTE Z ROUNDED =\n               SQRT(X * X + Y * Y - 2 * X * Y * COS(ZETA)).\n\n       250-LAW-OF-SINES.\n      *    Given two angles (xi and zeta) and the side opposite\n      *    angle xi (i.e. side x), find side z.  The law of sines\n      *    can also be used to find an angle, but special care\n      *    must be taken as there may be no solution, a unique\n      *    solution, a double solution (specifically a right angle),\n      *    or two solutions (one acute, one obtuse).\n           COMPUTE Z ROUNDED = X * SIN(ZETA) / SIN(Z).\n\n       300-TO-DEGREES.\n           COMPUTE ZETA ROUNDED = 180 * XI / PI.\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "00001000000000000000\n00002000000000000000\n00003000000000000000\n00004000000000000000\n00005000000000000000\n00006000000000000000\n00007000000000000000\n00008000000000000000\nr\nd\nr\nd\nr\nd\nr\nd"
  }
}