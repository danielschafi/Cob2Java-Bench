{
  "cobol_file": {
    "file_name": "successful_snippet_fa3e68dd.cob",
    "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. AdventOfCode2019-3a.\n\n       ENVIRONMENT DIVISION.\n       INPUT-OUTPUT SECTION.\n       FILE-CONTROL.\n       SELECT InputFile ASSIGN USING InputPath\n       ORGANIZATION IS LINE SEQUENTIAL.\n\n       DATA DIVISION.\n       FILE SECTION.\n       FD InputFile RECORD IS VARYING IN SIZE FROM 1 TO 9999 CHARACTERS.\n       01 InputRec  PIC X(9999).\n\n       WORKING-STORAGE SECTION.\n       01 InputPath           PIC X(50).\n       01 Chunk.\n          02 Dir              PIC X(1).\n          02 RawDist          PIC X(10).\n       01 Ptr                 PIC 9(5).\n       01 Seps                PIC 9(5).\n       01 Dist                PIC 9(10).\n       01 Abs                 PIC S9(10).\n       01 Ord                 PIC S9(10).\n       01 Len                 PIC 9(5).\n       01 FILLER              OCCURS 0 TO 9999 DEPENDING ON Len.\n          02 PosAbs           PIC S9(10).\n          02 PosOrd           PIC S9(10).\n       01 PrevAbs             PIC S9(10).\n       01 PrevOrd             PIC S9(10).\n       01 Idx                 PIC 9(5).\n       01 PrevIdx             PIC 9(5).\n       01 CrossAbs            PIC S9(10).\n       01 CrossOrd            PIC S9(10).\n       01 Manhattan           PIC S9(10).\n       01 BestManhattan       PIC S9(10).\n       01 BestManhattanFmt    PIC Z(10).\n\n\n\n       PROCEDURE DIVISION.\n       Begin SECTION.\n           ACCEPT InputPath FROM ARGUMENT-VALUE.\n           OPEN INPUT InputFile.\n\n      * Read the first line.\n           READ InputFile.\n           PERFORM CountSeps.\n\n           MOVE 0 TO Abs.\n           MOVE 0 TO Ord.\n           MOVE 1 TO Ptr.\n           MOVE 0 TO PosAbs(1).\n           MOVE 0 TO PosOrd(1).\n           MOVE 1 TO Len.\n           PERFORM Seps TIMES\n            PERFORM ReadRec\n            ADD 1 TO Len\n            MOVE Abs TO PosAbs(Len)\n            MOVE Ord TO PosOrd(Len)\n           END-PERFORM.\n\n      * Read the second line.\n           READ InputFile.\n           PERFORM CountSeps.\n\n           MOVE 0 TO Abs.\n           MOVE 0 TO Ord.\n           MOVE 0 TO PrevAbs.\n           MOVE 0 TO PrevOrd.\n           MOVE 1 TO Ptr.\n           MOVE HIGH-VALUE TO BestManhattan.\n\n           PERFORM Seps TIMES\n            MOVE Abs TO PrevAbs\n            MOVE Ord TO PrevOrd\n            PERFORM ReadRec\n\n            MOVE 1 TO PrevIdx\n            PERFORM VARYING Idx FROM 2 UNTIL Idx IS GREATER THAN Len\n             IF PrevAbs = Abs AND PosOrd(PrevIdx) = PosOrd(Idx)\n              IF (\n               (PosAbs(PrevIdx) <= Abs AND Abs <= PosAbs(Idx))\n               OR (PosAbs(Idx) <= Abs AND Abs <= PosAbs(PrevIdx))\n              ) AND (\n               (PrevOrd <= PosOrd(Idx) AND PosOrd(Idx) <= Ord)\n               OR (Ord <= PosOrd(Idx) AND PosOrd(Idx) <= PrevOrd)\n              )\n               MOVE Abs TO CrossAbs\n               MOVE PosOrd(Idx) TO CrossOrd\n               PERFORM EvaluateManhattan\n              END-IF\n             ELSE IF PrevOrd = Ord AND PosAbs(PrevIdx) = PosAbs(Idx)\n              IF (\n               (PosOrd(PrevIdx) <= Ord AND Ord <= PosOrd(Idx))\n               OR (PosOrd(Idx) <= Ord AND Ord <= PosOrd(PrevIdx))\n              ) AND (\n               (PrevAbs <= PosAbs(Idx) AND PosAbs(Idx) <= Abs)\n               OR (Abs <= PosAbs(Idx) AND PosAbs(Idx) <= PrevAbs)\n              )\n               MOVE PosAbs(Idx) TO CrossAbs\n               MOVE Ord TO CrossOrd\n               PERFORM EvaluateManhattan\n              END-IF\n             END-IF\n             MOVE Idx TO PrevIdx\n            END-PERFORM\n           END-PERFORM.\n           MOVE BestManhattan TO BestManhattanFmt.\n           DISPLAY BestManhattanFmt.\n\n           CLOSE InputFile.\n           STOP RUN.\n\n      * Subroutine CountSeps.\n      *\n      * Count the number of sections in the input record.\n      *\n      * Uses: InputRec, Seps\n       CountSeps SECTION.\n           MOVE 0 TO Seps.\n           INSPECT InputRec TALLYING Seps FOR ALL \",\".\n           IF Seps < 9999\n            ADD 1 TO Seps\n           ELSE\n            MOVE 9999 TO Seps\n           END-IF.\n\n      * Subroutine ReadRec.\n      *\n      * Read a section of the input record, parsing the direction and\n      * distance and resulting into the Abs and Ord values being\n      * updated.\n      *\n      * Uses: InputRec, Chunk, Ptr, Dist, Dir, Ord, Abs\n       ReadRec SECTION.\n           UNSTRING InputRec DELIMITED BY \",\" INTO Chunk\n            WITH POINTER Ptr\n           END-UNSTRING.\n           COMPUTE Dist = FUNCTION NUMVAL(RawDist).\n           EVALUATE Dir\n            WHEN \"U\"\n             COMPUTE Ord = Ord + Dist\n            WHEN \"D\"\n             COMPUTE Ord = Ord - Dist\n            WHEN \"R\"\n             COMPUTE Abs = Abs + Dist\n            WHEN \"L\"\n             COMPUTE Abs = Abs - Dist\n           END-EVALUATE.\n\n      * Subroutine EvaluateManhattan.\n      *\n      * Calculate the Manhattan distance for the current Cross values,\n      * and keep the minimal one in the result variable.\n      *\n      * Uses: CrossAbs, CrossOrd, Manhattan, BestManhattan\n       EvaluateManhattan SECTION.\n           MOVE FUNCTION ABS(CrossAbs) TO Manhattan.\n           ADD FUNCTION ABS(CrossOrd) TO Manhattan.\n           IF Manhattan NOT EQUAL TO 0\n           AND Manhattan IS LESS THAN BestManhattan\n            MOVE Manhattan TO BestManhattan\n           END-IF.\n\n"
  },
  "input_files": [],
  "output_files": [],
  "input_output_files": [],
  "sysin_file": {
    "file_name": "sysin.txt",
    "content": "/path/to/inputfile1.txt                                        \n/path/to/inputfile2.txt                                        \n/path/to/inputfile3.txt                                        \n/path/to/inputfile4.txt                                        \n/path/to/inputfile5.txt                                        \n/path/to/inputfile6.txt                                        \n/path/to/inputfile7.txt                                        \n/path/to/inputfile8.txt"
  }
}