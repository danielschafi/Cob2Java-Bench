{
    "cobol_file": {
        "file_name": "successful_snippet_6d0a4c82.cob",
        "content": "       IDENTIFICATION DIVISION.\n       PROGRAM-ID. PRIME-FIBONACCI.\n       \n       DATA DIVISION.\n       WORKING-STORAGE SECTION.\n       01 FIBONACCI-VARS.\n          03 FIB                PIC 9(6).\n          03 FIB-B              PIC 9(6).\n          03 FIB-C              PIC 9(6).\n          03 FIB-OUT            PIC Z(5)9.\n       01 PRIME-VARS.   \n          03 PRIME-FLAG         PIC X.\n             88 PRIME           VALUE 'X'.\n          03 DSOR               PIC 9(4).\n          03 DSOR-SQ            PIC 9(6).\n          03 DIV-RSLT           PIC 9(6)V9(3).\n          03 FILLER             REDEFINES DIV-RSLT.\n             05 FILLER          PIC 9(6).\n             05 FILLER          PIC 9(3).\n                88 DIVISIBLE    VALUE ZERO.\n          \n       PROCEDURE DIVISION.\n       BEGIN.\n           MOVE 1 TO FIB, FIB-B.\n           PERFORM FIND-PRIME-FIBONACCI 9 TIMES.\n           STOP RUN.\n       \n       FIND-PRIME-FIBONACCI.\n           ADD FIB, FIB-B GIVING FIB-C.\n           MOVE FIB-B TO FIB.\n           MOVE FIB-C TO FIB-B.\n           PERFORM CHECK-PRIME.\n           IF NOT PRIME, GO TO FIND-PRIME-FIBONACCI.\n           MOVE FIB TO FIB-OUT.\n           DISPLAY FIB-OUT.\n       \n       CHECK-PRIME SECTION.\n       BEGIN.\n           MOVE SPACE TO PRIME-FLAG.\n           IF FIB IS LESS THAN 5, GO TO TRIVIAL-PRIME.\n           DIVIDE FIB BY 2 GIVING DIV-RSLT.\n           IF DIVISIBLE, GO TO DONE.\n           DIVIDE FIB BY 3 GIVING DIV-RSLT.\n           IF DIVISIBLE, GO TO DONE.\n           MOVE 5 TO DSOR.\n           MOVE 25 TO DSOR-SQ.\n           MOVE 'X' TO PRIME-FLAG.\n           PERFORM TEST-DIVISOR \n           UNTIL NOT PRIME OR DSOR-SQ IS GREATER THAN FIB.\n           GO TO DONE.\n           \n       TEST-DIVISOR.\n           DIVIDE FIB BY DSOR GIVING DIV-RSLT.\n           IF DIVISIBLE, MOVE SPACE TO PRIME-FLAG.\n           ADD 2 TO DSOR.\n           DIVIDE FIB BY DSOR GIVING DIV-RSLT.\n           IF DIVISIBLE, MOVE SPACE TO PRIME-FLAG.\n           ADD 4 TO DSOR.\n           MULTIPLY DSOR BY DSOR GIVING DSOR-SQ.\n       \n       TRIVIAL-PRIME.\n           IF FIB IS EQUAL TO 2 OR 3, MOVE 'X' TO PRIME-FLAG.\n       DONE.\n           EXIT.\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": null,
    "code_file": "successful_snippet_6d0a4c82.cob",
    "compiled_file": "successful_snippet_6d0a4c82",
    "expected_output": "     2\n     3\n     5\n    13\n    89\n   233\n  1597\n 28657\n514229\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}