{
    "cobol_file": {
        "file_name": "successful_snippet_cb687fb3.cob",
        "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs06a.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(1024).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs06a'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(009) COMP    VALUE 0.\n           05  DAY-COUNT          PIC 9(009) COMP    VALUE 0.\n           05  NB-DAYS            PIC 9(009) COMP    VALUE 0.\n           05  MAX-FISH           PIC 9(009) COMP    VALUE 1000000.\n           05  NB-FISH            PIC 9(009) COMP    VALUE 0.\n           05  UNSTRING-PTR       PIC 9(009) COMP    VALUE 1.\n           05  CLI-ARGS           PIC X(080)         VALUE LOW-VALUES.\n           05  FISH-X             PIC X(001)         VALUE LOW-VALUES.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n           05  NB-DAYS-X          PIC X(004)         VALUE LOW-VALUES.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT            PIC X(1024)        VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n\n       01  FISH-TABLE.\n           05  FISH-TBL\n               OCCURS 1000000\n               INDEXED FISH-INDX.\n               10  FISH           PIC S9(004) COMP.\n               10  NEW-FISH-SW    PIC X(001).\n                   88  NEW-FISH                      VALUE 'Y'\n                                                     FALSE 'N'.\n\n       Procedure Division.\n           ACCEPT CLI-ARGS FROM COMMAND-LINE\n           UNSTRING CLI-ARGS DELIMITED SPACE OR LOW-VALUE\n             INTO PROCESS-TYPE NB-DAYS-X\n           END-UNSTRING\n\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           MOVE FUNCTION NUMVAL(NB-DAYS-X) TO NB-DAYS\n\n           DISPLAY MYNAME ' number of days ' NB-DAYS\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           INITIALIZE FISH-TABLE\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM 1000-LOAD-INPUT UNTIL FISH-X = SPACES\n\n           CLOSE INPT-DATA\n\n           DISPLAY MYNAME ' initial number of fish ' NB-FISH\n\n           PERFORM 2000-PROCESS-ONE-DAY\n             VARYING DAY-COUNT FROM 1 BY 1\n             UNTIL DAY-COUNT > NB-DAYS\n\n           SUBTRACT 1 FROM DAY-COUNT\n           DISPLAY MYNAME ' number of fish ' NB-FISH\n             ' after ' DAY-COUNT ' days'\n           DISPLAY MYNAME ' records read ' WS-REC-COUNT\n\n           GOBACK.\n\n       1000-LOAD-INPUT.\n           MOVE SPACES TO FISH-X\n           UNSTRING WS-INPT\n             DELIMITED ','\n             INTO FISH-X\n             POINTER UNSTRING-PTR\n           END-UNSTRING\n\n           IF FISH-X NOT = SPACES\n               ADD 1 TO NB-FISH\n               SET FISH-INDX TO NB-FISH\n               MOVE FUNCTION NUMVAL(FISH-X) TO FISH(FISH-INDX)\n               SET NEW-FISH(FISH-INDX) TO FALSE\n           END-IF\n           .\n\n       2000-PROCESS-ONE-DAY.\n           IF PROCESS-TEST\n               RESET TRACE\n           END-IF          \n\n           PERFORM VARYING FISH-INDX FROM 1 BY 1\n           UNTIL FISH-INDX > NB-FISH\n             EVALUATE FISH(FISH-INDX)\n               WHEN 0\n                    PERFORM 2010-RESET-FISH\n               WHEN OTHER\n                    IF NEW-FISH(FISH-INDX)\n                        SET NEW-FISH(FISH-INDX) TO FALSE\n                    ELSE\n                        SUBTRACT 1 FROM FISH(FISH-INDX)\n                    END-IF\n             END-EVALUATE\n           END-PERFORM\n\n           IF PROCESS-TEST\n               DISPLAY MYNAME ' day ' DAY-COUNT SPACE WITH NO ADVANCING\n               PERFORM VARYING FISH-INDX FROM 1 BY 1\n               UNTIL FISH-INDX > NB-FISH\n                 DISPLAY FISH(FISH-INDX) ',' WITH NO ADVANCING\n               END-PERFORM\n               DISPLAY SPACE\n               READY TRACE\n           END-IF          \n           .\n\n       2010-RESET-FISH.\n           MOVE 6 TO FISH(FISH-INDX)\n           ADD 1 TO NB-FISH\n           IF NB-FISH > MAX-FISH\n               DISPLAY\n                 MYNAME\n                 ' internal fish table overflow on day '\n                 DAY-COUNT\n               MOVE 8 TO RETURN-CODE\n               GOBACK\n           END-IF\n           MOVE 8 TO FISH(NB-FISH)\n           SET NEW-FISH(NB-FISH) TO TRUE\n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n           END-READ\n\n           .\n\n\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": {
        "file_name": "sysin.txt",
        "content": "PROCESS-TYPE1 NB-DAYS-X005\nPROCESS-TYPE2 NB-DAYS-X010\nPROCESS-TYPE3 NB-DAYS-X015\nPROCESS-TYPE4 NB-DAYS-X020\nPROCESS-TYPE5 NB-DAYS-X025\nPROCESS-TYPE6 NB-DAYS-X030\nPROCESS-TYPE7 NB-DAYS-X035\nPROCESS-TYPE8 NB-DAYS-X040"
    },
    "code_file": "successful_snippet_cb687fb3.cob",
    "compiled_file": "successful_snippet_cb687fb3",
    "expected_output": "cs06a    number of days 000000000\ncs06a    initial number of fish 000000001\ncs06a    number of fish 000000001 after 000000000 days\ncs06a    records read 000000001\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}