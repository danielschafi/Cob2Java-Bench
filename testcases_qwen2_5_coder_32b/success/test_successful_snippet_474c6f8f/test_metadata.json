{
    "cobol_file": {
        "file_name": "successful_snippet_474c6f8f.cob",
        "content": "       ID Division.\n      * \n      * Copyright (C) 2021 Craig Schneiderwent.  All rights reserved.\n      * \n      * I accept no liability for damages of any kind resulting \n      * from the use of this software.  Use at your own risk.\n      *\n      * This software may be modified and distributed under the terms\n      * of the MIT license. See the LICENSE file for details.\n      *\n      *\n       Program-ID. cs05a.\n       Environment Division.\n       Input-Output Section.\n       File-Control.\n           Select INPT-DATA            Assign Keyboard.\n       Data Division.\n       File Section.\n       FD  INPT-DATA.\n       01  INPT-DATA-REC-MAX PIC X(024).\n\n       Working-Storage Section.\n       01  CONSTANTS.\n           05  MYNAME             PIC X(008) VALUE 'cs05a'.\n\n       01  WORK-AREAS.\n           05  WS-REC-COUNT       PIC 9(008) COMP    VALUE 0.\n           05  X1                 PIC 9(009) COMP    VALUE 0.\n           05  Y1                 PIC 9(009) COMP    VALUE 0.\n           05  X2                 PIC 9(009) COMP    VALUE 0.\n           05  Y2                 PIC 9(009) COMP    VALUE 0.\n           05  OVERLAP-COUNT      PIC 9(008) COMP    VALUE 0.\n           05  CURR-AIM           PIC 9(008) COMP    VALUE 0.\n           05  CURR-PRODUCT       PIC 9(016) COMP    VALUE 0.\n           05  OPERATION-ARG      PIC 9(008) COMP    VALUE 0.\n           05  PROCESS-TYPE       PIC X(004)         VALUE LOW-VALUES.\n           05  COORD-1            PIC X(008)         VALUE SPACES.\n           05  COORD-2            PIC X(008)         VALUE SPACES.\n           05  COORD-X1           PIC X(004)         VALUE SPACES.\n           05  COORD-Y1           PIC X(004)         VALUE SPACES.\n           05  COORD-X2           PIC X(004)         VALUE SPACES.\n           05  COORD-Y2           PIC X(004)         VALUE SPACES.\n\n       01  WS-INPT-DATA.\n           05  WS-INPT            PIC X(024)         VALUE SPACES.\n\n       01  SWITCHES.\n           05  INPT-DATA-EOF-SW   PIC X(001)         VALUE 'N'.\n               88  INPT-DATA-EOF                     VALUE 'Y'.\n           05  PROCESS-SW         PIC X(004)         VALUE LOW-VALUES.\n               88  PROCESS-TEST                      VALUE 'TEST'.\n\n       01  THE-OCEAN-FLOOR.\n           05  Y-VAL OCCURS 1000 INDEXED Y-INDX1 Y-INDX2.\n               10  X-VAL OCCURS 1000 INDEXED X-INDX1 X-INDX2.\n                   15  POINT      PIC 9(004).\n\n       Procedure Division.\n           ACCEPT PROCESS-TYPE FROM COMMAND-LINE\n           MOVE FUNCTION UPPER-CASE(PROCESS-TYPE)\n             TO PROCESS-SW\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF\n\n           INITIALIZE THE-OCEAN-FLOOR\n\n           OPEN INPUT INPT-DATA\n\n           PERFORM 8010-READ-INPT-DATA\n\n           PERFORM 1000-LOAD-INPUT\n             UNTIL INPT-DATA-EOF\n\n           CLOSE INPT-DATA\n\n           PERFORM 9010-DUMP-THE-OCEAN-FLOOR\n\n           PERFORM 2000-COUNT-INTERSECTIONS\n\n           DISPLAY MYNAME ' overlap count ' OVERLAP-COUNT\n           DISPLAY MYNAME ' records read ' WS-REC-COUNT\n\n           GOBACK.\n\n       1000-LOAD-INPUT.\n           UNSTRING WS-INPT\n             DELIMITED '->'\n             INTO COORD-1 COORD-2\n           END-UNSTRING\n           UNSTRING COORD-1\n             DELIMITED ','\n             INTO COORD-X1 COORD-Y1\n           END-UNSTRING\n           UNSTRING COORD-2\n             DELIMITED ','\n             INTO COORD-X2 COORD-Y2\n           END-UNSTRING\n\n           COMPUTE X1 = FUNCTION NUMVAL(COORD-X1) + 1\n           COMPUTE Y1 = FUNCTION NUMVAL(COORD-Y1) + 1\n           COMPUTE X2 = FUNCTION NUMVAL(COORD-X2) + 1\n           COMPUTE Y2 = FUNCTION NUMVAL(COORD-Y2) + 1\n\n           EVALUATE TRUE ALSO TRUE\n             WHEN X1 = X2 ALSO Y1 < Y2\n                  SET X-INDX1 TO X1\n                  PERFORM 1100-PLOT-THE-LINE\n                    VARYING Y-INDX1 FROM Y1 BY 1\n                    UNTIL Y-INDX1 > Y2\n             WHEN X1 = X2 ALSO Y1 > Y2\n                  SET X-INDX1 TO X1\n                  PERFORM 1100-PLOT-THE-LINE\n                    VARYING Y-INDX1 FROM Y2 BY 1\n                    UNTIL Y-INDX1 > Y1\n             WHEN Y1 = Y2 ALSO X1 < X2\n                  SET Y-INDX1 TO Y1\n                  PERFORM 1100-PLOT-THE-LINE\n                    VARYING X-INDX1 FROM X1 BY 1\n                    UNTIL X-INDX1 > X2\n             WHEN Y1 = Y2 ALSO X1 > X2\n                  SET Y-INDX1 TO Y1\n                  PERFORM 1100-PLOT-THE-LINE\n                    VARYING X-INDX1 FROM X2 BY 1\n                    UNTIL X-INDX1 > X1\n             WHEN OTHER\n                  DISPLAY MYNAME ' ignoring ' WS-INPT\n           END-EVALUATE\n\n           PERFORM 8010-READ-INPT-DATA\n           .\n\n       1100-PLOT-THE-LINE.\n           ADD 1 TO POINT(Y-INDX1,X-INDX1)\n           .\n\n       2000-COUNT-INTERSECTIONS.\n           IF PROCESS-TEST\n               RESET TRACE\n           END-IF          \n\n           PERFORM VARYING Y-INDX1 FROM 1 BY 1 UNTIL Y-INDX1 > 1000\n           AFTER X-INDX1 FROM 1 BY 1 UNTIL X-INDX1 > 1000\n             IF POINT(Y-INDX1,X-INDX1) > 1\n                 ADD 1 TO OVERLAP-COUNT\n             END-IF\n           END-PERFORM\n\n           IF PROCESS-TEST\n               READY TRACE\n           END-IF          \n           .\n\n       8010-READ-INPT-DATA.\n           INITIALIZE WS-INPT-DATA\n           READ INPT-DATA INTO WS-INPT-DATA\n             AT END SET INPT-DATA-EOF TO TRUE\n             NOT AT END\n               ADD 1 TO WS-REC-COUNT\n           END-READ\n\n           .\n\n       9010-DUMP-THE-OCEAN-FLOOR.\n           IF PROCESS-TEST\n               RESET TRACE\n               PERFORM VARYING Y-INDX1 FROM 1 BY 1\n               UNTIL Y-INDX1 > 10\n                 DISPLAY MYNAME SPACE WITH NO ADVANCING\n                 PERFORM VARYING X-INDX1 FROM 1 BY 1\n                 UNTIL X-INDX1 > 10\n                   DISPLAY POINT(Y-INDX1,X-INDX1) SPACE\n                     WITH NO ADVANCING\n                 END-PERFORM\n                 DISPLAY SPACE\n               END-PERFORM\n               READY TRACE\n           END-IF\n           .\n\n"
    },
    "input_files": [],
    "output_files": [],
    "input_output_files": [],
    "sysin_file": {
        "file_name": "sysin.txt",
        "content": "LOAD  \nBACKUP  \nREPORT  \nVALIDATE  \nUPDATE  \nDELETE  \nCREATE  \nEXIT"
    },
    "code_file": "successful_snippet_474c6f8f.cob",
    "compiled_file": "successful_snippet_474c6f8f",
    "expected_output": "cs05a    ignoring LOAD                    \ncs05a    ignoring BACKUP                  \ncs05a    ignoring REPORT                  \ncs05a    ignoring VALIDATE                \ncs05a    ignoring UPDATE                  \ncs05a    ignoring DELETE                  \ncs05a    ignoring CREATE                  \ncs05a    ignoring EXIT                    \ncs05a    overlap count 00000000\ncs05a    records read 00000008\n",
    "expected_output_files": [],
    "expected_input_output_files": []
}